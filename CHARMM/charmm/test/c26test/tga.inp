* CHARMM Testcase c26test/tga.inp
* Author: Charles L. Brooks, III
* Date  : January 20, 1998
* Model : N-methyl alanyl acetamide
* Files : toph19.rtf and param19.prm
* Test  : build NMA using internal coordinates and then manipulates the
*         structure based upon the p/y dihedral ics
*         It has been modified to explore the use of the replica command
*         for copying multiple structures to explore conformations,
*         e.g., in GAs
*

!  THIS EXAMPLE PERFORMS EVOLUTIONARY STRATEGY ON 100 CHROMOSOMES OF
!  fixed bond/angle/improper N-methyl alanyl acetamide
!  with convergence of the population limiting the evolution
!  2 niches are present
!

if ?genetic .ne. 1 then
 echo "Test NOT performed."
 echo tga testcase result: skip
 stop
endif

if ?numnode .ne. 1 then
 echo "Test NOT performed in parallel."
 echo tga testcase result: skip
 stop
endif

stream datadir.def

open read form unit 11 name @0toph19.rtf
read rtf card unit 11
close unit 11
open read form unit 11 name @0param19.prm
read param card unit 11
close unit 11

! Read in the sequence and generate the psf (protein structure file - an
! internal CHARMM data structure describing the specific connectivity for
! the specific structure of interest.  It is created using the information
! from the topology and parameter files
Read sequ card
* maa
*
3
AMN ALA CBX

Generate maa setup !the segment identifier is called maa and the setup
                   !command indicates that the internal coordinate tables
                   !(based upon ic information in the rtf) will be created

ic parameters  !fill the current ic table based upon values from the
               !parameter and/or rtf files

ic seed 1 cl 1 c 1 o  !provide the three atoms to "seed" building

ic build  !build the structure based upon the internal coordinates (ics)

print ic
print coordinates
print ic
coor orie  !orient molecule so CM is at origin and principal axes are along
           !cartesian directions


set npar 100
GAlgorithm SETUp -
   CHROmosomes @npar select segid maa end -
   VARIable_ICs
    DIHEdral INCLude  1 C     2 N     2 CA    2 C   end
    DIHEdral INCLude  2 N     2 CA    2 C     3 N   end
   END   


ic parameters  !fill the current ic table based upon values from the
               !parameter and/or rtf files
Nbonds cdie eps 1.0 cutnb 99.0 ctofnb 90.0 wrnmxd 99.0 swit vswit
energy

set e0 ?ener

GALGorithm EVOLve -
RAND iseed 111 rdihe 180.0 - !rdihe 180.0 -
  MAXGenerations 100 -
  niches 2 inte 10 -
  gsize 1 muta 0.5 cross 0.5  pinte 1.0  -  
  print 10 nprint 14 Dist 30.0 anst 1.0 toler 0.01 delete leave 10 clear

set e0 ?ener

set npar 10
ic fill
print ic

@testcheck @e0 510.140602 0.1 tga

stop
