* CHARMM Testcase c26test/tgall.inp
* Author: Charles L. Brooks, III
* Date  : January 20, 1998
* Model : N-methyl alanyl acetamide
* Files : toph19.rtf and param19.prm
* Test  : build NMA using internal coordinates and then manipulates the
*         structure based upon the p/y dihedral ics
*         It has been modified to explore the use of the replica command
*         for copying multiple structures to explore conformations,
*         e.g., in GAs
*

!  THIS EXAMPLE PERFORMS GENERATIONAL UPDATE GA ON 30 CHROMOSOMES OF 
!  FLEXIBLE BOND/ANGLE/IMPROPER N-methyl alanyl acetamide

if ?genetic .ne. 1 then
 echo tgall testcase result: skip
 echo "Test NOT performed."
 stop
endif

if ?numnode .ne. 1 then
 echo tgall testcase result: skip
 echo "Test NOT performed in parallel."
 stop
endif

stream datadir.def

open read form unit 11 name @0toph19.rtf
read rtf card unit 11
close unit 11
open read form unit 11 name @0param19.prm
read param card unit 11
close unit 11

! Read in the sequence and generate the psf (protein structure file - an
! internal CHARMM data structure describing the specific connectivity for
! the specific structure of interest.  It is created using the information
! from the topology and parameter files
Read sequ card
* maa
*
3
AMN ALA CBX

Generate maa setup !the segment identifier is called maa and the setup
                   !command indicates that the internal coordinate tables
                   !(based upon ic information in the rtf) will be created

ic parameters  !fill the current ic table based upon values from the
               !parameter and/or rtf files
 

ic seed 1 cl 1 c 1 o  !provide the three atoms to "seed" building
ic build  !build the structure based upon the internal coordinates (ics)


ic fill
print ic
print coordinates
Nbonds cdie eps 1.0 cutnb 99.0 ctofnb 90.0 wrnmxd 99.0 swit vswit
energy
coor orie  !orient molecule so CM is at origin and principal axes are along
           !cartesian directions

set npar 10
GAlgorithm SETUp -
   CHROmosomes @npar select segid maa end -
   VARIable_ICs
   DIHEdral INCLude  1 C     2 N     2 CA    2 C   end
   DIHEdral INCLude  2 N     2 CA    2 C     3 N   end
!
! improper dihedrals
  DIHEdral Improp  2 N     1 CL    1 C    1 O   end
  DIHEdral Improp  1 C     2 CA    2 N    2 H   end
  DIHEdral Improp  3 N     2 CA    2 C    2 O   end
  DIHEdral Improp  2 N     2 C     2 CA   2 CB   end
  DIHEdral Improp  2 C     3 CA    3 N    3 H   end
     DIHEdral improp  2 CA    2 C     3 N     3 CA  end 
     DIHEdral Improp  1 CL    1 C     2 N     2 CA  end
   BOND ALL
   ANGLE ALL
   END   

ic random ! randomization of dihedrals
print ic
print coor
Nbonds cdie eps 1.0 cutnb 99.0 ctofnb 90.0 wrnmxd 99.0 swit vswit inbfrq 100 
energy
print ic

!prnlev 3 
GALGorithm EVOLve -
RAND rdihe 360.0  iseed 111 -
  MAXGenerations 1000 elite 1 epres 1.2 -
  steady -
  niches 1 inte 50  nbfrq 10 -
  gsize 1 muta 0.4 cros 0.6  pinte 1.0 conv tolc 0.01  -  
  print 200 Dist 20.0 iprop 3.0  anst 1.5  bost  0.002  clear  delete

print ic

@testcheck ?ener 47499.8455 0.1 tgall


stop
