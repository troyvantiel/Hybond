* peptide in water, 100 steps of dynamics
* PBC in (29A)**3 cubic box 
* test of the coor anal solvent 3D histogram facility
*
stream datadir.def
open unit 1 read form name @0toph19.rtf
read rtf card unit 1
close unit 1

open unit 1 read form name @0param19.prm
read param card unit 1
close unit 1

read sequence card
* ypgdv
*
5
tyr pro gly asp val

generate pept setup

read sequence tip3 756

generate wat noangles nodihedrals

open unit 1 read form name @0turnwat14.chr
read coor card unit 1
close unit 1

set 6 29.0
set 7 29.0
set 8 29.0

open unit 1 read form name @0cubic.img
read image card unit 1
close unit 1

image byres xcen 0.0 ycen 0.0 zcen 0.0 sele segid wat end
image byseg xcen 0.0 ycen 0.0 zcen 0.0 sele segid pept end

faster on 

! to simplify things keep peptide fairly rigid
cons harm force 10.0 sele type ca end
shake para bonh tol 1.0e-8 
open unit 21 write unform name @9pept100.cor
dyna leap verlet strt nstep 100 timestep 0.002 -
 firstt 295.0 finalt 295.0 twindh 10.0 twindl -10.0 -
 iasors 1 iasvel 1 ichecw 1 ieqfrq 0 -
 iunr -1 iunw -1 iunc 21 iunv -1 kuni -1 - 
 inbfrq 25 imgfrq 25 nsavc 5 nsavv 0 nprint 25 iprfrq 25 -
 atom cdie fshif vdis vshif  ctofnb 9.0 cutnb 10.0 cutim 10.0

open unit 21 read unform name @9pept100.cor
open unit 31 write unform name @9pept100_hist.dat
open unit 32 write form name @9pept100_hist.pdb

coor anal  solvent species select type oh2 end fini - 
           firstu 21 nunit 1 skip 20 ihist 31 ipdb 32 XMIN -10.0 XMAX 10.0 DX 1.0 -
           threshold 0.8
stop
! following does not work yet (June 98)
open unit 21 read unform name @9pept100.cor
open unit 31 write unform name @9pept100_dipo.dat
open unit 32 write form name @9pept100_dipo.pdb
coor anal  solvent species select type oh2 end fini - 
           firstu 21 nunit 1 skip 20 ihist 31 ipdb 32 XMIN -10.0 XMAX 10.0 DX 1.0 -
           threshold 0.8 dipo

stop
