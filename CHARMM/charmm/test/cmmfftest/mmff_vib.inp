* mmff_vib.inp
* author:  Ryszard Czerminski (Sep 30, 1993)
* purpose: to test VIBRAN facility working with MMFF
* external files: MMFF*.PAR files
* UPDATED by Josh Buckner 18 April 2019
*

if ?numnode gt 1 then
  echo "mmff not compatible with parallel runs"
  echo "testcase result: skip"
  stop
endif

set tol = 1e-3
set pass = 1
set eref = 1.62354
set gref = 18.87146

stream datadir.def

bomlev 5
read rtf  card unit  5
* TOPOLOGY FILE FOR WATER
*
   20    1                ! Version number
MASS    31 HOH    1.00794 H  ! HYDROGEN IN H2O
MASS    70 OH2   15.9994  O  ! OXYGEN ON WATER

AUTOGENERATE ANGLES
DEFA FIRS NONE LAST NONE

RESI OH2    .000   ! MMFF WATER MODEL
GROUP
ATOM OH2  OH2    -0.86
ATOM H1   HOH     0.43
ATOM H2   HOH     0.43
BOND OH2 H1  OH2 H2   !    H1   H2  ! THE LAST BOND IS NEEDED FOR SHAKE

END

stream @0mmff_setup.str

read sequence OH2 1
generate SOLV

read coor card
* WATER COORDINATES
*
    3
    1    1 OH2  OH2    0.00000   0.06415   0.00000 WAT  1
    2    1 OH2  H1     0.81650  -0.51321   0.00000 WAT  1
    3    1 OH2  H2    -0.81650  -0.51321   0.00000 WAT  1


energy CUTNB 999. CTOFNB 888. cdie e14fac 0.75 switch vswitch

echo enew  ?ener  ?grms
calc ediff = abs ( ?ener - @eref )
calc grmsd = abs ( ?grms - @gref )

set epass = 0
set gpass = 0
if @ediff le @tol set epass = 1
if @grmsd le @tol set gpass = 1
calc egpass = @epass * @gpass
calc pass = @pass * @egpass

test first tol 0.

vibran
diag
write seco card unit 6 finite step 0.000001 tol 0.
end

if @pass gt 0 then
      set status = PASS
else
      set status = FAIL
endif

echo testcase result: @status

stop
