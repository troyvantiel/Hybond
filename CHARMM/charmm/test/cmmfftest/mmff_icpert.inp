* mmff_icpert.inp
* author:  jay banks, 13-apr-94
* purpose: to test internal coordinate TSM with MMFF force field
* external files: MMFFANG.PAR MMFFBOND.PAR MMFFCHG.PAR MMFFDEF.PAR
*                 MMFFOOP.PAR MMFFSTBN.PAR MMFFSUP.PAR MMFFSYMB.PAR
*                 MMFFTOR.PAR MMFFVDW.PAR
*                 icala.mrk
*

if ?numnode gt 1 then
  echo "mmff not compatible with parallel runs"
  echo "testcase result: skip"
  stop
endif

stream datadir.def

bomlev 5

stream @0mmff_setup.str

read merck unit 5
* ICMRK: JAY BANKS, 12-APR-94                                                   
MOL ICALA    O  E =       0.0000   G =  0.00E+00  MMFF                          
   22    21
    2.3299    -0.6837     1.3530     6 14 0     1 C   1   ACE    0.5500     IALA
    2.0034    -1.2876     2.3772     8 40 0     2 O   1   ACE   -0.5500     IALA
    3.0575     0.6491     1.4205     6 10 0     3 CH3 1   ACE    0.0000     IALA
    3.2274     0.9081     2.4656     1  3 0     4 H1  1   ACE    0.0000     IALA
    2.4475     1.4257     0.9596     1  3 0     5 H2  1   ACE    0.0000     IALA
    4.0203     0.5693     0.9162     1  3 0     6 H3  1   ACE    0.0000     IALA
    2.0772    -1.1818     0.1437     7 32 0     7 N   2   ALA   -0.4000     IALA
    2.4587    -0.7621    -0.6515     1  1 0     8 HN  2   ALA    0.2500     IALA
    1.3563    -2.4304    -0.0024     6 10 0     9 CA  2   ALA    0.0500     IALA
    0.6071    -2.4679     0.7880     1  3 0    10 HA  2   ALA    0.0500     IALA
    0.6971    -2.4972    -1.3751     6 10 0    11 CB  2   ALA   -0.1500     IALA
    1.4358    -2.3583    -2.1644     1  3 0    12 HB1 2   ALA    0.0500     IALA
   -0.0514    -1.7109    -1.4718     1  3 0    13 HB2 2   ALA    0.0500     IALA
    0.2276    -3.4700    -1.5203     1  3 0    14 HB3 2   ALA    0.0500     IALA
    2.3819    -3.5448     0.1175     6 14 0    15 C   2   ALA    0.6000     IALA
    3.1747    -3.8039    -0.7891     8 40 0    16 O   2   ALA   -0.5500     IALA
    2.4198    -4.1209     1.3151     7 32 0    17 N   3   CBX   -0.4000     IALA
    1.9225    -3.6866     2.0415     1  1 0    18 H   3   CBX    0.2500     IALA
    3.2840    -5.3037     1.5983     6 10 0    19 CA  3   CBX    0.0000     IALA
    4.3134    -4.9552     1.5148     1  3 0    20 H1  3   CBX    0.0500     IALA
    3.0867    -6.0734     0.8544     1  3 0    21 H2  3   CBX    0.0500     IALA
    3.0867    -5.6531     2.6101     1  3 0    22 H3  3   CBX    0.0500     IALA
    3     1  1      1     2  2      1     7  1      4     3  1      5     3  1
    6     3  1     11     9  1      7     8  1      7     9  1     16    15  2
   15     9  1     15    17  1      9    10  1     11    12  1     11    13  1
   11    14  1     17    18  1     17    19  1     19    20  1     19    21  1
   19    22  1
END

! Generate the dipeptide seqment.
!read sequ card
!* N-methylalanylacetamide (the alanine dipeptide)
!* constructed as a blocked alanine residue
!*
!3
!ace ala cbx
!

generate IALA setup warn first none last none mmff

write merck unit 6
* test merck mol format
*

print psf

ic read unit 5 card
* ICMRK: JAY BANKS, 12-APR-94
* MODIFIED FROM ICPERT: CHARLIE L. BROOKS III, 09-DEC-91
* READ THE RTF, ETC., FOR THE "alanine dipeptide" (WITHOUT WATERS),
* AND WRITE TO A MERCK FORMAT FILE.
*  DATE:    25/ 4/94     14:57:38      CREATED BY USER: banksjl
*
  20   1   0   0   0   0   0   0   0   0   0   0   0   0   0   0   0   0   0   0
   19    1
    1   1 CH3   1 C     2 N     2 CA     1.5199  117.32 -178.37  120.54   1.4492
    2   1 H1    1 CH3   1 C     2 N      0.0000    0.00    0.00  117.32   1.3320
    3   1 H2    1 CH3   1 C     2 N      0.0000    0.00    0.00  117.32   1.3320
    4   1 H3    1 CH3   1 C     2 N      0.0000    0.00    0.00  117.32   1.3320
    5   1 CH3   2 N     1 *C    1 O      1.5199  117.32 -179.03  121.38   1.2331
    6   1 C     2 CA    2 *N    2 HN     1.3320  120.54  173.09  118.84   0.9767
    7   1 C     2 N     2 CA    2 C      1.3320  120.54  -85.00  106.75   1.5192
    8   2 N     2 CA    2 C     3 N      1.4492  106.75  100.00  114.09   1.3295
    9   3 N     2 CA    2 *C    2 O      1.3295  114.09 -174.38  122.05   1.2319
   10   2 CA    2 C     3 N     3 CA     1.5192  114.09  175.11  122.08   1.4920
   11   2 N     2 C     2 *CA   2 CB     1.4492  106.75  119.07  109.33   1.5242
   12   2 N     2 C     2 *CA   2 HA     1.4492  106.75    0.00    0.00   0.0000
   13   2 C     2 CA    2 CB    2 HB1    1.5192  109.33    0.00    0.00   0.0000
   14   2 HB1   2 CA    2 *CB   2 HB2    0.0000    0.00    0.00    0.00   0.0000
   15   2 HB1   2 CA    2 *CB   2 HB3    0.0000    0.00    0.00    0.00   0.0000
   16   2 C     3 CA    3 *N    3 H      1.3295  122.08 -173.93  120.25   0.9817
   17   2 C     3 N     3 CA    3 H1     1.3295  122.08    0.00    0.00   0.0000
   18   3 H1    3 N     3 *CA   3 H2     0.0000    0.00    0.00    0.00   0.0000
   19   3 H1    3 N     3 *CA   3 H3     0.0000    0.00    0.00    0.00   0.0000

ic fill
ic print

NBONDED  NBXMOD 5  ATOM CDIEL SWITCH VATOM VSWITCH -
CUTNB 15.0 CTOFNB 14.0 CTONNB 11.0 EPS 1.0 E14FAC 0.75 WMIN 1.5
update ihbfrq 0 inbfrq -1
energy

! Shake bonds to hydrogen.
shake bonh param tol 1.0e-8

! Set up internal coordinate constraints to fix the dihedral angles at
! their current values.  This is done with a "tsm" command.
! We set the tolerance for the constraints to 10**-5 degrees, and the
! maximum number of iterations to 100.
tsm
 fix dihe iala 1 c iala 2 n iala 2 ca iala 2 c toli 1.0e-5
 fix dihe iala 2 n iala 2 ca iala 2 c iala 3 n toli 1.0e-5
 maxi 100
end

! Equilibration dynamics for 10 steps.
dynamics verlet strt nstep 10 timestep 0.001 -
 inbfrq 10 imgfrq 10 cutnb 15.0 nprint 1 -
 firstt 240.0 finalt 300.0 twindh 5.0 twindl -5.0 -
 atom cdie swit vatom vswi

! Set up the perturbations.  We'll do four interaction energy
! calculations (we'll get four free energy points), for phi and
! psi changes of +/- 5 and 10 degrees.  We save them each step on
! unit 10.  The "move" selection specifies which atoms are to be
! moved when the internal coordinate is changed; "by" is the
! maximum range of the perturbation (10 degrees in this example).
tsm
 savi icun 10 icfr 1 nwin 2
 move dihe iala 1 c iala 2 n iala 2 ca iala 2 c by -10.0 -
   inte sele (bynum 1:8) end
 move dihe iala 2 n iala 2 ca iala 2 c iala 3 n by -10.0 -
   inte sele (bynum 15:22) end
end

! Open a file for the interaction energies.
open unit 10 write form name @9mmff_icpert.icp

! Production dynamics for 10 steps.
dynamics verlet strt nstep 10 timestep 0.001 -
 inbfrq 10 imgfrq 10 cutnb 15.0 nprint 1 -
 firstt 240.0 finalt 300.0 twindh 5.0 twindl -5.0 -
 atom cdie swit vatom vswi

close unit 10

! Clear the TSM data structures.
tsm clear

! Process the perturbation interaction energies to get the
! free energy, internal energy, and entropy as functions
! of the "perturbed" internal coordinates.

! Open the data file.
open unit 10 read form name @9mmff_icpert.icp

! Another "tsm" command is used.  "ic" specifies that these are
! internal coordinate perturbations; "surf" indicates that the
! free energy, etc. should be printed as functions of the ic's at
! the end of the processing; "inte" specifies that we want
! average interaction energies and total energies to be computed
! and printed out; the temperature and delta T used in the
! thermodynamic calculations are 300 K and 10 K, respectively.
! "ctem" specifies that we want to see the average temperature
! computed from the kinetic energy; "bins" is the number of steps per
! batch mean used in the calculation of the statistical errors; we'll
! use 2 batch means in the error calculation, each with 5 steps; "begin"
! and "stop" refer to dataset numbers, rather than dynamics steps
! (in this case they're the same).
tsm post ic surf inte maxp 2 maxw 2 maxs 100
 proc firs 10 nuni 1 bins 5 ctem temp 300.0 delt 10.0 -
  begin 1 stop 10
end

stop
