* c37test/quasi-pca TESTS A NUMBER OF VIBRAN COMMANDS
*

! tries to constrain bonds across parallel partition boundaries
! VIBRAN and quasi are not parallel
if ?NUMNODE .gt. 1 then
 echo "Test NOT performed."
 stop
endif

if ?novibran .eq. 1 then
 echo "Test NOT performed."
 stop
endif

stream datadir.def

OPEN READ FORM UNIT 11 NAME  @0toph9.rtf
READ RTF  CARD UNIT 11
CLOSE UNIT 11
BOMLEV -2
OPEN READ FORM UNIT 12 NAME  @0param6.prm
READ PARAM CARD UNIT 12
CLOSE UNIT 12
BOMLEV -1

READ SEQUENCE CARDS
* TRYPTOPHAN
*
    1
TRP
GENERATE TRYP setup

OPEN WRITE FILE UNIT 18 NAME @9FOR018.DAT
WRITE PSF FILE UNIT 18
* TRYPTOPHAN USING EXPLICIT HYDROGEN
*

ic purge

READ COORD CARDS
* TRYPTOPHAN BUILT AND MINIMIZED WITH SD 50 STEPS, ABNR 500 STEP,
* AND CG 50 STEPS, EPS=R POTENTIAL
*
   19
    1    1 TRP  HT1    0.10124   0.57409  -1.38529
    2    1 TRP  HT2    1.71114   0.43637  -1.90440
    3    1 TRP  N      1.06717   0.30225  -1.10075
    4    1 TRP  HT3    1.07999  -0.68894  -0.79051
    5    1 TRP  CA     1.47212   1.19190   0.02727
    6    1 TRP  CB     2.69565   2.02615  -0.34753
    7    1 TRP  CG     3.92166   1.16873  -0.67893
    8    1 TRP  CD1    4.81252   0.77506   0.29018
    9    1 TRP  CD2    4.29511   0.67909  -1.89046
   10    1 TRP  NE1    5.72693   0.04608  -0.32026
   11    1 TRP  HE1    6.09389  -0.74349   0.12669
   12    1 TRP  CE2    5.42381  -0.01032  -1.60094
   13    1 TRP  CE3    3.76875   0.77367  -3.15481
   14    1 TRP  CZ2    6.13756  -0.67683  -2.56270
   15    1 TRP  CZ3    4.47167   0.10800  -4.14811
   16    1 TRP  CH2    5.63634  -0.60503  -3.85644
   17    1 TRP  C      0.27986   2.11295   0.30889
   18    1 TRP  OT1   -0.40216   2.48057  -0.63623
   19    1 TRP  OT2   -0.10570   2.19226   1.45887

mini abnr nstep 100 nprint 10  CUTNB 100.0 CTONNB 99.0 CTOFNB 99.5
COOR ORIE MASS

OPEN WRITE FORM UNIT 17 NAME @9FOR017.DAT
WRITE COOR CARD FILE UNIT 17
* TRYPTOPHAN MINIMIZED FOR VIBRAN TEST CASE
*

SHAKE BOND

! small dynamic to test PROJ TRAJ
OPEN WRITE CARD UNIT 88 NAME @9FOR088.DAT
DYNA VERL NSTEP 500  timestp .001 INBFRQ 0 IHBFRQ 0 -
      FIRSTT 50. FINALT 298. NPRINT 50 -
     ISEED 311 IPRFRQ 500 IHTFRQ 0 IEQFRQ 100 NTRFRQ 100   IUNWRI 88

OPEN READ  CARD UNIT 88 NAME @9FOR088.DAT
OPEN WRITE UNFO UNIT 41 NAME @9FOR041.DAT
DYNA REST IUNREA 88  NSTEP 100 IEQFRQ 0  -
      NSAVC 1 IUNCRD 41  IUNWRI 89  IPRFRQ 1000

OPEN READ  CARD UNIT 89 NAME @9FOR088.DAT
OPEN WRITE UNFO UNIT 42 NAME @9FOR042.DAT
DYNA REST IUNREA 89  NSTEP 100 IEQFRQ 0  -
      NSAVC 1 IUNCRD 42  IUNWRI -1  IPRFRQ 1000

OPEN READ  FILE UNIT 41 NAME @9FOR041.DAT
OPEN WRITE UNFO UNIT 43 NAME @9FOR043.DAT
OPEN READ  CARD UNIT 17 NAME @9FOR017.DAT
READ COOR COMP UNIT 17 CARD
MERGE COOR FIRS 41 OUTP 43 sele all end orient sele all end

OPEN READ  FILE UNIT 41 NAME @9FOR041.DAT
OPEN WRITE UNFO UNIT 12 NAME @9test-skip10.dcd
OPEN READ  CARD UNIT 17 NAME @9FOR017.DAT
READ COOR COMP UNIT 17 CARD
MERGE COOR FIRS 41 NSKIP 10 OUTP 12 sele all end orient sele all end

OPEN READ  FILE UNIT 42 NAME @9FOR042.DAT
OPEN WRITE UNFO UNIT 44 NAME @9FOR044.DAT
OPEN READ  CARD UNIT 17 NAME @9FOR017.DAT
READ COOR COMP UNIT 17 CARD
MERGE COOR FIRS 42 OUTP 44 sele all end orient sele all end

OPEN READ  FILE UNIT 43 NAME @9FOR043.DAT
COOR DYNA COMP FIRS 43

VIBRAN NMOD 100
! test a number of keywords combinations
OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
QUASI FIRSTU 43 NUNIT 1 TEMP 298. MEAN

OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
QUASI FIRSTU 43 STPD NOTP

OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
QUASI FIRSTU 43 STPD MEAN NOTP

OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
QUASI FIRSTU 43 STPD TEMP 298.

OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
QUASI FIRSTU 43 CORD NOTP

OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
QUASI FIRSTU 43 NUNIT 1 TEMP 298. NOMA

OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
QUASI FIRSTU 43 NUNIT 1 TEMP 298. MEAN NOMA

OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
QUASI FIRSTU 43 STPD NOMA NOTP

OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
QUASI FIRSTU 43 STPD NOMA MEAN NOTP

OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
QUASI FIRSTU 43 STPD NOMA MEAN NORM NOTP

OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
QUASI FIRSTU 43 STPD NOMA NOTP



!!!! mark 2 - 57 coord 100 frames
!!!! next two output should be close to identical
!!!!  (except for number of modes and numerical imprecisions)
! CORD (def)
OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
QUASI FIRSTU 43 CORD NOMA NORM MEAN NOTP
OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
OPEN WRIT FORM UNIT 10 NAME @9FOR010.PDB
PROJ TRAJ FIRSTU 43 NOMA NONO MODE 54 THRU 57 -
     WRIT PDB WUNI 10 SPIR

! STPD
OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
QUASI FIRSTU 43 STPD NOMA NORM MEAN NOTP
OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
OPEN WRIT FORM UNIT 11 NAME @9FOR011.PDB
PROJ TRAJ FIRSTU 43 NOMA NONO MODE 97 THRU 100 -
     WRIT PDB WUNI 11 SPIR

!!!! mark 3 - 57 coord 10 frames
!!!! next two output should be close to identical
!!!!  (except for number of modes and numerical imprecisions)
! CORD
OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
QUASI FIRSTU 43 NSKIP 10 CORD NOMA NORM MEAN NOTP
OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
OPEN WRIT FORM UNIT 12 NAME @9FOR012.PDB
PROJ TRAJ FIRSTU 43 NSKIP 10 NOMA NONO MODE 54 THRU 57 -
     WRIT PDB WUNI 12

! STPD (def)
OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
QUASI FIRSTU 43 NSKIP 10 STPD NOMA NORM MEAN NOTP
OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
OPEN WRIT FORM UNIT 13 NAME @9FOR013.PDB
PROJ TRAJ FIRSTU 43 NSKIP 10 NOMA NONO MODE 7 THRU 10 -
     WRIT PDB WUNI 13



!!!! mark 4 - auto
! quasi AUTO
OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
QUASI FIRSTU 43 NSKIP 10 AUTO NOMA NOTP
OPEN READ FILE UNIT 43 NAME @9FOR043.DAT
OPEN WRIT FORM UNIT 46 NAME @9FOR046.DAT
PROJ TRAJ FIRSTU 43 NSKIP 10 MODE 7 THRU 10 -
     WRIT PDB WUNI 46 SPIR

END

STOP
