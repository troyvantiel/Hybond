* This input script tests the use of NBFIX with OpenMM
*

if ?openmm .ne. 1 then
   echo "Test NOT performed."
   stop
endif
if ?numnode gt 1 envi OPENMM_PLATFORM "Reference"

stream datadir.def

read rtf card
* Topology for water and ions
*
31  1

MASS  6   SOD  22.98977 NA ! Sodium Ion
MASS  15  CLA  35.45000 CL ! Chloride Ion

default first none last none

RESI SOD       1.00 ! Sodium Ion
GROUP
ATOM SOD  SOD  1.00
PATCHING FIRST NONE LAST NONE
RESI CLA      -1.00 ! Chloride Ion
GROUP
ATOM CLA  CLA -1.00
PATCHING FIRST NONE LAST NONE

END

set tol = 2e-3
set pme = 1
set nbfix = 0
set rxnfld = 
label gentst

if @nbfix gt 0 goto setnbfix
if @nbfix eq 0 goto setnonbfix

label fromsetup

read sequ sod 1
generate sod 

read sequ cla 1
generate cla

goto crystal
label fromxtal

set cut = 7.75
set cutoffs = cdie atom vatom cutnb @cut cutim @cut ctofnb @cut ctonnb @cut vswitch switch 

! PME
if @PME gt 0 set ewald = Ewald kappa 0.320 pmEwald order 4 fftx 16 ffty 16 fftz 16
if @pme eq 0 then
      set ewald = noewald
      set cutoffs = cdie atom vatom cutnb @cut cutim @cut ctofnb @cut ctonnb @cut vswitch omrf
endif

faster on

scalar x set 0
scalar y set 0
scalar z set 0

calc d = @size / 40
calc halfbox = @size / 2

set eps = 78.3    ! value of solvent dielectric in OpenMM reaction field
! Calculate constants for the reaction field
calc k_rf = ( @eps - 1 ) / ( 2 * @eps + 1 ) / ( @cut * @cut * @cut )
calc c_rf = ( 3 * @eps ) / ( 2 * @eps + 1 ) / ( @cut )

!Set one at the left x box boundary
scalar x set -@halfbox select segid cla end

! Set the other 3-@d A from this one, toward the right x box edge
Calc xd = -@halfbox + 8 * @d
scalar x set @xd select segid sod end

label move

      coor trans xdir @d select segid sod end
      energy eps 1.0 @cutoffs @ewald
      set ech = ?ener
      calc echelec = ?ener - ?vdw - ?imnb

      energy omm @rxnfld
      set eomm = ?ener

      q 1 2
      set dist = ?dist

      scalar x stat select segid sod end

      set dpbc = @dist
      if @dist gt @halfbox then
          calc dpbc = @size - @dist
      endif

!     Calculate the Reaction Field for this separation
      Calc RF = 1 + @{k_rf} * @dpbc * @dpbc * @dpbc - @{c_rf} * @dpbc
      Calc ech_rf = @ech + @echelec * ( @RF - 1 )
      if @pme eq 0 set ech = @{ech_rf}
!      echo @dist @dpbc @ech @eomm

      !!!!!!!!!!!CHECK SUCCESS CRITERIA!!!!!!!!!!!!!
      if @ech .ne. 0 then
        calc diff = abs ( ( @ech - @eomm ) / @ech )
      else
        calc diff = abs ( ( @ech - @eomm )  )
      endif
      if diff gt @tol then
         echo failed for pme=@{pme}, nbfix=@nbfix for distance (w/pbc) = @dpbc diff = @diff
      else
         echo passed for pme=@{pme}, nbfix=@nbfix for distance (w/pbc) = @dpbc
      endif

if ?save le -@xd goto move

if @nbfix eq 0 then
      if @pme eq 1 then
          set pme = 0
          set rxnfld = omrf omrx 78.5
      else
          set nbfix = 1
          set pme = 1
          set rxnfld = 
      endif
else
      if @pme eq 1 then
           set pme = 0
           set rxnfld = omrf omrx 78.5
      else
           stop
      endif
endif

omm clear
delete atom select all end
goto gentst

stop

label setnbfix
read para card flex
* Parameters for water and ions
*

ATOMS
MASS  6   SOD  22.98977 ! Sodium Ion
MASS  15  CLA  35.45000 ! Chloride Ion

NONBONDED nbxmod  5 atom cdiel switch vatom vdistance vswitch -
cutnb 14.0 ctofnb 12.0 ctonnb 10.0 eps 1.0 e14fac 1.0 wmin 1.5

SOD      0.0       -1      2
CLA      0.0       -1      2

NBFIX
!              Emin         Rmin
!            (kcal/mol)     (A)
SOD    CLA      -1          4
END
goto fromsetup

label setnonbfix
read para card flex
* Parameters for water and ions
*

ATOMS
MASS  6   SOD  22.98977 ! Sodium Ion
MASS  15  CLA  35.45000 ! Chloride Ion

NONBONDED nbxmod  5 atom cdiel switch vatom vdistance vswitch -
cutnb 14.0 ctofnb 12.0 ctonnb 10.0 eps 1.0 e14fac 1.0 wmin 1.5

SOD      0.0       -1      2
CLA      0.0       -1      2

END

goto fromsetup

label crystal
! Dimension of a box
set size = 15.5516
set theta = 90.0
Crystal define cubic @size @size @size @theta @theta @theta    

crystal read card
* GENERATED BY CHARMM-GUI (http://www.charmm-gui.org) on Sep, 26. 2012.
* STREAM FILE FOR CRYSTAL SYMMETRY OPERATION
*

 SYMMETRY
 (X,Y,Z)                                 
 END

 IMAGES
 ! Operation     A     B     C
           1    -1    -1    -1
           1    -1     0    -1
           1    -1     1    -1
           1     0    -1    -1
           1     0     0    -1
           1     0     1    -1
           1    -1    -1     0
           1    -1     0     0
           1    -1     1     0
           1     0    -1     0
           1     0     1     0
           1    -1    -1     1
           1    -1     0     1
           1    -1     1     1
           1     0    -1     1
           1     0     0     1
           1     0     1     1
           1     1     1     1
           1     1     0     1
           1     1    -1     1
           1     1     1     0
           1     1     0     0
           1     1    -1     0
           1     1     1    -1
           1     1     0    -1
           1     1    -1    -1
 END
 

goto fromxtal

