* CHARMM c35a2 testcase
* Full QM/MM Hessian Test Case 
* Water Dimer
* Author: H. Lee Woodcock, 06/2007
*

IF ?QCHEM .NE. 1 THEN
 echo "Test NOT performed."
 STOP
endif

bomlev -1
STREam datadir.def

read rtf card
* TOPOLOGY FILE FOR PROTEINS USING EXPLICIT HYDROGEN ATOMS: VERSION 19
*
   20    1                ! Version number
MASS     4 HT     1.00800 ! TIPS3P WATER HYDROGEN
MASS    58 OT    15.99940 ! TIPS3P WATER OXYGEN
RESI TIP3    .000   ! TIPS3P WATER MODEL
GROUP
ATOM OH2  OT     -0.834
ATOM H1   HT      0.417
ATOM H2   HT      0.417
BOND OH2  H1        OH2  H2        H1   H2  ! THE LAST BOND IS NEEDED FOR SHAKE
ANGLE  H1 OH2 H2
!ACCE OH2
IC H1 OH2 H2 BLN  0.0 0.0 0.0 0.0 0.0
IC H2 OH2 H1 BLN  0.0 0.0 0.0 0.0 0.0
PATC  FIRS NONE LAST NONE

end

read param card
*
BOND
HT   OT     450.0       0.9572 ! from TIPS3P geometry
HT   HT       0.0       1.5139 ! from TIPS3P geometry (for SHAKE w/PARAM)
ANGLE
HT   OT   HT      55.0     104.52    ! FROM TIPS3P GEOMETRY


NONBONDED  NBXMOD 5  ATOM CDIEL SHIFT VATOM VDISTANCE VSHIFT -
     CUTNB 12.0  CTOFNB 10.5  CTONNB 9.0  EPS 1.0  E14FAC 0.4  WMIN 1.5
!

HT       0.0440    -0.0498    0.8000 !TIP3P water hydrogen, see NBFIX below
OT       0.8400    -0.1591    1.6000 !TIP3P water oxygen,   see NBFIX below

NBFIX
!                  Emin       Rmin
!                  (kcal/mol) (A)
!
!	We're gonna NBFIX the TIP3P water-water interactions
!	here to make them more like Jorgensen's.  The vdW parameters
!	specified above will be in effect, therefore, for ONLY
!	protein (read, protein OR nucleic acid)-water interactions.
!	OT-OT is exactly Jorgensen's; HT interactions are added
!	here.
!
OT   OT      -0.152073  3.5365  ! TIPS3P VDW INTERACTION
HT   HT      -0.04598   0.4490
HT   OT      -0.08363   1.9927

end

read sequ tip3 2

GENERATE wat SETUP WARN

print coor
print psf

READ COOR CARD
*   WATER
*
    6
    1    1 TIP3 OH2   -1.34273  -0.01842  -0.111755 wat  1      0.00000
    2    1 TIP3 H1    -1.72642  -0.74705   0.432330 wat  1      0.00000
    3    1 TIP3 H2    -1.73516   0.77178   0.330642 wat  1      0.00000
    4    2 TIP3 OH2    1.39010   0.01092   0.091880 wat  2      0.00000
    5    2 TIP3 H1     1.64464  -0.05138  -0.860714 wat  2      0.00000
    6    2 TIP3 H2     0.40369   0.00007   0.014497 wat  2      0.00000

coor orient mass
print coor 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!! Set options for QM/MM Hessian !!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
 envi qchemcnt  "data/qmmm-hess.inp"
 envi qcheminp  "q1.inp"
 envi qchemexe  "../tool/qchem"
 envi qchemout  "q1.out"
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

 qchem remove sele resid 1 show end
 
 vibran nmode 18 
 diag 
 thermo temp 300.0
 end

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!! Set options for pure QM Hessian !!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
 envi qchemcnt  "data/qm-hess.inp"
 envi qcheminp  "q1.inp"
 envi qchemexe  "../tool/qchem"
 envi qchemout  "q1.out"
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

 qchem remove sele all show end
 
 vibran nmode 18 
 diag 
 thermo temp 300.0
 end

stop

