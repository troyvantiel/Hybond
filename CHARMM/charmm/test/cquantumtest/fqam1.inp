* Fluctuating charge dynamics simulation of a QM/MM water dimer
* The QM region is AM1, and the MM region is TIP4P/FQ
* Ben Webb: c28a2 15th July 2000
*

if ?flucq .ne. 1 then stop

BOMLEV -2
stream datadir.def

! Read in standard RTF, and modified FlucQ parameters
OPEN READ CARD UNIT 8 NAME @0flucq.rtf
READ RTF CARD UNIT 8
OPEN READ CARD UNIT 8 NAME @0flucq.prm
READ PARAM CARD UNIT 8

READ SEQU TIP4 1
READ SEQU QWAT 1
GENE WATR SETUP NOANGL NODI

OPEN READ CARD UNIT 8 NAME @0flucq.crd
READ COOR CARD UNIT 8
CLOSE UNIT 5

! Constrain M site to be 0.15 Angstroms along the H-O-H bisector
LONE BISE DIST 0.15 ANGLE 0.0 DIHE 0.0 -
     SELE TYPE M1 END -
     SELE TYPE OH2 END -
     SELE TYPE H1 END -
     SELE TYPE H2 END

! Constrain all bonds at equilibrium values
SHAKE BONH PARA

! Activate the QM region (AM1)
QUANTUM AM1 SELE RESN QWAT END REMO

! Model the TIP4 water in the MM region with FlucQ
FLUCQ ON SELE RESN TIP4 END

! Get the minimum energy charges for the starting Cartesian coordinates
FLUCQ EXACT

ENERGY

! Print charges and forces
FLUCQ PRINT

! Run a short dynamics simulation, just to make sure FlucQ is working
! Maintain the charge temperature at 1 Kelvin by
! Nose-Hoover thermostatting
DYNA STRT LEAP NSTEP 100 TIME 0.001 CDIE -
   IPRFRQ 100 IHTFRQ 0 -
   FIRSTT 300.0 FINALT 300.0 TEMINC 0 NPRINT 10 -
   TWINDH 10.0 TWINDL -10.0 TCONS TCOU 0.001 -
   FQTEMP 1.0 FQMASS 0.1 -
   IASORS 1 IASVEL 1 ICHECW 0

! Print out the charges on the TIP4P water
! If FlucQ is working, the water should be polarised by the QM
! region; i.e. charges should be roughly H1=0.437 H2=0.464
SCALAR CHAR SHOW SELE RESN TIP4 END

STOP
