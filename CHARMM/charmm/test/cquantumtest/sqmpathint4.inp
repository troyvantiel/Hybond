* CHARMM 25 Testcase QM/MM-Ewald using group based cutoff and PME 
* Kwangho Nam, 07-01-2005
* water-vacum system
*

set pnode
if ?NUMNODE .gt. 1 set pnode node 0

if ?squantm .ne. 1 then stop

!bomlev -3
stream datadir.def
set 6 @9                  ! 9 is used for cell dimension

read rtf card              
* TIPS3P WATER TOPOLOGY FILE
* TIPS3P WATER TOPOLOGY FILE
*
   18    1
MASS     4 HT     1.00800 ! TIPS3P WATER HYDROGEN
MASS    64 OT    15.99940 ! TIPS3P WATER OXYGEN

RESI TIP3    .000   ! TIPS3P WATER MODEL
GROUP
ATOM OH2  OT     -0.834
ATOM H1   HT      0.417
ATOM H2   HT      0.417
BOND OH2 H1  OH2 H2  H1 H2  ! THE LAST BOND IS NEEDED FOR SHAKE
THET H1  OH2  H2
!                          NOTE: NO DONORS OR ACCEPTORS FOR TIPS3P
PATC  FIRS NONE LAST NONE

END

read param card
* TIPS3P WATER PARAMETER FILE
*
BOND
HT   OT     450.0       0.9572 ! from TIPS3P geometry
HT   HT       0.0       1.5139 ! from TIPS3P geometry (for SHAKE w/PARAM)
THETAS
HT   OT   HT      55.0     104.52    ! FROM TIPS3P GEOMETRY
!
NONBONDED   GROUP CDIEL SWITCH VGROUP VDISTANCE VSWITCH -
     CUTNB 9.0  CTOFNB 8.0 CTONNB 8.0 EPS 1.0  E14FAC 1.0  WMIN 1.5
!
HT       0.0440    -0.0498    0.8000 !TIP3P water hydrogen, see NBFIX below
OT       0.8400    -0.1591    1.6000 !TIP3P water oxygen,   see NBFIX below
!!HT       0.0440    1.0       0.8000
!!OT       0.8400    6.0       1.6000

NBFIX
!OT   OT      -0.152073   3.5365  ! TIPS3P VDW INTERACTION
!HT   OT      -0.05       1.0  ! MODIFIED TIPS3P WITH CATASTROPHY ZONE REMOVED
OT   OT      -0.152073  3.5365  ! TIPS3P VDW INTERACTION
HT   HT      -0.04598   0.4490
HT   OT      -0.08363   1.9927
! FOR NOW, USE CHARLIE'S PARAMETERS
!OT OT -0.1521 3.5364
!HT HT -0.04598 0.4490
!OT HT -0.08363 1.9927

END

read sequence TIP3 560
gener WAT noangle nodihedral

open unit 14 read card name @0wat560.crd 
read coor card unit 14             
close unit 14

set 7 25.40608
set 8 25.40608
set 9 25.40608 

crystal define cubic @7 @8 @9 90.0 90.0 90.0
CRYStal BUILd  CUTOff 15.0
image byres xcen 0.0 ycen 0.0 zcen 0.0 sele all end

update -
    elec group switch cdie eps 1. -
    ewald KAPPA 0.34 spline PMEWald ORDEr 6 -
    FFTX 30 FFTY 30 FFTZ 30 -
    vdw vgroup vswitch -
    cutnb 11.0 ctofnb 10.0 ctonnb 9.0  -
    inbfrq 25 cutim 13.0 imgfrq 25 wmin 0.5 

energy

define QMS sele segid WAT .and. (resid 408 .or. resid 413 .or. resid 504) show end
define W1 sele segid WAT .and. resid 408 end
define W2 sele segid WAT .and. resid 413 end
define W3 sele segid WAT .and. resid 504 end

print coord sele QMS end

quantum am1 charge 0 sele QMS end -
            remo swit NEWD 1 KMAX 5

energy

! Set up 3D umbrella sampling
set kumx     50.0
set kumy     50.0
set kumz     50.0
set d0x      -1.0      ! New
set d0y      -1.0      ! New
set d0z      -1.0      ! New
set NStep    120       ! # of MD steps
set umstart  20        ! # of equilibration steps

! Umbrella sampling settings
calc ldx  @d0x - 1.0
calc hdx  @d0x + 1.0
calc ldy  @d0y - 1.0
calc hdy  @d0y + 1.0
calc ldz  @d0z - 1.0
calc hdz  @d0z + 1.0
set  ddx        0.01
set  ddy        0.01
set  ddz        0.01

open write formatted unit 20 name @0umb3d.rcor1
open write formatted unit 21 name @0umb3d.rcor2
open write formatted unit 22 name @0umb3d.rcor3
open write formatted unit 23 name @0umb3d.pmf

! W1 OH bond
rxncor: define P1 point select ( W1 .and. type OH2  ) end
rxncor: define P2 point select ( W1 .and. type H2  ) end
rxncor: define RC1 distance P1 P2 ! Breaking bond
! W2 OH bond
rxncor: define P3 point select ( W2 .and. type OH2  ) end
rxncor: define P4 point select ( W2 .and. type H2  ) end
rxncor: define RC2 distance P3 P4 ! Breaking bond
! W3 OH bond
rxncor: define P5 point select ( W3 .and. type OH2  ) end
rxncor: define P6 point select ( W3 .and. type H2  ) end
rxncor: define RC3 distance P5 P6 ! Breaking bond
! W1/W2 forming OH bond
rxncor: define RC4 distance P2 P3 ! Forming bond
! W2/W3 forming OH bond
rxncor: define RC5 distance P4 P5 ! Forming bond
! W3/W1 forming OH bond
rxncor: define RC6 distance P6 P1 ! Forming bond
! Anti-symmetric stretch bonds
rxncor: define RCR1 combine RC1 1.0 RC4 -1.0
rxncor: define RCR2 combine RC2 1.0 RC5 -1.0
rxncor: define RCR3 combine RC3 1.0 RC6 -1.0

rxncor: set nrxn 3 RCR1 RCR2 RCR3

open read formatted unit 24 name @0umb3d.pot
rxncor: bias name RCR1 unit 24
close unit 24
open read formatted unit 24 name @0umb3d.pot
rxncor: bias name RCR2 unit 24
close unit 24
open read formatted unit 24 name @0umb3d.pot
rxncor: bias name RCR3 unit 24
close unit 24

rxncor: trace RCR1 unit 20 freq 1
rxncor: trace RCR2 unit 21 freq 1
rxncor: trace RCR3 unit 22 freq 1
rxncor: umbrella name RCR1 kumb @kumx del0 @d0x form 5
rxncor: umbrella name RCR2 kumb @kumy del0 @d0y form 5
rxncor: umbrella name RCR3 kumb @kumz del0 @d0z form 5
rxncor: statistics name RCR1 lowdelta @ldx hidelta @hdx deldel @ddx -
                   name RCR2 lowdelta @ldy hidelta @hdy deldel @ddy -
                   name RCR3 lowdelta @ldz hidelta @hdz deldel @ddz start @umstart

open write unformatt unit 32 name @0umb3d.trj         ! write trajectory f

dynamics leap cpt start -
         nstep @NStep timestep 0.001 -
         iunrea -30 iunwri -31 iuncrd 32 iunvel -1 kunit -1 -
         nprint 10 nsavc 10 isvfrq 10 ntrf 10 ixtfrq 10 iprfrq 10 -
         iseed 4028987 firstt 298.15 finalt 298.15 tstruc 298.15 -
         teminc 0.00 ihtfrq 0 ieqfrq 0 iasor 0 iasvel 1 iscvel 0 -
         ichecw 0 twindh 5.0 twindl -5.0  -
         hoover tmass 1000.0 reft 298.15 -
         pconstant pinternal pmass 500.0 preferencce 1.0 ECHECK 1000000.0

rxncor: write unit 23
close unit 23

! Write pdb file
open unit 1 card write name @0umb3d.pdb
write coor pdb unit 1
* MD-PBC of 3 QM water in water box
* PMF simulations window @d0x @d0y @d0z
*
close unit 1

!--Test: PI QM/MM Ewald ---
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Trajectory settings
set NSkip           1     ! # of MD steps
set NSave           1     ! Initialize sampling from
calc NSkip @NSave*@NSkip  ! Skip configurations from trj

set NBeads         18     ! 
set T          298.15
set Neq            10     ! Equilibration of beads not necessary when bisecting entire bead
set Nav            10     ! Use ~ 100
set NMove           1
set RNum           -1     ! Time-dependent randum number seed
set RNum       618897     ! Should be -1 except for test-case

open read  unit 11 unfo name @0umb3d.trj  ! read coordinates from trj file
open write unit 12 form name @0umb3d.qub
open write unit 14 form name @0umb3d.bds

QUB SQCP SELE QMS SHOW END NOEW FAST FFOCK -
    TEMP @T MCON @Nav MCEQ @Neq BEAD @NBeads NBMOVE @NMove -
    IRAN @RNum -
    FIRST 11 OUNI 12 BDIN -13 BDOUT -14 -
    NUNI 1 BEGI -1 STOP -1 SKIP @NSkip

close unit 11
close unit 12
close unit 14

STOP
