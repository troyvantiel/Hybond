* MNDO97/CHARMM Testcase QM/MM-Ewald using group based cutoff and PME 
* Kwangho Nam, 07-01-2005
* water-vacum system
*

if ?mndo97 .ne. 1 then
  echo "Test NOT performed."
  stop
endif

set pnode
if ?NUMNODE .gt. 1 set pnode node 0

bomlev -3
stream datadir.def
set 6 @9                  ! 9 is used for cell dimension

read rtf card              
* TIPS3P WATER TOPOLOGY FILE
* TIPS3P WATER TOPOLOGY FILE
*
   18    1
MASS     4 HT     1.00800 ! TIPS3P WATER HYDROGEN
MASS    64 OT    15.99940 ! TIPS3P WATER OXYGEN

RESI TIP3    .000   ! TIPS3P WATER MODEL
GROUP
ATOM OH2  OT     -0.834
ATOM H1   HT      0.417
ATOM H2   HT      0.417
BOND OH2 H1  OH2 H2  H1 H2  ! THE LAST BOND IS NEEDED FOR SHAKE
THET H1  OH2  H2
!                          NOTE: NO DONORS OR ACCEPTORS FOR TIPS3P
PATC  FIRS NONE LAST NONE

END

read param card
* TIPS3P WATER PARAMETER FILE
*
BOND
HT   OT     450.0       0.9572 ! from TIPS3P geometry
HT   HT       0.0       1.5139 ! from TIPS3P geometry (for SHAKE w/PARAM)
THETAS
HT   OT   HT      55.0     104.52    ! FROM TIPS3P GEOMETRY
!
NONBONDED   GROUP CDIEL SWITCH VGROUP VDISTANCE VSWITCH -
     CUTNB 9.0  CTOFNB 8.0 CTONNB 8.0 EPS 1.0  E14FAC 1.0  WMIN 1.5
!
HT       0.0440    -0.0498    0.8000 !TIP3P water hydrogen, see NBFIX below
OT       0.8400    -0.1591    1.6000 !TIP3P water oxygen,   see NBFIX below
!!HT       0.0440    1.0       0.8000
!!OT       0.8400    6.0       1.6000

NBFIX
!OT   OT      -0.152073   3.5365  ! TIPS3P VDW INTERACTION
!HT   OT      -0.05       1.0  ! MODIFIED TIPS3P WITH CATASTROPHY ZONE REMOVED
OT   OT      -0.152073  3.5365  ! TIPS3P VDW INTERACTION
HT   HT      -0.04598   0.4490
HT   OT      -0.08363   1.9927
! FOR NOW, USE CHARLIE'S PARAMETERS
!OT OT -0.1521 3.5364
!HT HT -0.04598 0.4490
!OT HT -0.08363 1.9927

END

read sequence TIP3 560
gener WAT noangle nodihedral

open unit 14 read card name @0wat560.crd 
read coor card unit 14             
close unit 14

set 7 25.40608
set 8 25.40608
set 9 25.40608 

crystal define cubic @7 @8 @9 90.0 90.0 90.0
CRYStal BUILd  CUTOff 15.0
image byres xcen 0.0 ycen 0.0 zcen 0.0 sele all end

update -
    elec group switch cdie eps 1. -
    ewald KAPPA 0.34 spline PMEWald ORDEr 6 -
    FFTX 30 FFTY 30 FFTZ 30 -
    vdw vgroup vswitch -
    cutnb 11.0 ctofnb 10.0 ctonnb 9.0  -
    inbfrq 25 cutim 13.0 imgfrq 25 wmin 0.5 

energy

print coord sele segid WAT .and. resid 7 end

open unit 66 name @0mndo_wat.inp form read
mndo unit 66 sele segid WAT .and. resid 7 end -
             sele none end remo NEWD -1 KMAX 5

energy

dynamics leap cpt start -
         nstep 100 timestep 0.001 -
         iunrea -30 iunwri -31 iuncrd -32 iunvel -1 kunit -1 -
         nprint 10 nsavc 10 isvfrq 10 ntrf 10 ixtfrq 10 iprfrq 10 -
         firstt 298.15 finalt 298.15 tstruc 298.15 -
         teminc 0.00 ihtfrq 0 ieqfrq 0 iasor 0 iasvel 1 iscvel 0 -
         ichecw 0 twindh 5.0 twindl -5.0  -
         hoover tmass 1000.0 reft 298.15 -
         pconstant pinternal pmass 500.0 preferencce 1.0 ECHECK 1000000.0


STOP
