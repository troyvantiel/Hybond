* QM/MM Free Energy Perturbation Example: Water Dimer
*

bomlev -3

if ?qchem .ne. 1 then
 echo "cquantumtest/qmmm_pert.inp> Test NOT performed."
 stop
endif

STREam datadir.def

read rtf card
* TOPOLOGY FILE FOR PROTEINS USING EXPLICIT HYDROGEN ATOMS: VERSION 19
*
   20    1                ! Version number
MASS     4 HT     1.00800 ! TIPS3P WATER HYDROGEN
MASS    58 OT    15.99940 ! TIPS3P WATER OXYGEN
RESI TIP3    .000   ! TIPS3P WATER MODEL
GROUP
ATOM OH2  OT     -0.834
ATOM H1   HT      0.417
ATOM H2   HT      0.417
BOND OH2  H1        OH2  H2        H1   H2  ! THE LAST BOND IS NEEDED FOR SHAKE
ANGLE  H1 OH2 H2
!ACCE OH2
IC H1 OH2 H2 BLN  0.0 0.0 0.0 0.0 0.0
IC H2 OH2 H1 BLN  0.0 0.0 0.0 0.0 0.0
PATC  FIRS NONE LAST NONE

end

read param card
*
BOND
HT   OT     450.0       0.9572 ! from TIPS3P geometry
HT   HT       0.0       1.5139 ! from TIPS3P geometry (for SHAKE w/PARAM)
ANGLE
HT   OT   HT      55.0     104.52    ! FROM TIPS3P GEOMETRY


NONBONDED  NBXMOD 5  ATOM CDIEL SHIFT VATOM VDISTANCE VSHIFT -
     CUTNB 12.0  CTOFNB 10.5  CTONNB 9.0  EPS 1.0  E14FAC 0.4  WMIN 1.5
!

HT       0.0440    -0.0498    0.8000 !TIP3P water hydrogen, see NBFIX below
OT       0.8400    -0.1591    1.6000 !TIP3P water oxygen,   see NBFIX below

NBFIX
!                  Emin       Rmin
!                  (kcal/mol) (A)
!
!	We're gonna NBFIX the TIP3P water-water interactions
!	here to make them more like Jorgensen's.  The vdW parameters
!	specified above will be in effect, therefore, for ONLY
!	protein (read, protein OR nucleic acid)-water interactions.
!	OT-OT is exactly Jorgensen's; HT interactions are added
!	here.
!
OT   OT      -0.152073  3.5365  ! TIPS3P VDW INTERACTION
HT   HT      -0.04598   0.4490
HT   OT      -0.08363   1.9927

end

!-----------------------------------------------------------------------

read sequence TIP3 2
gener W noangle nodihedral

read coor card
* QM/MM Pert Test Case: Water Dimer
*  DATE:    01/14/05     11:55:32      CREATED BY USER: hlwood
*
    6
    1    1 TIP3 OH2   -1.30910  -0.25601  -0.24045 W    1      0.00000
    2    1 TIP3 H1    -1.85344   0.07163   0.52275 W    1      0.00000
    3    1 TIP3 H2    -1.70410   0.16529  -1.04499 W    1      0.00000
    4    2 TIP3 OH2    1.37293   0.05498   0.10603 W    2      0.00000
    5    2 TIP3 H1     1.65858  -0.85643   0.10318 W    2      0.00000
    6    2 TIP3 H2     0.40780  -0.02508  -0.02820 W    2      0.00000


!-----------------------------------------------------------------------
 envi qchemexe  "qchem"               ! Command to call quantum program
 envi qchemcnt  "data/qchem_pert.inp" ! Non Pert Control file 
 envi qcheminp  "q1.inp"              ! Non Pert Quantum input file
 envi qchemout  "qchem.out"           ! Non Pert Quantum output file
 envi sainp     "data/s0.inp"         ! State 0 control file
 envi sbinp     "data/s1.inp"         ! State 1 control file
 envi stateainp "state0.inp"          ! State 0 quantum input file
 envi statebinp "state1.inp"          ! State 1 quantum input file
 envi stateaout "state0.out"          ! State 0 quantum output file
 envi statebout "state1.out"          ! State 1 quantum output file
!-----------------------------------------------------------------------

qchem remove sele resid 2 show end

! Compute unperturbed 
energy

coor force comp
print coor comp

! Call Pert and scale the charges of lambda 1 state
pert sele resid 1 end
scalar charge show
scalar charge mult 0.25 sele resid 1 end
scalar charge show

! Lambda 0
energy lambda 0.0
coor force comp
print coor comp

! Lambda 1
energy lambda 1.0
coor force comp
print coor comp

! Lambda 0.5
! Energy should be 1/2 of State 0 and State 1 
energy lambda 0.5
coor force comp
print coor comp

stop

