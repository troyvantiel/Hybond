* CHARMM c36a1 Testcase
* but4.inp - Generalized Hybrid Orbital Testcase for pKa perturbation.
* Kwangho Nam, 06-26-2008
*
  
stream datadir.def

if ?squantm .ne. 1 then stop
  
!
! Topology file
! -------------
OPEN READ FORMatted UNIT 1 NAME @0but.topo
READ RTF CARD UNIT 1
CLOSe UNIT 1
  
!
! Parameter file
! --------------
OPEN READ FORMatted UNIT 1 NAME @0qmlalk.para
READ PARAmeter CARD UNIT 1
CLOSe UNIT 1

!
! sequence 
! ----------------
READ SEQUence CARD 
* test butane
*
 1
BUTE

GENERATE BUTN SETUP

IC PARAM
IC SEED 1 C2 1 C3 1 C4
IC BUILD
IC FILL
IC PURGE
PRINT IC
PRINT COORD

COOR ORIE MASS sele all end

bomblev -10 
! -------------------------------------------
! let's define the quantum part of the system
! -------------------------------------------
define qmas sele BYNU 1:11 end
define qmbs sele BYNU 1:7 .or. BYNU 11 end

define mms sele .not. qmas end

!prnlev 8

nbonds vshift cutnb 12.0 ctofnb 11.0 ctonnb 10. 

energy

quantum sele qmas end glnk sele bynu 11 end -
     am1 charge 0 remo NOGA  -
     dual PERT sele qmas .and. .not. bynum 10 end -
               GLN2 sele bynu 11 end -
     khar -1 PKAP NOMM -
     REF0 0.50 PEF1 0.45 PEF2 0.55 TEMP 298.15

energy

nbonds vshift cutnb 12.0 ctofnb 11.0 ctonnb 10. 

mini abnr nstep 200 tolgrd 0.001 nprint 10

test first step 0.00001 tol 0.0000000001 sele all end

print coor

stop
