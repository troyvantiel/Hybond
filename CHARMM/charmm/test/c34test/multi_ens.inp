* CHARMM Testcase: c34test/multi_ens.inp
* Author:   Robert Best
* Date:     December 14, 2006
* Version:  c34a2
* Test:     exponential averaging of multiple force fields 
*

! This tests the exponential mixing of multiple force fields
! via:
!     exp(-bmix*E) = exp(-bmix*E1) + ... + exp(-bmix*EN)
! [ in case of N potentials E1,E2,..,EN ]
!
! In this case we have four processes 
! The system is two particles with a harmonic potential; the
! minimum and offsets of the potenials are varied for the
! different processes:
!
! E1 = 10*(r-3)**2
! E2 = 1.5 + 10*(r-4.5)**2
! E3 = 20*(r-5.7)**2
! E4 = 10*(r-7)**2

STREAM datadir.def

if ?ensemble .ne. 1 then
 echo "Test NOT performed."
 stop
endif

set nrep ?nensem
set node ?whoiam

! parameters for sp energy calcs ------------------------------
set xmin = 2
set xmax = 8
set nx = 300
calc dx = ( @xmax - @xmin ) / @nx

! read topology -----------------------------------------------
READ RTF CARD
* Topology info
*
   20   1
MASS 1   G1       100.000000
MASS 2   G2       100.000000

resi test 0.0
atom g1 g1 0.0
atom g2 g2 0.0
bond g1 g2

END

! setup parameters ---------------------------------------------
if @node .eq. 0 set rmin = 3
if @node .eq. 1 set rmin = 4.5
if @node .eq. 2 set rmin = 5.7
if @node .eq. 3 set rmin = 7
if @node .eq. 0 set kb = 10
if @node .eq. 1 set kb = 10
if @node .eq. 2 set kb = 20
if @node .eq. 3 set kb = 10

ensemble open unit 10 read card name @0twop_parm.inp
read param card unit 10
ensemble close unit 10

! generate system ---------------------------------------------
read sequ test 1
generate test

read coor card 
* coords
*
    2
    1    1 TEST G1     0.00000   0.00000   0.00000
    2    1 TEST G2    -1.00000   0.00000   0.00000


! setup exponential averaging -------------------------------------
open unit 50 write card name @9multi_expavg.dat
ensemble expavg beta 1.0 unit 50 -
	offset 0.0 1.5 0.0 0.0

! do s.p. calcs of E(r) vs r --------------------------------------
set inc = 0
label loopx
incr inc by 1
calc r = @xmin + @dx * @inc
scalar x set @r select bynu 2 end
energy
if @inc .lt. @nx goto loopx

! Use, e.g., the following to extract E(r) from the output file:
!
! awk '/scalar x set/{getline;x=$4}/ENER>/{print x,$3}' output/multi_ens.out | sed -e 's/"//g' 
!

stop
