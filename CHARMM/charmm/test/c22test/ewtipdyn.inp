* EWTIPDYN.INP : Stephen H. Fleischman, 04-Dec-91
* Test Ewald dynamics with water box.
*

stream datadir.def
set testname ewtipdyn
format (F20.7)

read rtf card
* TIPS3P WATER TOPOLOGY FILE
*
   20    1
MASS     4 HT     1.00800 ! TIPS3P WATER HYDROGEN
MASS    58 OT    15.999940 ! TIPS3P WATER OXYGEN

RESI TIP3    .000   ! TIPS3P WATER MODEL
GROUP
ATOM OH2  OT   -0.834 
ATOM H1   HT    0.417
ATOM H2   HT    0.417
BOND OH2 H1  OH2 H2  H1 H2 ! THE LAST BOND IS NEEDED FOR SHAKE
THET H1   OH2  H2 
!                            NOTE: NO DONORS OR ACCEPTORS FOR TIPS3P
PATC  FIRS NONE LAST NONE
END

bomlev -2
read param card
* TIPS3P WATER PARAMETER FILE
*
BOND
HT  OT     450.0   0.9572 ! from TIPS3P geometry
HT  HT       0.0   1.5139 ! from TIPS3P geometry (for SHAKE w/PARAM)
THETAS
HT  OT  HT  55.0 104.52   ! FROM TIPS3P GEOMETRY
!
NONBONDED   ATOM CDIEL SWITCH VATOM VDISTANCE VSWITCH -
     CUTNB 8.0  CTOFNB 7.5  CTONNB 6.5  EPS 1.0  E14FAC 1.0  WMIN 1.5
!
HT       0.0440    1.0       0.8000  
OT       0.8400    6.0       1.6000  

NBFIX
!OT  OT -0.152073  3.5365 ! TIPS3P VDW INTERACTION
!HT  OT -0.05      1.0    ! MODIFIED TIPS3P WITH CATASTROPHY ZONE REMOVED
!                           FOR NOW, USE CHARLIE'S PARAMETERS
OT OT -0.1521 3.5364
HT HT -0.04598 0.4490
OT HT -0.08363 1.9927

END
bomlev -1

! Read in 125 TIP3 solvent molecules and their
! equilibrated coordinates.

READ SEQU TIP3 125
GENE SOLV SETU NOANGLE

OPEN READ UNIT 10 CARD NAME @0tip125.crd
READ COOR UNIT 10 CARD
CLOSE UNIT 10
COOR ORIENT NOROT

! Set parameter 9 to 15.5516 A, the length of one side
! of the box of water.  This value will be used as a scale
! factor for all image transformations.

SET 9 15.5516

! Open and read the image transformation file for creating
! 26 image objects around the primary structure

CRYSTAL DEFINE CUBIC @9 @9 @9 90.0 90.0 90.0
CRYSTAL BUILD CUTOFF 14.0 NOPERATIONS 0
IMAGE BYRES XCEN 0.0 YCEN 0.0 ZCEN 0.0 SELE ALL END

!!!! try to optimize config first
SHAKE BONH PARAM
MINI SD   NSTEP 25 NPRINT 1 INBFRQ 5 IMGFRQ 5   -
 CUTNB 10.0  CTONNB 8.5 CTOFNB 9.5 CUTIM 10.0 VSWIT SHIFT
set tt 1
set sp minsd
set refval -1556.53398
@testcheck ?ener  @refval .000001 @testname_e_@tt_@sp

MINI ABNR NSTEP 25 NPRINT 1 INBFRQ 5 IMGFRQ 5   -
 CUTNB 10.0  CTONNB 8.5 CTOFNB 9.5 CUTIM 10.0 VSWIT SHIFT
set tt 2
set sp abnr
set refval -1620.22190
@testcheck ?ener  @refval .000001 @testname_e_@tt_@sp

!!!! VSWIT !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!! Slow
!
FAST OFF
UPDATE IMGFRQ 5   -
 CUTNB 10.0  CTONNB 8.5 CTOFNB 9.5 CUTIM 10.0 VSWIT SHIFT
SHAKE BONH PARAM  NOFAST
DYNAMICS NSTEPS 50 NPRINT 10 ECHECK 9999.0 -
 IMGFRQ 5 INBFRQ 5 EWALD KMAX 5 KSQMAX 27 KAPPA .333 -
 CUTNB 10.0  CTONNB 8.5 CTOFNB 9.5 CUTIM 10.0 VSWIT SHIFT FIRSTT 250.
set tt 3
set sp slowdyn
set refval -1270.60215
@testcheck ?tote  @refval .000001 @testname_e_@tt_@sp
!
!!!! Fast Scalar
!
FAST ON
UPDATE IMGFRQ 5   -
CUTNB 10.0  CTONNB 8.5 CTOFNB 9.5 CUTIM 10.0 VSWIT SHIFT
SHAKE BONH PARAM
DYNAMICS NSTEPS 50 NPRINT 10 ECHECK 9999.0 -
 IMGFRQ 5 INBFRQ 5 EWALD KMAX 5 KSQMAX 27 KAPPA .333 -
 CUTNB 10.0  CTONNB 8.5 CTOFNB 9.5 CUTIM 10.0 VSWIT SHIFT FIRSTT 250.
set tt 4
set sp fastdyn
set refval -1166.36832
@testcheck ?tote  @refval .000001 @testname_e_@tt_@sp
!
!!!! Vector/parallel
!

!!!! VSHIFT !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!! Slow
FAST OFF
UPDATE IMGFRQ 5   -
CUTNB 10.0  CTONNB 8.5 CTOFNB 9.5 CUTIM 10.0 VSHIF SHIFT
SHAKE BONH PARAM NOFAST
DYNAMICS NSTEPS 50 NPRINT 10 ECHECK 9999.0 -
 IMGFRQ 5 INBFRQ 5  EWALD KMAX 5 KSQMAX 27  KAPPA .333 -
 CUTNB 10.0  CTONNB 8.5 CTOFNB 9.5 CUTIM 10.0 VSHIF SHIFT FIRSTT 250.
set tt 5
set sp slowdyn
set refval -1101.49809
@testcheck ?tote  @refval .000001 @testname_e_@tt_@sp

!!!! Fast Scalar
FAST ON
UPDATE IMGFRQ 5   -
CUTNB 10.0  CTONNB 8.5 CTOFNB 9.5 CUTIM 10.0 VSHIF SHIFT
SHAKE BONH PARAM
DYNAMICS NSTEPS 50 NPRINT 10 ECHECK 9999.0 -
 IMGFRQ 5 INBFRQ 5  EWALD KMAX 5 KSQMAX 27  KAPPA .333 -
 CUTNB 10.0  CTONNB 8.5 CTOFNB 9.5 CUTIM 10.0 VSHIF SHIFT FIRSTT 250.
set tt 6
set sp fastdyn
set refval -1040.74853
@testcheck ?tote  @refval .000001 @testname_e_@tt_@sp
stop
!

STOP
