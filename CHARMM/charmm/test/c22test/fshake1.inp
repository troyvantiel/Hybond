* FSHAKE1.INP : Stephen Fleischman 11/91
* tests fast shake with both solute and waters.
*

stream datadir.def
set testname fshake1
format (F20.7)

READ RTF CARD
* Residue topology file for glycerol
* ...
*
   20    1
MASS     1 H      1.00800 ! hydrogen which can h-bond to neutral atom
MASS     2 HA     1.00800 ! aliphatic hydrogen
MASS     3 HT     1.00800 ! TIPS3P WATER HYDROGEN
MASS     4 CT    12.01100 ! aliphatic carbon
MASS     5 OH1   15.99940 ! hydroxy oxygen
MASS     6 OT    15.99940 ! TIPS3P WATER OXYGEN

! Autogenerate all angles in the topology definitions

AUTOGEN ANGLES

DEFAULT FIRST NONE LAST NONE    

! Topology definition for glycerol

RESIDUE GLYC  0.0    

GROUP                                 !  H11    O1 -- H1
       ATOM   C1   CT     0.05        !     \  /
       ATOM   H11  HA     0.10        !      C1
       ATOM   H12  HA     0.10        !     /  \ 
       ATOM   O1   OH1   -0.65        !  H12    \
       ATOM   H1   H      0.40        !         |         
GROUP                                 !         |  
       ATOM   C2   CT     0.15        !         |
       ATOM   H21  HA     0.10        !  H21 -- C2 -- O2 -- H2     
       ATOM   O2   OH1   -0.65        !         |
       ATOM   H2   H      0.40        !         |
GROUP                                 !         |
       ATOM   C3   CT     0.05        !  H31    /     
       ATOM   H31  HA     0.10        !     \  /
       ATOM   H32  HA     0.10        !      C3
       ATOM   O3   OH1   -0.65        !     /  \ 
       ATOM   H3   H      0.40        !  H32    O3 -- H3

BOND   C1   H11        C1   H12       C1   O1       O1   H1
BOND   C1   C2
BOND   C2   H21        C2   O2        O2   H2
BOND   C2   C3 
BOND   C3   H31        C3   H32       C3   O3       O3   H3       

DIHE   H1   O1   C1   C2
DIHE   O1   C1   C2   C3
DIHE   H2   O2   C2   C1
DIHE   O3   C3   C2   C1
DIHE   H3   O3   C3   C2

! Internal coordinate definitions 

IC     H1   O1   C1   C2       0.0000   0.00   180.00   0.00   0.0000
IC     O1   C1   C2   C3       0.0000   0.00   180.00   0.00   0.0000
IC     H2   O2   C2   C1       0.0000   0.00   180.00   0.00   0.0000
IC     O3   C3   C2   C1       0.0000   0.00   180.00   0.00   0.0000
IC     H3   O3   C3   C2       0.0000   0.00   180.00   0.00   0.0000
IC     O1   C2  *C1   H11      0.0000   0.00   120.00   0.00   0.0000
IC     O1   C2  *C1   H12      0.0000   0.00   240.00   0.00   0.0000
IC     C3   C1  *C2   O2       0.0000   0.00   120.00   0.00   0.0000
IC     C3   C1  *C2   H21      0.0000   0.00   240.00   0.00   0.0000
IC     O3   C2  *C3   H31      0.0000   0.00   120.00   0.00   0.0000
IC     O3   C2  *C3   H32      0.0000   0.00   240.00   0.00   0.0000

RESI TIP3    .000   ! TIPS3P WATER MODEL
GROUP
ATOM OH2  OT     -0.834
ATOM H1   HT      0.417
ATOM H2   HT      0.417
BOND OH2  H1        OH2  H2        H1   H2  ! THE LAST BOND IS NEEDED FOR SHAKE
ANGL H1  OH2  H2
!ACCE OH2
PATC  FIRS NONE LAST NONE

END

READ PARA CARD
* from PARAM19.PRM
*

BOND
CT   CT     200.0   1.53
CT   OH1    400.0   1.42
HA   CT     300.0   1.08 
H    OH1    450.0   0.96
HT   OT     450.0   0.9572 
HT   HT       0.0   1.5139 

THETAS
CT   CT   CT      45.0     111.00
CT   CT   HA      40.0     109.50
CT   CT   OH1     50.0     109.50
CT   OH1  H       35.0     109.5
HA   CT   HA      40.0     109.5
HA   CT   OH1     50.0     109.5
HT   OT   HT      55.0     104.52    ! FROM TIPS3P GEOMETRY

PHI
X    CT   CT   X        1.6       3       0.0
X    CT   OH1  X        0.5       3       0.0

NONBONDED  NBXMOD 5  ATOM CDIEL SHIFT VATOM VDISTANCE VSHIFT - 
     CUTNB 8.0  CTOFNB 7.5  CTONNB 6.5  EPS 1.0  E14FAC 0.4  WMIN 1.5 
H        0.0440    -0.0498    0.8000 
HA       0.1000    -0.0450    1.4680 
HT       0.0440    -0.0498    0.8000
CT       1.65    -0.0262       2.490   1.65   -0.1   1.9
! 
OH1      0.8400    -0.1591    1.6000
OT       0.8400    -0.1591    1.6000
 
NBFIX 
OT   OT      -0.152073  3.5365  ! TIPS3P VDW INTERACTION 
HT   HT      -0.04598   0.4490 
HT   OT      -0.08363   1.9927 

HBOND AEXP 4 REXP 6 HAEX 0 AAEX 0   NOACCEPTORS  HBNOEXCLUSIONS  ALL -
   CUTHB 0.5 CTOFHB 5.0 CTONHB 4.0  CUTHA 90.0  CTOFHA 90.0  CTONHA 90.0 
OH*  O*      -0.00 2.75 

END
! Generate the solute (one glycerol molecule); read in
! the initial coordinates created in Example 2.4.

READ SEQU CARD
* Glycerol
*
1
GLYC
GENE SOLU SETU

READ COOR CARD
* INITIAL COORDINATES FOR GLYCEROL GENERATED
*
   14
    1    1 GLYC C1     1.43000   0.00000   0.00000 GLYC 1      0.00000
    2    1 GLYC H11    1.77923  -0.52379  -0.88982 GLYC 1      0.00000
    3    1 GLYC H12    1.77923  -0.52379   0.88982 GLYC 1      0.00000
    4    1 GLYC O1     0.00000   0.00000   0.00000 GLYC 1      0.00000
    5    1 GLYC H1    -0.32045  -0.90494   0.00000 GLYC 1      0.00000
    6    1 GLYC C2     1.98189   1.43771   0.00000 GLYC 1      0.00000
    7    1 GLYC H21    1.63266   1.96150   0.88982 GLYC 1      0.00000
    8    1 GLYC O2     1.54236   2.15536  -1.15616 GLYC 1      0.00000
    9    1 GLYC H2     1.89801   3.04678  -1.13440 GLYC 1      0.00000
   10    1 GLYC C3     3.52189   1.43771   0.00000 GLYC 1      0.00000
   11    1 GLYC H31    3.88574   0.92397   0.88982 GLYC 1      0.00000
   12    1 GLYC H32    3.88574   0.92397  -0.88982 GLYC 1      0.00000
   13    1 GLYC O3     4.03435   2.77273   0.00000 GLYC 1      0.00000
   14    1 GLYC H3     4.99402   2.74760   0.00000 GLYC 1      0.00000

! Center the solute about the origin.

COOR ORIE

! Read in 125 TIP3 solvent molecules and their
! equilibrated coordinates.

READ SEQU TIP3 125
GENE SOLV SETU NOANGLE

OPEN READ UNIT 10 CARD NAME @0tip125.crd
READ COOR UNIT 10 CARD APPE
CLOSE UNIT 10

! Delete any waters which overlap the solute.

DELE ATOM SELE ( .BYRES. ( SEGID SOLV .AND. TYPE OH2 .AND. -
     ( ( .NOT. SEGID SOLV .AND. .NOT. HYDROGEN ) -
     .AROUND. 2.60 ) ) ) END

! Set parameter 6, 7 and 8 to 15.5516 A, the length of one side
! of the box of water.  This value will be used as a scale
! factor for all image transformations.
SET 6 15.5516
SET 7 15.5516
SET 8 15.5516

! Open and read the image transformation file for creating
! 26 image objects around the primary structure

OPEN READ UNIT 15 CARD NAME @0cubic.img
READ IMAG UNIT 15 CARD

! Apply image centering to all water molecules

IMAGE BYRES XCEN 0.0 YCEN 0.0 ZCEN 0.0 SELE SEGI SOLV END

! Minimize the structure and write the minimized coordinates

MINI ABNR NSTEP 50 TOLGRA 0.1 -
   CUTNB 10.0  CTOFNB 7.5  CTONNB 6.5  EPS 1.0  E14FAC 0.4  WMIN 1.5 -
   NBXMOD 5  ATOM CDIEL SHIFT VATOM VDISTANCE VSWIT

OPEN WRITE UNIT 10 CARD NAME @9glcsolmin.crd
WRITE COOR UNIT 10 CARD
* SOLVATED GLYCEROL: MINIMIZED WATER
*

ener
set tt 1
set sp noshake
set refval -1683.19847
@testcheck ?ener @refval .000001 @testname_e_@tt_@sp

SHAKE BONH param  NOFAST
ener
set tt 2
set sp shakenofast
set refval -1594.97596
@testcheck ?ener @refval .000001 @testname_e_@tt_@sp

COOR INIT
! Read the minimized coordinates
OPEN READ UNIT 08 CARD NAME @9glcsolmin.crd
READ COOR UNIT 08 CARD
CLOSE UNIT 8

! original shake:
! Shake all bonds with hydrogens
SHAKE BONH param  NOFAST

DYNA STRT VERL NSTE 100 TIME 0.001 IMGFRQ 50 -
   IPRFRQ 100 IHTFRQ 50 IEQFRQ 0 INBFRQ 50 IHBFRQ 0 -
   IUNREA -1 IUNWRI -1 IUNCRD -1 IUNVEL -1 KUNIT -1 -
   NPRINT 10 ISEED 314159 -
   FIRSTT 0.0 FINALT 300.0 TEMINC 5 -
   TWINDH 10.0 TWINDL -10.0 -
   IASORS 1 IASVEL 1 ICHECW 0 -
   NBXMOD 5  ATOM CDIEL SHIFT VATOM VDISTANCE VSWIT -
   CUTNB 8.0  CTOFNB 7.5  CTONNB 6.5  EPS 1.0  E14FAC 0.4  WMIN 1.5

set tt 3
set sp dynbonh
set refval -1603.64053
@testcheck ?tote @refval .000001 @testname_e_@tt_@sp

OPEN WRIT UNIT 8 CARD NAME @9GLYCDYNS.CRD
WRIT COOR UNIT 8 CARD
* GLYCEROL IN WATER COORDINATES FOR SHAKE BONH
*

! start over for fast shake
COOR INIT
! Read the minimized coordinates
OPEN READ UNIT 08 CARD NAME @9glcsolmin.crd
READ COOR UNIT 08 CARD
CLOSE UNIT 8

! Shake all water bonds
!SHAKE BONH PARAM FAST WATER TIP3 SELE RESNAME TIP3 END
! MFC changed this line since coords cannot be compared
!     if only water is selected for shake, will not match
!     the dynamics of previous dynamics

SHAKE BONH PARAM FAST WATER TIP3 

DYNA STRT VERL NSTE 100 TIME 0.001 IMGFRQ 50 -
   IPRFRQ 100 IHTFRQ 50 IEQFRQ 0 INBFRQ 50 IHBFRQ 0 -
   IUNREA -1 IUNWRI -1 IUNCRD -1 IUNVEL -1 KUNIT -1 -
   NPRINT 10 ISEED 314159 -
   FIRSTT 0.0 FINALT 300.0 TEMINC 5 -
   TWINDH 10.0 TWINDL -10.0 -
   IASORS 1 IASVEL 1 ICHECW 0 -
   NBXMOD 5  ATOM CDIEL SHIFT VATOM VDISTANCE VSWIT -
   CUTNB 8.0  CTOFNB 7.5  CTONNB 6.5  EPS 1.0  E14FAC 0.4  WMIN 1.5
set tt 4
set sp dynwat
set refval -1603.64053
@testcheck ?tote @refval .000001 @testname_e_@tt_@sp

OPEN read UNIT 41 CARD NAME @9GLYCDYNs.CRD
read COOR comp UNIT 41 CARD
close unit 41

COOR DIFF COMP
PRINT COOR COMP !sele resn glyc end
!PRINT COOR sele resn glyc end
STOP
