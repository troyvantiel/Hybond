* MEWTEST: Charlie L. Brooks III, December 9, 1991
* This input file illustrates and tests non-linear
* lambda scaling for use in perturbations which "disappear"
* an entire molecule.  In this case the methane -> nothing
* calculations is illustrated.
*

if ?qtsm .ne. 1 then
 echo "Test NOT performed."
 stop
endif

if ?NUMNODE .gt. 1 then
 echo "Test NOT performed."
 stop
endif

stream datadir.def
set testname mewtest
format (F20.7)

read rtf card
* CARBON TETRACHLORIDE AND METHANE TOPOLOGY FILE
* INCLUDES CCL4-METHANE HYBRID, TIP3 WATER, AND EXT-H HYDROCARBONS
*
   18    1
MASS     1 HT      1.00800 ! TIPS3P WATER HYDROGEN
MASS     2 OT     15.99940 ! TIPS3P WATER OXYGEN
MASS     3 CH4    16.03200

RESI TIP3  0.00000   ! TIPS3P WATER MODEL
GROUP
ATOM OH2  OT     -0.834 
ATOM H1   HT      0.417
ATOM H2   HT      0.417
BOND OH2  H1       OH2 H2        H1  H2  ! THE LAST BOND IS NEEDED FOR SHAKE
THET H1   OH2  H2 
PATC  FIRS NONE LAST NONE

RESI CH4   0.00000
GROUP
ATOM CH4  CH4     0.00000
PATC  FIRS NONE LAST NONE

END

read param card
* CARBON TETRACHLORIDE AND METHANE PARAMETER FILE
* INCLUDES CCL4-METHANE HYBRID, TIP3 WATER, AND EXT-H HYDROCARBONS
*
BOND
HT   OT     450.0       0.9572 ! from TIPS3P geometry
HT   HT       0.0       1.5139 ! from TIPS3P geometry (for SHAKE w/PARAM)
THETAS
HT   OT   HT      55.0     104.52    ! from TIPS3P geometry
!
! Non-bonded specs set up as in PARAM19.INP
NONBONDED   ATOM CDIEL SHIFT VATOM VDISTANCE VSHIFT -
     CUTNB 8.0  CTOFNB 7.5  CTONNB 6.5  EPS 1.0  E14FAC 0.4  WMIN 1.5
!
HT       0.0440    1.0       0.8000  
OT       0.8400    6.0       1.6000  
CH4      0.0000    -0.2940    2.0934 ! Jorgensen et. al. CP, 129, 193 (1989)
!
NBFIX
!OT   OT      -0.152073   3.5365     ! TIPS3P VDW INTERACTION
!HT   OT      -0.05       1.0        ! MODIFIED TIPS3P WITH
!                                      CATASTROPHY ZONE REMOVED 
!                                      FOR NOW, USE CHARLIE'S PARAMETERS
OT   OT    -0.1521  3.5364
HT   HT    -0.04598 0.4490
OT   HT    -0.08363 1.9927

END

read sequ card
* methane
*
1
ch4

generate me setup

read sequence tip3 245

generate wat noangles nodihedrals
                           
open unit 10 read form name @0mewtest.crd
read coor card unit 10
close unit 10

set 6 31.1032
set 7 15.5516
set 8 15.5516

open unit 10 read form name @0cubic.img
read image card unit 10
close unit 10

image byres xcen 0.0 ycen 0.0 zcen 0.0 sele segid wat end
image byseg xcen 0.0 ycen 0.0 zcen 0.0 sele segid me end

shake bonh tol 1.0e-8

faster on

set z1 1
set 1 1
set ttz 1  ! test number
set tnum 1
set 2 -0.05

set reftote1 -2498.16689
set reftote2 -2498.16689
set reftote3 -2343.66101
set reftote4 -2192.75776
set reftote5 -2064.47479
set reftote6 -1971.6458676
set reftote7 -1916.9440312
set reftote8 -1870.6177010
set reftote9 -1838.3807781
set reftote10 -1809.2324582
set reftote11 -1791.0927439

!====================================================
label LOOP
!====================================================

   goto oneincr
   label backoneincr

   incr 2 by 0.1
   goto tnumincr
   label backtnumincr
 
   tsm
    reac sele (atom me 1 ch4) end
    prod none
    lambda @2 power 2
   end
   
   dynamics verlet strt nstep 10 timestep 0.0015 -
    iprfrq 1000 ihtfrq 0 ieqfrq 200 ntrfrq 0 -
    iasors 1 ichecw 1 iasvel 1 -
    iunrea -1 iunwri -1 iuncrd -1 iunvel -1 kunit -1 -
    nprint 100 nsavc 0 nsavv 0 ihbfrq 0 inbfrq 10 imgfrq 10 -
    atom cdie shif vdis vswi cutnb 7.75 cutim 7.75 ctofnb 7.5 -
    firstt 200.0 finalt 298.0 twindh 5.0 twindl -5.0 -
    ilbfrq 0 rbuf 0.0 tbath 298.0
   
   open unit 12 write form name @9mew@1.prt
   
   tsm
    reac sele (atom me 1 ch4) end
    prod none
    lambda @2 power 4
    save unit 12 freq 1
   end
   
   title
   * mew job: methane to nothing in water; window @1
   *
   
   dynamics verlet strt nstep 10 timestep 0.0015 -
    iprfrq 1000 ihtfrq 0 ieqfrq 200 ntrfrq 0 -
    iasors 1 ichecw 1 iasvel 1 -
    iunrea -1 iunwri -1 iuncrd -1 iunvel -1 kunit -1 -
    nprint 100 nsavc 0 nsavv 0 ihbfrq 0 inbfrq 10 imgfrq 10 -
    atom cdie shif vdis vswi cutnb 7.75 cutim 7.75 ctofnb 7.5 -
    firstt 200.0 finalt 298.0 twindh 5.0 twindl -5.0 -
    ilbfrq 0 rbuf 0.0 tbath 298.0
   
   close unit 12
   
   set sp dynaener
   @testcheck ?tote @reftote@@tnum .00000001 @testname_@tnum_@sp

   !open unit 1 write form name @9mew@1.chr
   !write coor card unit 1
   !* coordinates from mew job; window @1
   !*                         
   
   if @2 lt 0.95 goto LOOP

!====================================================
! Now do post processing via perturbation method
!====================================================

open unit 10 form read name @9mew2.prt
open unit 11 form read name @9mew3.prt
open unit 12 form read name @9mew4.prt
open unit 13 form read name @9mew5.prt
open unit 14 form read name @9mew6.prt
open unit 15 form read name @9mew7.prt
open unit 16 form read name @9mew8.prt
open unit 17 form read name @9mew9.prt
open unit 18 form read name @9mew10.prt
open unit 19 form read name @9mew11.prt

tsm POST PSTAck 100 PLOT

PROC FIRST 10 NUNIT 1 LAMB 0.0 TEMP 298.0 DELT 10. BINS 2 CTEM
PROC FIRST 10 NUNIT 1 LAMB 0.1 TEMP 298.0 DELT 10. BINS 2 CTEM
PROC FIRST 11 NUNIT 1 LAMB 0.1 TEMP 298.0 DELT 10. BINS 2 CTEM
PROC FIRST 11 NUNIT 1 LAMB 0.2 TEMP 298.0 DELT 10. BINS 2 CTEM
PROC FIRST 12 NUNIT 1 LAMB 0.2 TEMP 298.0 DELT 10. BINS 2 CTEM
PROC FIRST 12 NUNIT 1 LAMB 0.3 TEMP 298.0 DELT 10. BINS 2 CTEM
PROC FIRST 13 NUNIT 1 LAMB 0.3 TEMP 298.0 DELT 10. BINS 2 CTEM
PROC FIRST 13 NUNIT 1 LAMB 0.4 TEMP 298.0 DELT 10. BINS 2 CTEM
PROC FIRST 14 NUNIT 1 LAMB 0.4 TEMP 298.0 DELT 10. BINS 2 CTEM
PROC FIRST 14 NUNIT 1 LAMB 0.5 TEMP 298.0 DELT 10. BINS 2 CTEM
PROC FIRST 15 NUNIT 1 LAMB 0.5 TEMP 298.0 DELT 10. BINS 2 CTEM
PROC FIRST 15 NUNIT 1 LAMB 0.6 TEMP 298.0 DELT 10. BINS 2 CTEM
PROC FIRST 16 NUNIT 1 LAMB 0.6 TEMP 298.0 DELT 10. BINS 2 CTEM
PROC FIRST 16 NUNIT 1 LAMB 0.7 TEMP 298.0 DELT 10. BINS 2 CTEM
PROC FIRST 17 NUNIT 1 LAMB 0.7 TEMP 298.0 DELT 10. BINS 2 CTEM
PROC FIRST 17 NUNIT 1 LAMB 0.8 TEMP 298.0 DELT 10. BINS 2 CTEM
PROC FIRST 18 NUNIT 1 LAMB 0.8 TEMP 298.0 DELT 10. BINS 2 CTEM
PROC FIRST 18 NUNIT 1 LAMB 0.9 TEMP 298.0 DELT 10. BINS 2 CTEM
PROC FIRST 19 NUNIT 1 LAMB 0.9 TEMP 298.0 DELT 10. BINS 2 CTEM
PROC FIRST 19 NUNIT 1 LAMB 1.0 TEMP 298.0 DELT 10. BINS 2 CTEM

END                         

! Now do post processing via thermodynamic integration method

open unit 10 form read name @9mew2.prt
open unit 11 form read name @9mew3.prt
open unit 12 form read name @9mew4.prt
open unit 13 form read name @9mew5.prt
open unit 14 form read name @9mew6.prt
open unit 15 form read name @9mew7.prt
open unit 16 form read name @9mew8.prt
open unit 17 form read name @9mew9.prt
open unit 18 form read name @9mew10.prt
open unit 19 form read name @9mew11.prt

tsm POST PSTAck 100 PLOT TI ENDPoints

PROC FIRST 10 NUNIT 1 TEMP 298.0 BINS 2 CTEM
PROC FIRST 11 NUNIT 1 TEMP 298.0 BINS 2 CTEM
PROC FIRST 12 NUNIT 1 TEMP 298.0 BINS 2 CTEM
PROC FIRST 13 NUNIT 1 TEMP 298.0 BINS 2 CTEM
PROC FIRST 14 NUNIT 1 TEMP 298.0 BINS 2 CTEM
PROC FIRST 15 NUNIT 1 TEMP 298.0 BINS 2 CTEM
PROC FIRST 16 NUNIT 1 TEMP 298.0 BINS 2 CTEM
PROC FIRST 17 NUNIT 1 TEMP 298.0 BINS 2 CTEM
PROC FIRST 18 NUNIT 1 TEMP 298.0 BINS 2 CTEM
PROC FIRST 19 NUNIT 1 TEMP 298.0 BINS 2 CTEM

END                         

@testcheck ?deltaa  3.1460150691288096  .00000001 @testname_delta_a
@testcheck ?deltae  3.3477441711552300  .00000001 @testname_delta_e
@testcheck ?deltas .0006769432 .000001 @testname_delta_s


!MFC dummy test for pass/fail to say we got this far
!    There is another test which is not very good above

@testcheck 1.0 1.0 .00000001 @testname_finish


stop



label tnumincr
incr ttz
if @ttz .ge.  2.0 set tnum 2
if @ttz .ge.  3.0 set tnum 3
if @ttz .ge.  4.0 set tnum 4
if @ttz .ge.  5.0 set tnum 5
if @ttz .ge.  6.0 set tnum 6
if @ttz .ge.  7.0 set tnum 7
if @ttz .ge.  8.0 set tnum 8
if @ttz .ge.  9.0 set tnum 9
if @ttz .ge. 10.0 set tnum 10
if @ttz .ge. 11.0 set tnum 11

gotom backtnumincr

label oneincr
incr z1
if @z1 .ge.  2.0 set 1 2
if @z1 .ge.  3.0 set 1 3
if @z1 .ge.  4.0 set 1 4
if @z1 .ge.  5.0 set 1 5
if @z1 .ge.  6.0 set 1 6
if @z1 .ge.  7.0 set 1 7
if @z1 .ge.  8.0 set 1 8
if @z1 .ge.  9.0 set 1 9
if @z1 .ge. 10.0 set 1 10
if @z1 .ge. 11.0 set 1 11

goto backoneincr
