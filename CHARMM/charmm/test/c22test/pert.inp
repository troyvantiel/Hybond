* PERT.INP
* author:  Ryszard Czerminski (December 11, 1991)
* purpose: to test PERT command (slow growth method)
* problem: calculates free energy perturbation for migrating -OH group
*          in methanol (CH3-OH -> OH-CH3)
* external files: none
*

if ?pert .ne. 1 then
 echo "Test NOT performed."
 stop
endif

if ?novibran .eq. 1 then
 echo "Test NOT performed."
 stop
endif

stream datadir.def

bomlev -3 ! to pass non-integer charge warning 7/21/97

read rtf  card
* TOPOLOGY FILE FOR METHANOL TO METHANOL
*
   20    1                ! Version number
MASS     1 H      1.00800 ! Hydrogen bonding hydrogen (neutral group)
MASS     3 HA     1.00800 ! Aliphatic or aromatic non-hydrogen bonding hydrogen
MASS     4 HT     1.00800 ! TIPS3P water model hydrogen
MASS    10 CT    12.01100 ! Aliphatic carbon  -sp3
MASS    45 OT    15.99940 ! Hydroxyl oxygen (tetrahedral)/Ionizable acid oxygen
MASS    46 OW    15.99940 ! TIP3P water model oxygen
MASS    98 DU1   3.00800  ! dummy for hydrogens 
MASS    99 DU2   15.0350  ! dummy for carbon 

AUTOGENERATE ANGLES
DEFA FIRS NTER LAST CTER

RESI MNL            0.00  !  methanol residue
GROU
ATOM C    CT     -0.2
ATOM HA   HA      0.2  
ATOM HB   HA      0.2  
GROU
ATOM O1   OT     -0.8
ATOM H1   H       0.4
ATOM O2   HA      0.2
ATOM H2   DU1     0.
BOND C HA   C HB    C O1   C O2   O1 H1   O2 H2
DIHE H1 O1 C HA  
DIHE H1 O1 C HB
DIHE H1 O1 C O2
DIHE H2 O2 C HA  
DIHE H2 O2 C HB
DIHE H2 O2 C O1
DONO  H2 O2
DONO  H1 O1
ACCE  O1
ACCE  O2
IC    BLNK H1   O1     C     0.00     0.00   180.00    0.00   0.00
IC    H1   O1   C      HA    0.00     0.00    60.00    0.00   0.00
IC    H1   O1   C      HB    0.00     0.00   240.00    0.00   0.00
IC    H2   O2   C      HA    0.00     0.00   240.00    0.00   0.00
IC    H2   O2   C      HB    0.00     0.00    60.00    0.00   0.00
IC    H2   O2   C      O1    0.00     0.00   180.00    0.00   0.00
IC    H1   O1   C      O2    0.00     0.00   180.00    0.00   0.00
PATCH FIRST NONE LAST NONE

!
! Water models
RESI TIP3    .000   !  TIPS3P
GROUP
 ATOM OH2  OW     -0.834
 ATOM H1   HT      0.417
 ATOM H2   HT      0.417
BOND OH2  H1        OH2  H2        H1   H2  ! THE LAST BOND IS NEEDED FOR SHAKE
ANGLE  H1 OH2 H2
PATC  FIRS NONE LAST NONE
END

read parameter card 
* From Parameter File for CHARMM version 20
* Copyright (c) 1986  Polygen Corporation  All Rights Reserved
* Includes parameters for both polar and all hydrogen topology files
*
BOND
CT   CT     225.0       1.540
CT   OT     400.0       1.420
OT   H      450.0       0.960
HA   CT     345.0       1.09
HT   HT       0.0       1.5139
HT   OW     450.0       0.9572
! bond perturbation
OT   DU1    345.0       0.2 ! perturbation OT   H      450.0       0.960
HA   DU1    345.0       0.2 ! perturbation OT   H      450.0       0.960

THETAS
HA   CT   CT      41.0     109.47
HA   CT   HA      34.0     109.47
HA   CT   OT      44.0     109.47
H    OT   CT      49.0     109.47
HT   HT   OW       0.0     127.74
HT   OW   HT      55.0     104.5
! angle perturbation
H    OT   DU1      3.40    109.47  ! perturbation HA   CT   HA      34.0     109.47
DU1  OT   DU1      0.34    109.47  ! perturbation HA   CT   HA      34.0     109.47
DU1  OT   CT       4.90    109.47  ! perturbation H    OT   CT      49.0     109.47
DU1  HA   CT       4.9     109.47  ! perturbation H    OT   CT      49.0     109.47

PHI
X    CT   CT   X        1.6  3    0.0
X    CT   OT   X        0.5  3    0.0
! dihedral perturbation
X    CT   HA   X        0.1  3    0.0

! STANDARD DEFAULTS
NONBONDED  NBXMOD 5  ATOM CDIEL SWIT VATOM VDISTANCE VSWIT -
     CUTNB 99.0  CTOFNB 98.0  CTONNB 97.0  EPS 1.0  E14FAC 0.5  WMIN 1.5
!
! The following nonbonded parameters have not been tested or verified.
!
!                  Emin         Rmin    (these columns used
!               (kcal/mol)       (A)     for 1-4 interactions)
!
H        0.044   -0.0498       0.800
HA       0.100   -0.00450      1.368
HT       0.044   -0.0498       0.800
CT       1.65    -0.0903       1.800
OT       0.84    -0.1591       1.600
OW       0.84    -0.1591       1.600
DU*      0.10     0.0          1.0
!

NBFIX
!                  Emin       Rmin
!                  (kcal/mol) (A)
!
OW   OW      -0.152073  3.5365
HT   HT      -0.04598   0.4490
HT   OW      -0.08363   1.9927
!
!STANDARD DEFAULTS
HBOND AEXP 4 REXP 6 HAEX 4 AAEX 2   NOACCEPTORS  HBNOEXCLUSIONS  ALL  -
   CUTHB 0.5 CTOFHB 5.0 CTONHB 4.0  CUTHA 90.0  CTOFHA 90.0  CTONHA 90.0
!
O*   O*      -4.25 2.75
!
END

read sequence card
*...
*
    1
mnl
generate meth setup warn first none last none

scalar mass   set  8.     sele atom meth 1 o1 show end
scalar mass   set  3.     sele atom meth 1 h1 show end
scalar mass   set  8.     sele atom meth 1 o2 show end
scalar mass   set  3.     sele atom meth 1 h2 show end

!
! Building cartesians from internal coordinates and
! minimizing has been replaced by reading pre-minimized coordinates
! directly from the input. In this way we can decouple modifications
! in minimizers from modifications in dynamics and/or pert module.
! Ryszard Czerminski (92/05/18)
!
!ic param   ! to fill the table with parameter values
!coor set sele atom meth 1 c end 
!coor set sele atom meth 1 o1 end xdir 1.42 ydir 0.0 zdir 0.0
!coor set sele atom meth 1 h1 end xdir 1.92 ydir 0.89 zdir 0.0
!ic build
read coor card
* initial minimized coordinates
*
    7
    1    1 MNL  C     -0.15841   0.34707   0.00000 METH 1      0.00000
    2    1 MNL  HA    -0.21747   0.97522  -0.89112 METH 1      0.00000
    3    1 MNL  HB    -0.21747   0.97522   0.89112 METH 1      0.00000
    4    1 MNL  O1     1.05536  -0.40295   0.00000 METH 1      0.00000
    5    1 MNL  H1     1.83183   0.16859   0.00000 METH 1      0.00000
    6    1 MNL  O2    -1.00784  -0.33920   0.00000 METH 1      0.00000
    7    1 MNL  H2    -1.17819  -0.23442   0.00000 METH 1      0.00000

print psf
test psf

update inbfrq 10000 ihbfrq 0 cdie cutnb 999.
!mini sd nstep 100 nprint 10 tolg 1.
!mini nrap nstep 100 nprint 100 step 0.1 tolg 0.0000001 tols 0.0000001
!coor orie mass

! Not sure what vibran analysis doing in pert job, removing. clb3
!vibran
!diag
!thermo temp 300.0 mode 7 thru 21
!end

pert
scalar type   set  3      sele atom meth 1 o1 show end
scalar type   set 98      sele atom meth 1 h1 show end
scalar type   set 45      sele atom meth 1 o2 show end
scalar type   set  1      sele atom meth 1 h2 show end
scalar charge set  0.2    sele atom meth 1 o1 show end
scalar charge set  0.0    sele atom meth 1 h1 show end
scalar charge set -0.8    sele atom meth 1 o2 show end
scalar charge set  0.4    sele atom meth 1 h2 show end
print psf

set 1 31459

open unit 88 write form name scratch/slow15.punit
write title unit 88
** punit file for slow growth test cases
**
*  LSTART  0.00000  LSTOP  0.01000  PSTART  340    PSTOP  360    PSLOW
*  LSTART  0.01000  LSTOP  0.02000  PSTART  360    PSTOP  380    PSLOW
*

set s = 0.02
set e = 0.04
set s1 = 380
set e1 = 400
label writeit                                                                
write title unit 88
*  LSTART  @s  LSTOP  @e  PSTART  @s1    PSTOP  @e1    PSLOW

incr s by 0.02
incr e by 0.02
incr s1 by 20
incr e1 by 20
if e le 0.96 goto writeit

write title unit 88
*  LSTART  0.96000  LSTOP  0.97000  PSTART 1320    PSTOP 1340    PSLOW
*  LSTART  0.97000  LSTOP  0.98000  PSTART 1340    PSTOP 1360    PSLOW
*  LSTART  0.98000  LSTOP  0.98500  PSTART 1360    PSTOP 1380    PSLOW
*  LSTART  0.98500  LSTOP  0.99000  PSTART 1380    PSTOP 1400    PSLOW
*  LSTART  0.99000  LSTOP  0.99100  PSTART 1400    PSTOP 1410    PSLOW
*  LSTART  0.99100  LSTOP  0.99200  PSTART 1410    PSTOP 1420    PSLOW
*  LSTART  0.99200  LSTOP  0.99300  PSTART 1420    PSTOP 1430    PSLOW
*  LSTART  0.99300  LSTOP  0.99400  PSTART 1430    PSTOP 1440    PSLOW
*  LSTART  0.99400  LSTOP  0.99500  PSTART 1440    PSTOP 1450    PSLOW
*  LSTART  0.99500  LSTOP  0.99600  PSTART 1450    PSTOP 1460    PSLOW
*  LSTART  0.99600  LSTOP  0.99700  PSTART 1460    PSTOP 1470    PSLOW
*  LSTART  0.99700  LSTOP  0.99800  PSTART 1470    PSTOP 1480    PSLOW
*  LSTART  0.99800  LSTOP  0.99900  PSTART 1480    PSTOP 1490    PSLOW
*  LSTART  0.99900  LSTOP  0.99950  PSTART 1490    PSTOP 1495    PSLOW
*  LSTART  0.99950  LSTOP  1.00000  PSTART 1495    PSTOP 1500    PSLOW

open read  card unit 88 name scratch/slow15.punit
dyna verlet leap strt nstep 1500 timestep 0.001 -
    iprfrq 1000 ihtfrq 0 ieqfrq 500 ntrfrq 0  -
    iuncrd -31 iseed @1 -
    nprint 1000 nsavc 0 nsavv 0 -
    firstt 300.0 finalt 300.0 teminc 30.0   -
    iasors 0 iasvel 1 iscvel 0 ichecw 0 twindh 5.0 twindl -5.0 -
    punit 88 wmin 1.0

print energy
pert off

!Again not sure what this tests, removing. clb3
!mini sd   nstep 100 nprint 20 tolg  0.5
!mini nrap nstep 100 nprint 100 step 0.1 tolg 0.0000001 tols 0.0000001
!coor orie mass
!vibran
!diag
!thermo temp 300.0 mode 7 thru 24
!end

ic fill
ic print

format (F9.5)
write title unit 6
* final methanol structure ENERGY=?ener
* BOND=?BOND
* ANGL=?ANGL
* DIHE=?DIHE
* ELEC=?ELEC
* VDW =?VDW
*
print coor
stop
