* ICPERT: Charlie L. Brooks III, 09-Dec-91
* Test the internal coordinate constraint and perturbation routines.
* We use the TSM i.c. constraint and perturbation methods
* to calculate the conformational thermodynamics for changing
* phi and psi of the alanine dipeptide (N-methylalanylacetamide).
* The peptide is placed in a box of water so we can test the image
* interaction energy part of the perturbation routines.
*

if ?qtsm .ne. 1 then
 echo "Test NOT performed."
 stop
endif

if ?NUMNODE .gt. 1 then
 echo "Test NOT performed."
 stop
endif

stream datadir.def
set testname icpert
format (F20.7)

read rtf card
* Alanine Topology File. (TOPH19.RTF)
*
    19    1
  MASS     1 H      1.00800 ! hydrogen which can h-bond to neutral atom
  MASS     4 HT     1.00800 ! TIPS3P WATER HYDROGEN
  MASS    11 C     12.01100 ! carbonyl carbon
  MASS    12 CH1E  13.01900 ! extended atom carbon w/ one hydrogen
  MASS    14 CH3E  15.03500 !   -    "    -           three
  MASS    38 NH1   14.00670 ! peptide nitrogen bound to one hydrogen
  MASS    51 O     15.99940 ! carbonyl oxygen
  MASS    58 OT    15.99940 ! TIPS3P WATER OXYGEN

  DECL -C
  DECL -O
  DECL +N
  DECL +H
  DECL +CA

  AUTOGENERATE ANGLES
  DEFA FIRS NTER LAST CTER

  RESI ALA     0.00000
  GROU
  ATOM N    NH1    -0.35
  ATOM H    H       0.25
  ATOM CA   CH1E    0.10
  GROU
  ATOM CB   CH3E    0.00
  GROU
  ATOM C    C       0.55
  ATOM O    O      -0.55
  BOND N    CA        CA   C         C    +N        C    O         N    H
  BOND CA   CB
  DIHE -C   N    CA   C         N    CA   C    +N        CA   C    +N   +CA
  IMPH N    -C   CA   H         C    CA   +N   O         CA   N    C    CB
  DONO  H  N
  ACCE O  C
  IC   -C   CA   *N   H      0.0000    0.00  180.00    0.00   0.0000
  IC   -C   N    CA   C      0.0000    0.00  180.00    0.00   0.0000
  IC   N    CA   C    +N     0.0000    0.00  180.00    0.00   0.0000
  IC   +N   CA   *C   O      0.0000    0.00  180.00    0.00   0.0000
  IC   CA   C    +N   +CA    0.0000    0.00  180.00    0.00   0.0000
  IC   N    C    *CA  CB     0.0000    0.00  120.00    0.00   0.0000

  RESI ACE     0.00000 ! Acetyl blocking group with standard naming.
  GROU
  ATOM CH3  CH3E    0.00
  GROU
  ATOM C    C       0.55
  ATOM O    O      -0.55
  BOND CH3  C         C    +N        C    O
  DIHE CH3  C    +N   +CA
  IMPH C    CH3  +N   O
  ACCE O C
  IC   +N   CH3  *C   O      0.0000    0.00  180.00    0.00   0.0000
  IC   CH3  C    +N   +CA    0.0000    0.00  180.00    0.00   0.0000
  PATC  FIRS NONE LAST NONE

  RESI CBX     0.00000
  GROU
  ATOM N    NH1    -0.35
  ATOM H    H       0.25
  ATOM CA   CH3E    0.10
  BOND N    CA        N    H
  IMPH N    -C   CA   H
  DONO  H    N
  IC   -C   CA   *N   H      0.0000    0.00  180.00    0.00   0.0000
  PATC  FIRS NONE LAST NONE

  RESI TIP3    .000   ! TIPS3P WATER MODEL
  GROUP
  ATOM OH2  OT     -0.834
  ATOM H1   HT      0.417
  ATOM H2   HT      0.417
  BOND OH2  H1  OH2  H2   H1  H2  ! THE LAST BOND IS NEEDED FOR SHAKE
  ANGLE  H1 OH2 H2
  ACCE OH2
  PATC  FIRS NONE LAST NONE

END

Read param card
* Parameter file for alanine. (PARAM19.PRM)
*

 BONDS
  C    CH1E   405.0       1.52
  C    CH3E   405.0       1.52
  C    NH1    471.0       1.33
  C    O      580.0       1.23
  CH1E CH3E   225.0       1.52
  CH1E NH1    422.0       1.45
  CH3E NH1    422.0       1.49
  H    NH1    405.0       0.98!  GELIN AND IR STRETCH 3200 CM 1
  HT   OT     450.0       0.9572 ! from TIPS3P geometry
  HT   HT       0.0       1.5139 ! from TIPS3P geometry (for SHAKE w/PARAM)
 THETAS
  CH1E C    NH1     20.0     117.5
  CH1E C    O       85.0     121.5
  CH3E C    NH1     20.0     117.5
  CH3E C    O       85.0     121.5
  NH1  C    O       65.0     121.0
  C    CH1E CH3E    70.0     106.5
  C    CH1E NH1     45.0     111.6
  CH3E CH1E NH1     65.0     108.5
  C    NH1  CH1E    77.5     120.0
  C    NH1  CH3E    77.5     120.0
  C    NH1  H       30.0     120.0
  CH1E NH1  H       35.0     120.0
  CH3E NH1  H       35.0     120.0
  HT   OT   HT      55.0     104.52    ! FROM TIPS3P GEOMETRY
 PHI
  X    C    CH1E X        0.0       3       0.0! FROM GELIN THESIS AMIDES
  X    C    NH1  X        8.2       2     180.0! THE EXPERIMENTAL BARRIER.
  X    CH1E NH1  X        0.3       3       0.0
 IMPHI
  C    X    X    O      100.0    0     0.0
  CH1E X    X    CH3E    55.0    0   35.26439
  NH1  X    X    H       45.0    0     0.0! AMIDES PROTON OOP
 NONBONDED  NBXMOD 5  ATOM CDIEL SHIFT VATOM VDISTANCE VSHIFT -
     CUTNB 8.0  CTOFNB 7.5  CTONNB 6.5  EPS 1.0  E14FAC 0.4  WMIN 1.5
!
!                    Emin      Rmin
!                 (kcal/mol)    (A)
  H        0.0440  -0.0498    0.8000 
  HT       0.0440    -0.0498    0.8000 !TIP3P water hydrogen, see NBFIX below
  C        1.65    -0.1200    2.100   1.65 -0.1 1.9
  CH1E     1.35    -0.0486    2.365   1.35 -0.1 1.9
  CH3E     2.17    -0.1811    2.165   1.77 -0.1 1.9
  N*       1.1000  -0.2384    1.6000   ! includes N,NC2,NH1,NH2,NH3,NP,and NR
  O*       0.8400  -0.1591    1.6000   ! includes O, OH1, OM, and OS
  OT       0.8400    -0.1591    1.6000 !TIP3P water oxygen,   see NBFIX below
 NBFIX
!
!	We're gonna NBFIX the TIP3P water-water interactions
!	here to make them more like Jorgensen's.  The vdW parameters
!	specified above will be in effect, therefore, for ONLY
!	protein (read, protein OR nucleic acid)-water interactions.
!	OT-OT is exactly Jorgensen's; HT interactions are added
!	here.
!                Emin      Rmin
!              (kcal/mol)   (A)
  OT   OT      -0.152073  3.5365  ! TIPS3P VDW INTERACTION
  HT   HT      -0.04598   0.4490
  HT   OT      -0.08363   1.9927
 HBOND AEXP 4 REXP 6 HAEX 0 AAEX 0   NOACCEPTORS  HBNOEXCLUSIONS  ALL  -
     CUTHB 0.5 CTOFHB 5.0 CTONHB 4.0  CUTHA 90.0  CTOFHA 90.0  CTONHA 90.0
  N*+* O*          -0.00      2.9

END

! Generate the dipeptide seqment.
read sequ card
* N-methylalanylacetamide (the alanine dipeptide)
* constructed as a blocked alanine residue
*
3
ace ala cbx

generate ala setup

read coor card
* ALANINE DIPEPTIDE WITH PHI,PSI ~= -85,100
*  DATE:     7/ 1/90      0:40:30      CREATED BY USER: TOBIAS
*
   12
    1    1 ACE  CH3    3.06258   0.64613   1.42088 ALA  1      0.00000
    2    1 ACE  C      2.33541  -0.68685   1.35313 ALA  1      0.00000
    3    1 ACE  O      2.01413  -1.29380   2.37725 ALA  1      0.00000
    4    2 ALA  N      2.07725  -1.18175   0.14371 ALA  2      0.00000
    5    2 ALA  H      2.45870  -0.76210  -0.65152 ALA  2      0.00000
    6    2 ALA  CA     1.35635  -2.43045  -0.00242 ALA  2      0.00000
    7    2 ALA  CB     0.69707  -2.49721  -1.37506 ALA  2      0.00000
    8    2 ALA  C      2.38192  -3.54475   0.11749 ALA  2      0.00000
    9    2 ALA  O      3.17467  -3.80389  -0.78914 ALA  2      0.00000
   10    3 CBX  N      2.41984  -4.12094   1.31507 ALA  3      0.00000
   11    3 CBX  H      1.92248  -3.68658   2.04150 ALA  3      0.00000
   12    3 CBX  CA     3.28397  -5.30373   1.59827 ALA  3      0.00000

ic fill
ic print

! Adjust the dihedrals to the values we want them constrained.
set 1 -85.0    ! phi
set 2 100.0    ! psi

! Edit phi and psi and build the new coordinates.
ic edit
 dihe 1 c 2 n 2 ca 2 c @1
 dihe 2 n 2 ca 2 c 3 n @2
end
coor init sele (bynum 1:3) end
ic build
coor init sele (bynum 10:12) end
ic build
ic print

! Dunk the peptide and delete overlapping waters.

coor orie norot sele segi ala end

read sequ tip3 125

generate wat noangle setup           

open read unit 30 form name @0tip125.crd
read coord card append unit 30
close unit 30
                                
dele atom sele .byres. (segi wat .and. -
   ((.not. segid wat .and. .not. (hydrogen .or. lone)) -
	     			        .around. 2.8)) end

! Shake bonds to hydrogen.
shake bonh param tol 1.0e-8

! Set up image stuff.

set 6 15.5516
set 7 15.5516
set 8 15.5516

open read unit 30 form name @0cubic.img
read image card unit 30
close unit 30

image byres xcen 0.0 ycen 0.0 zcen 0.0 sele segid wat end
image byseg xcen 0.0 ycen 0.0 zcen 0.0 sele segid ala end

! Set up internal coordinate constraints to fix the dihedral angles at
! their current values.  This is done with a "tsm" command.
! We set the tolerance for the constraints to 10**-5 degrees, and the
! maximum number of iterations to 100.
tsm
 fix dihe ala 1 c ala 2 n ala 2 ca ala 2 c toli 1.0e-5
 fix dihe ala 2 n ala 2 ca ala 2 c ala 3 n toli 1.0e-5
 maxi 100
end

! Equilibration dynamics for 10 steps.
dynamics verlet strt nstep 10 timestep 0.001 -
 inbfrq 10 imgfrq 10 cutnb 7.75 nprint 1 -
 firstt 240.0 finalt 300.0 twindh 5.0 twindl -5.0 -
 atom cdie shif vdis vswi

! Set up the perturbations.  We'll do four interaction energy
! calculations (we'll get four free energy points), for phi and
! psi changes of +/- 5 and 10 degrees.  We save them each step on
! unit 10.  The "move" selection specifies which atoms are to be
! moved when the internal coordinate is changed; "by" is the
! maximum range of the perturbation (10 degrees in this example).
tsm
 savi icun 10 icfr 1 nwin 2
 move dihe ala 1 c ala 2 n ala 2 ca ala 2 c by -10.0 -
   inte sele (bynum 1:5) end
 move dihe ala 2 n ala 2 ca ala 2 c ala 3 n by -10.0 -
   inte sele (bynum 8:12) end
end

! Open a file for the interaction energies.
open unit 10 write form name @9icpert.icp

! Production dynamics for 10 steps.
dynamics verlet strt nstep 10 timestep 0.001 -
 inbfrq 10 imgfrq 10 cutnb 7.75 nprint 1 -
 firstt 240.0 finalt 300.0 twindh 5.0 twindl -5.0 -
 atom cdie shif vdis vswi

close unit 10

! Clear the TSM data structures.
tsm clear

! Process the perturbation interaction energies to get the
! free energy, internal energy, and entropy as functions
! of the "perturbed" internal coordinates.

! Open the data file.
open unit 10 read form name @9icpert.icp

! Another "tsm" command is used.  "ic" specifies that these are
! internal coordinate perturbations; "surf" indicates that the
! free energy, etc. should be printed as functions of the ic's at
! the end of the processing; "inte" specifies that we want
! average interaction energies and total energies to be computed
! and printed out; the temperature and delta T used in the
! thermodynamic calculations are 300 K and 10 K, respectively.
! "ctem" specifies that we want to see the average temperature
! computed from the kinetic energy; "bins" is the number of steps per
! batch mean used in the calculation of the statistical errors; we'll
! use 2 batch means in the error calculation, each with 5 steps; "begin"
! and "stop" refer to dataset numbers, rather than dynamics steps
! (in this case they're the same).

tsm post ic surf inte maxp 2 maxw 2 maxs 100
 proc firs 10 nuni 1 bins 5 ctem temp 300.0 delt 10.0 -
  begin 1 stop 10
end

!MFC this is a dummy test pass/fail, only tests if we got this far.
! I cannot find an easy way to check if the output is correct.
set tt 1
set sp dummy
@testcheck 1.0 1.0 .00001 @testname_@tt_@sp

stop
