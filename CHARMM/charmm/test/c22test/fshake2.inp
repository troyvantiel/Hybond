* FSHAKE2.INP: Stephan Fleischman, 11/91
* water dynamics shake test.
*

stream datadir.def
set testname fshake2
format (F20.7)

read rtf card
* TIPS3P WATER TOPOLOGY FILE
*
   20    1
MASS     4 HT     1.00800 ! TIPS3P WATER HYDROGEN
MASS    58 OT    15.99940 ! TIPS3P WATER OXYGEN

RESI TIP3    .000   ! TIPS3P WATER MODEL
GROUP
ATOM OH2  OT   -0.834 
ATOM H1   HT    0.417
ATOM H2   HT    0.417
BOND OH2 H1  OH2 H2  H1 H2 ! THE LAST BOND IS NEEDED FOR SHAKE
THET H1   OH2  H2 
!                            NOTE: NO DONORS OR ACCEPTORS FOR TIPS3P
PATC  FIRS NONE LAST NONE
END

bomlev -2
read param card
* TIPS3P WATER PARAMETER FILE
*
BOND
HT  OT     450.0   0.9572 ! from TIPS3P geometry
HT  HT       0.0   1.5139 ! from TIPS3P geometry (for SHAKE w/PARAM)
THETAS
HT  OT  HT  55.0 104.52   ! FROM TIPS3P GEOMETRY
!
NONBONDED   ATOM CDIEL SWITCH VATOM VDISTANCE VSWITCH -
     CUTNB 8.0  CTOFNB 7.5  CTONNB 6.5  EPS 1.0  E14FAC 1.0  WMIN 1.5
!
HT       0.0440    1.0       0.8000  
OT       0.8400    6.0       1.6000  

NBFIX
!OT  OT -0.152073  3.5365 ! TIPS3P VDW INTERACTION
!HT  OT -0.05      1.0    ! MODIFIED TIPS3P WITH CATASTROPHY ZONE REMOVED
!                           FOR NOW, USE CHARLIE'S PARAMETERS
OT OT -0.1521 3.5364
HT HT -0.04598 0.4490
OT HT -0.08363 1.9927

END
bomlev  0

! Read in 125 TIP3 solvent molecules and their
! equilibrated coordinates.

READ SEQU TIP3 125
GENE SOLV SETU NOANGLE NODIHED

OPEN READ UNIT 10 CARD NAME @0tip125.crd
READ COOR UNIT 10 CARD
CLOSE UNIT 10

! Set parameter 9 to 15.5516 A, the length of one side
! of the box of water.  This value will be used as a scale
! factor for all image transformations.

SET 6 15.5516
SET 7 15.5516
SET 8 15.5516

! Open and read the image transformation file for creating
! 26 image objects around the primary structure

OPEN READ UNIT 15 CARD NAME @0cubic.img
READ IMAG UNIT 15 CARD

! Apply image centering to all water molecules

IMAGE BYRES XCEN 0.0 YCEN 0.0 ZCEN 0.0 SELE SEGI SOLV END

! original shake:
! Shake all bonds with hydrogens
SHAKE BONH NOFAST

DYNA STRT VERL NSTE 100 TIME 0.001 IMGFRQ 50 -
   IPRFRQ 100 IHTFRQ 50 IEQFRQ 0 INBFRQ 50 IHBFRQ 0 -
   IUNREA -1 IUNWRI -1 IUNCRD -1 IUNVEL -1 KUNIT -1 -
   NPRINT 10 ISEED 314159 -
   FIRSTT 0.0 FINALT 300.0 TEMINC 5 -
   TWINDH 10.0 TWINDL -10.0 -
   IASORS 1 IASVEL 1 ICHECW 0 -
   NBXMOD 5  ATOM CDIEL SHIFT VATOM VDISTANCE VSWIT -
   CUTNB 8.0  CTOFNB 7.5  CTONNB 6.5  EPS 1.0  E14FAC 0.4  WMIN 1.5
set tt 1
set sp dynslowshk
set refval -1472.27378
@testcheck ?tote @refval .000001 @testname_@tt_@sp


COOR COPY COMP

OPEN WRIT UNIT 41 CARD NAME @9WATERDYNS.CRD
WRIT COOR UNIT 41 CARD
* COORDINATES FOR SHAKE BONH
*

! start over for fast shake
COOR INIT
OPEN READ UNIT 14 CARD NAME @0tip125.crd
READ COOR UNIT 14 CARD
CLOSE UNIT 14

! Shake all bonds with hydrogens
SHAKE BONH FAST WATER TIP3 SELE ALL END

DYNA STRT VERL NSTE 100 TIME 0.001 IMGFRQ 50 -
   IPRFRQ 100 IHTFRQ 50 IEQFRQ 0 INBFRQ 50 IHBFRQ 0 -
   IUNREA -1 IUNWRI -1 IUNCRD -1 IUNVEL -1 KUNIT -1 -
   NPRINT 10 ISEED 314159 -
   FIRSTT 0.0 FINALT 300.0 TEMINC 5 -
   TWINDH 10.0 TWINDL -10.0 -
   IASORS 1 IASVEL 1 ICHECW 0 -
   NBXMOD 5  ATOM CDIEL SHIFT VATOM VDISTANCE VSWIT -
   CUTNB 8.0  CTOFNB 7.5  CTONNB 6.5  EPS 1.0  E14FAC 0.4  WMIN 1.5
set tt 2
set sp dynfastshk
set refval -1472.27378
@testcheck ?tote @refval .000001 @testname_@tt_@sp

OPEN read UNIT 41 CARD NAME @9WATERDYNs.CRD
read COOR comp UNIT 41 CARD
close unit 41

coor rms
set tt 3
set sp coordiff
set refval 0.
@testcheck ?rms @refval .00001 @testname_@tt_@sp

COOR DIFF COMP
PRINT COOR COMP


STOP
