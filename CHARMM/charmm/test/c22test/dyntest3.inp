* TESTS A NUMBER OF DYNAMICS RESTART CALCULATIONS
*
stream datadir.def
BOMLEV -2

set testname dyntest3
format (F20.5)

if ?NUMNODE .gt. 1 then
  echo dyntest3 testcase result: skip
  echo "Test NOT performed."
  stop
endif

OPEN READ FORM UNIT 11 NAME  @0toph8.rtf
READ RTF  CARD UNIT 11
close unit 11

OPEN READ FORM UNIT 12 NAME  @0param3.prm
READ PARAM CARD UNIT 12
close unit 12

READ SEQUENCE CARDS
* TRYPTOPHAN
*
    1
TRP
GENERATE TRYP SETUP

!open write unit 18 file name @9dyntest3.psf
!WRITE PSF FILE UNIT 18
!* TRYPTOPHAN USING EXPLICIT HYDROGEN
!*

READ COORD CARDS
* TRYPTOPHAN BUILT AND MINIMIZED WITH SD 50 STEPS, ABNR 500 STEP,
* AND CG 50 STEPS
* EPS=R POTENTIAL
*
   19
    1    1 TRP  HT1    0.10124   0.57409  -1.38529
    2    1 TRP  HT2    1.71114   0.43637  -1.90440
    3    1 TRP  N      1.06717   0.30225  -1.10075
    4    1 TRP  HT3    1.07999  -0.68894  -0.79051
    5    1 TRP  CA     1.47212   1.19190   0.02727
    6    1 TRP  CB     2.69565   2.02615  -0.34753
    7    1 TRP  CG     3.92166   1.16873  -0.67893
    8    1 TRP  CD1    4.81252   0.77506   0.29018
    9    1 TRP  CD2    4.29511   0.67909  -1.89046
   10    1 TRP  NE1    5.72693   0.04608  -0.32026
   11    1 TRP  HE1    6.09389  -0.74349   0.12669
   12    1 TRP  CE2    5.42381  -0.01032  -1.60094
   13    1 TRP  CE3    3.76875   0.77367  -3.15481
   14    1 TRP  CZ2    6.13756  -0.67683  -2.56270
   15    1 TRP  CZ3    4.47167   0.10800  -4.14811
   16    1 TRP  CH2    5.63634  -0.60503  -3.85644
   17    1 TRP  C      0.27986   2.11295   0.30889
   18    1 TRP  OT1   -0.40216   2.48057  -0.63623
   19    1 TRP  OT2   -0.10570   2.19226   1.45887

open write unit 17 file name @9dyntest3.crd
WRITE COOR FILE UNIT 17
* TRYPTOPHAN BUILT AND MINIMIZED WITH SD 50 STEPS, ABNR 500 STEP,
* AND CG 50 STEPS
* EPS=R POTENTIAL
*

IC PURGE
IC FILL
PRINT COOR
NBONDS CUTNB 100.0 CTONNB 99.0 CTOFNB 99.5



!!!!! 1 marker for the first section of dynamics testing
set tt 1
UPDATE   INBFRQ 10 IHBFRQ 10  -
  CUTHB 4.5 CUTHA 65.0 CTONHA 55.0 CTOFHA 60.0 CTONHB 3.5 CTOFHB 4.0

set refval 108
@testcheck ?nnba @refval .5 @testname_nnba_@tt

!!!!! 2 second section of dynamics
set tt 2
open read unit 17 file name @9dyntest3.crd
open write unit 18 card name @9dyntest3.res 

READ COOR FILE UNIT 17 RESTART
DYNA VERLET NSTEP 20  TIME .001 INBFRQ 10 IHBFRQ 10 FIRSTT 1000. NPRINT 1 -
     ISEED 311 IPRFRQ 10 IHTFRQ 0 IEQFRQ 0 ISVFRQ 1 IUNWRITE 18

set refval 67.04688
@testcheck ?tote @refval .000001 @testname_tote_@tt
! print coordinates
PRINT COOR
! print velocities
PRINT COOR COMP


!  Now look at two ways to continue a dynamics runs
!      a)  continue current trajectory using a restart file
!      b)  start a trajectory using velocities stored in the 
!          coor comp array
!


!  b) Start from velocities in comparison coordinate set

DYNA VERLET NSTEP 20 TIME .001 INBFRQ 10 IHBFRQ 10 FIRSTT 1000. -
     NPRINT 1 -
     ISEED 311 IPRFRQ 10 IHTFRQ 0 IEQFRQ 0 IASORS 1 IASVEL 0  ! NDEGF 57

set tt 3
set refval 67.00755
@testcheck ?tote @refval .000001 @testname_tote_@tt

! a)  Restart from restart file
open read unit 19 card name @9dyntest3.res

DYNA VERLET RESTART NSTEP 20 TIME .001 INBFRQ 10 IHBFRQ 10 FIRSTT 1000. -
     NPRINT 1 IUNREAD 19 -
     ISEED 311 IPRFRQ 10 IHTFRQ 0 IEQFRQ 0 IASORS 1 IASVEL 0  ! NDEGF 57

set tt 4
set refval 67.00755
@testcheck ?tote @refval .000001 @testname_tote_@tt

STOP
