* CHARMM 25 Testcase ewald_atom.inp
* Bernard R. Brooks, 15-JUL-97, c25b1
* water-vacum system
*

bomlev -3
stream datadir.def
set 6 @9                  ! 9 is used for cell dimension

read rtf card              
* TIPS3P WATER TOPOLOGY FILE
* TIPS3P WATER TOPOLOGY FILE
*
   18    1
MASS     4 HT     1.00800 ! TIPS3P WATER HYDROGEN
MASS    64 OT    15.99940 ! TIPS3P WATER OXYGEN

RESI TIP3    .000   ! TIPS3P WATER MODEL
GROUP
ATOM OH2  OT     -0.834
ATOM H1   HT      0.417
ATOM H2   HT      0.417
BOND OH2 H1  OH2 H2  H1 H2  ! THE LAST BOND IS NEEDED FOR SHAKE
THET H1  OH2  H2
!                          NOTE: NO DONORS OR ACCEPTORS FOR TIPS3P
PATC  FIRS NONE LAST NONE

END

read param card
* TIPS3P WATER PARAMETER FILE
*
BOND
HT   OT     450.0       0.9572 ! from TIPS3P geometry
HT   HT       0.0       1.5139 ! from TIPS3P geometry (for SHAKE w/PARAM)
THETAS
HT   OT   HT      55.0     104.52    ! FROM TIPS3P GEOMETRY
!
NONBONDED   GROUP CDIEL SWITCH VGROUP VDISTANCE VSWITCH -
     CUTNB 9.0  CTOFNB 8.0 CTONNB 8.0 EPS 1.0  E14FAC 1.0  WMIN 1.5
!
HT       0.0440    -0.0498    0.8000 !TIP3P water hydrogen, see NBFIX below
OT       0.8400    -0.1591    1.6000 !TIP3P water oxygen,   see NBFIX below
!!HT       0.0440    1.0       0.8000
!!OT       0.8400    6.0       1.6000

NBFIX
!OT   OT      -0.152073   3.5365  ! TIPS3P VDW INTERACTION
!HT   OT      -0.05       1.0  ! MODIFIED TIPS3P WITH CATASTROPHY ZONE REMOVED
OT   OT      -0.152073  3.5365  ! TIPS3P VDW INTERACTION
HT   HT      -0.04598   0.4490
HT   OT      -0.08363   1.9927
! FOR NOW, USE CHARLIE'S PARAMETERS
!OT OT -0.1521 3.5364
!HT HT -0.04598 0.4490
!OT HT -0.08363 1.9927

END

read sequence TIP3 560
gener WAT noangle nodihedral

open unit 14 read card name @0wat560.crd 
read coor card unit 14             
close unit 14

set 7 25.40608
set 8 25.40608
set 9 25.40608 

crystal define cubic @7 @8 @9 90.0 90.0 90.0
CRYStal BUILd  CUTOff 15.0
image byres xcen 0.0 ycen 0.0 zcen 0.0 sele all end

skip bond angl vdw imnb 

energy -
    inbfrq 1 atom vatom cutnb 18.0 ctofnb 18. cdie eps 1. -
    ctonnb 10.0 shift vswitch cutim 18.0 imgfrq 1 wmin 0.5 -
    ewald KMAX 15 KSQMAX 600  KAPPA 0.30 spline

coor force comp

open unit 21 write card name @6ewald.frc
write coor card unit 21 comp
close unit 21

!TEST FIRST SELE ATOM WAT 1 * END

energy -
   ewald kappa .30 pmewald fftx 64 ffty 64 fftz 64 order 4

coor scale comp fact -1.0
scalar xcomp store 1
scalar ycomp store 2
scalar zcomp store 3
scalar dx +store 1
scalar dy +store 2
scalar dz +store 3
scalar xcomp recall 1
scalar ycomp recall 2
scalar zcomp recall 3
coor dist weigh comp
coor stat comp
scalar wmain = wcomp

open unit 31 write card name @6pme64_ewald.frc
write coor card unit 31 comp sele prop wcomp .gt. 0.0001 end
* force differences between PME 64 and Ewald
*

coor force comp

open unit 41 write card name @6pme64.frc
write coor card unit 41 comp
* forces for PME 64 
*

!TEST FIRST SELE ATOM WAT 1 * END

energy -
   ewald kappa .30 pmewald fftx 16 ffty 16 fftz 16 order 4

coor scale comp fact -1.0
scalar xcomp store 1
scalar ycomp store 2
scalar zcomp store 3
scalar dx +store 1
scalar dy +store 2
scalar dz +store 3
scalar xcomp recall 1
scalar ycomp recall 2
scalar zcomp recall 3
coor dist weigh comp
coor stat comp
scalar wmain = wcomp

open unit 51 write card name @6pme16_pme64.frc
write coor card comp unit 51 sele prop wcomp .gt. 0.0001 end
* force differences between PME 16 and PME 64
*

coor force comp

open unit 61 write card name @6pme16.frc
write coor card unit 61 comp
* forces for PME 16
*

stop
