* CHARMM 30 testcase cftmtst5.inp
* Testing CFTM:two-dimensional conformational thermodynamic integration
* Coordinate types: dihedrals (phi-psi)
* System: alanine dipeptide
* Action: fix psi,  constrain phi, calculate dA/dpsi with TI and TP
* Expected result: should be similar to cftmtst1.inp and cftmtst3.inp
* Author: Krzysztof Kuczera, Lawrence, KS 10-Jun-2003
*

if ?qtsm .ne. 1 then
 echo "Test NOT performed."
 stop
endif

! Multinode execution fails because Old integrator is not parallel.
if ?NUMNODE .gt. 1 then
    echo "Test NOT performed."
    stop
endif

stream datadir.def

read rtf card
* Alanine Topology File. (TOPH19.RTF)
*
    19    1
  MASS     1 H      1.00800 ! hydrogen which can h-bond to neutral atom
  MASS     4 HT     1.00800 ! TIPS3P WATER HYDROGEN
  MASS    11 C     12.01100 ! carbonyl carbon
  MASS    12 CH1E  13.01900 ! extended atom carbon w/ one hydrogen
  MASS    14 CH3E  15.03500 !   -    "    -           three
  MASS    38 NH1   14.00670 ! peptide nitrogen bound to one hydrogen
  MASS    51 O     15.99940 ! carbonyl oxygen
  MASS    58 OT    15.99940 ! TIPS3P WATER OXYGEN

  DECL -C
  DECL -O
  DECL +N
  DECL +H
  DECL +CA

  AUTOGENERATE ANGLES
  DEFA FIRS NTER LAST CTER

  RESI ALA     0.00000
  GROU
  ATOM N    NH1    -0.35
  ATOM H    H       0.25
  ATOM CA   CH1E    0.10
  GROU
  ATOM CB   CH3E    0.00
  GROU
  ATOM C    C       0.55
  ATOM O    O      -0.55
  BOND N    CA        CA   C         C    +N        C    O         N    H
  BOND CA   CB
  DIHE -C   N    CA   C         N    CA   C    +N        CA   C    +N   +CA
  IMPH N    -C   CA   H         C    CA   +N   O         CA   N    C    CB
  DONO  H  N
  ACCE O  C
  IC   -C   CA   *N   H      0.0000    0.00  180.00    0.00   0.0000
  IC   -C   N    CA   C      0.0000    0.00  180.00    0.00   0.0000
  IC   N    CA   C    +N     0.0000    0.00  180.00    0.00   0.0000
  IC   +N   CA   *C   O      0.0000    0.00  180.00    0.00   0.0000
  IC   CA   C    +N   +CA    0.0000    0.00  180.00    0.00   0.0000
  IC   N    C    *CA  CB     0.0000    0.00  120.00    0.00   0.0000

  RESI ACE     0.00000 ! Acetyl blocking group with standard naming.
  GROU
  ATOM CH3  CH3E    0.00
  GROU
  ATOM C    C       0.55
  ATOM O    O      -0.55
  BOND CH3  C         C    +N        C    O
  DIHE CH3  C    +N   +CA
  IMPH C    CH3  +N   O
  ACCE O C
  IC   +N   CH3  *C   O      0.0000    0.00  180.00    0.00   0.0000
  IC   CH3  C    +N   +CA    0.0000    0.00  180.00    0.00   0.0000
  PATC  FIRS NONE LAST NONE

  RESI CBX     0.00000
  GROU
  ATOM N    NH1    -0.35
  ATOM H    H       0.25
  ATOM CA   CH3E    0.10
  BOND N    CA        N    H
  IMPH N    -C   CA   H
  DONO  H    N
  IC   -C   CA   *N   H      0.0000    0.00  180.00    0.00   0.0000
  PATC  FIRS NONE LAST NONE

  RESI TIP3    .000   ! TIPS3P WATER MODEL
  GROUP
  ATOM OH2  OT     -0.834
  ATOM H1   HT      0.417
  ATOM H2   HT      0.417
  BOND OH2  H1  OH2  H2   H1  H2  ! THE LAST BOND IS NEEDED FOR SHAKE
  ANGLE  H1 OH2 H2
  ACCE OH2
  PATC  FIRS NONE LAST NONE

END

Read param card
* Parameter file for alanine. (PARAM19.PRM)
*

 BONDS
  C    CH1E   405.0       1.52
  C    CH3E   405.0       1.52
  C    NH1    471.0       1.33
  C    O      580.0       1.23
  CH1E CH3E   225.0       1.52
  CH1E NH1    422.0       1.45
  CH3E NH1    422.0       1.49
  H    NH1    405.0       0.98!  GELIN AND IR STRETCH 3200 CM 1
  HT   OT     450.0       0.9572 ! from TIPS3P geometry
  HT   HT       0.0       1.5139 ! from TIPS3P geometry (for SHAKE w/PARAM)
 THETAS
  CH1E C    NH1     20.0     117.5
  CH1E C    O       85.0     121.5
  CH3E C    NH1     20.0     117.5
  CH3E C    O       85.0     121.5
  NH1  C    O       65.0     121.0
  C    CH1E CH3E    70.0     106.5
  C    CH1E NH1     45.0     111.6
  CH3E CH1E NH1     65.0     108.5
  C    NH1  CH1E    77.5     120.0
  C    NH1  CH3E    77.5     120.0
  C    NH1  H       30.0     120.0
  CH1E NH1  H       35.0     120.0
  CH3E NH1  H       35.0     120.0
  HT   OT   HT      55.0     104.52    ! FROM TIPS3P GEOMETRY
 PHI
  X    C    CH1E X        0.0       3       0.0! FROM GELIN THESIS AMIDES
  X    C    NH1  X        8.2       2     180.0! THE EXPERIMENTAL BARRIER.
  X    CH1E NH1  X        0.3       3       0.0
 IMPHI
  C    X    X    O      100.0    0     0.0
  CH1E X    X    CH3E    55.0    0   35.26439
  NH1  X    X    H       45.0    0     0.0! AMIDES PROTON OOP
 NONBONDED  NBXMOD 5  ATOM CDIEL SHIFT VATOM VDISTANCE VSHIFT -
     CUTNB 8.0  CTOFNB 7.5  CTONNB 6.5  EPS 1.0  E14FAC 0.4  WMIN 1.5
!
!                    Emin      Rmin
!                 (kcal/mol)    (A)
  H        0.0440  -0.0498    0.8000
  HT       0.0440    -0.0498    0.8000 !TIP3P water hydrogen, see NBFIX below
  C        1.65    -0.1200    2.100   1.65 -0.1 1.9
  CH1E     1.35    -0.0486    2.365   1.35 -0.1 1.9
  CH3E     2.17    -0.1811    2.165   1.77 -0.1 1.9
  N*       1.1000  -0.2384    1.6000   ! includes N,NC2,NH1,NH2,NH3,NP,and NR
  O*       0.8400  -0.1591    1.6000   ! includes O, OH1, OM, and OS
  OT       0.8400    -0.1591    1.6000 !TIP3P water oxygen,   see NBFIX below
 NBFIX
!
!	We're gonna NBFIX the TIP3P water-water interactions
!	here to make them more like Jorgensen's.  The vdW parameters
!	specified above will be in effect, therefore, for ONLY
!	protein (read, protein OR nucleic acid)-water interactions.
!	OT-OT is exactly Jorgensen's; HT interactions are added
!	here.
!                Emin      Rmin
!              (kcal/mol)   (A)
  OT   OT      -0.152073  3.5365  ! TIPS3P VDW INTERACTION
  HT   HT      -0.04598   0.4490
  HT   OT      -0.08363   1.9927
 HBOND AEXP 4 REXP 6 HAEX 0 AAEX 0   NOACCEPTORS  HBNOEXCLUSIONS  ALL  -
     CUTHB 0.5 CTOFHB 5.0 CTONHB 4.0  CUTHA 90.0  CTOFHA 90.0  CTONHA 90.0
  N*+* O*          -0.00      2.9

END

! Generate the dipeptide seqment.
read sequ card
* N-methylalanylacetamide (the alanine dipeptide)
* constructed as a blocked alanine residue
*
3
ace ala cbx

generate ala setup

read coor card
* ALANINE DIPEPTIDE WITH PHI,PSI ~= -85,100
*  DATE:     7/ 1/90      0:40:30      CREATED BY USER: TOBIAS
*
   12
    1    1 ACE  CH3    3.06258   0.64613   1.42088 ALA  1      0.00000
    2    1 ACE  C      2.33541  -0.68685   1.35313 ALA  1      0.00000
    3    1 ACE  O      2.01413  -1.29380   2.37725 ALA  1      0.00000
    4    2 ALA  N      2.07725  -1.18175   0.14371 ALA  2      0.00000
    5    2 ALA  H      2.45870  -0.76210  -0.65152 ALA  2      0.00000
    6    2 ALA  CA     1.35635  -2.43045  -0.00242 ALA  2      0.00000
    7    2 ALA  CB     0.69707  -2.49721  -1.37506 ALA  2      0.00000
    8    2 ALA  C      2.38192  -3.54475   0.11749 ALA  2      0.00000
    9    2 ALA  O      3.17467  -3.80389  -0.78914 ALA  2      0.00000
   10    3 CBX  N      2.41984  -4.12094   1.31507 ALA  3      0.00000
   11    3 CBX  H      1.92248  -3.68658   2.04150 ALA  3      0.00000
   12    3 CBX  CA     3.28397  -5.30373   1.59827 ALA  3      0.00000

ic fill
ic print

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! The followings are the most important part of 2-D FEP
! Adjust the dihedrals to the values we want them constrained.
set phi1    -42.0    ! phi
set phi2    61.0   ! psi

! Edit phi and psi and build the new coordinates.
ic edit
 dihe 1 c 2 n 2 ca 2 c @phi1
 dihe 2 n 2 ca 2 c 3 n @phi2
end

coor init sele (bynum 1:3) end
ic build
coor init sele (bynum 10:12) end
ic build
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
ic print

coor orie norot sele segi ala end

! constrain phi with stiff harmonic potential 
cons dihe ala 1 c  ala 2 n  ala 2 ca  ala 2 c  force 10000.0  main

! Fix psi and calculate dV/dpsi
tsm
! fix dihe ala 1 c ala 2 n ala 2 ca ala 2 c toli 1.0e-5
 fix dihe ala 2 n ala 2 ca ala 2 c ala 3 n toli 1.0e-5
 maxi 10000
 cfti
! move dihe ala 1 c ala 2 n ala 2 ca ala 2 c by 1.0 -
!   inte sele bynum 6:12 end
 move dihe ala 2 n ala 2 ca ala 2 c ala 3 n by 1.0 -
   inte sele bynum 8:12 end
end

! Special nb list for smal system
Update inbfrq -1 cutnb 99.0 ctonnb 6.5 ctofnb 7.5

mini abnr nstep 100 tolg 0.01
ic print
coor print

faster 1
! Shake bonds to hydrogen.
!shake bonh param tol 1.0e-8



energy


open unit 27 form write name @9temp.res

! Equilibration dynamics for 5 PS.
dynamics nose strt nstep 500 timestep 0.001 -
 QREF 50.0 TREF 300.0 NCYC 5 firstt 240.0 -
 inbfrq -1 nprint 500 iprfrq 500 tbath 300.0 rbuff 0.0 -
 IHTFRQ 0 IEQFRQ 0 NTRFRQ 0 ISVFRQ 500 -
 IUNCRD -1 IUNVEL -1 KUNIT -1 IUNWRI 27 IUNREA -1 echeck 100000.0 -
 nsavc 0 nsavv 0 IASORS 0 IASVEL 1 ISCVEL 0 ICHECW 0 ISEED 314159

close unit 27

! Production dynamics for 50 PS.

open unit 28 form read  name @9temp.res
open unit 29 file write name @9temp.dcd
open unit 30 form write name @9temp.icp

! set up saving TI data to text file - every 10-th step
tsm
  savi icun 30 icfr 10 nwin 1
end

Update inbfrq 1 cutnb 99.0 ctonnb 6.5 ctofnb 7.5

dynamics nose rest nstep 2000 timestep 0.001 -
  QREF 50.0 TREF 300.0 NCYC 5 -
 inbfrq -1 nprint 500 iprfrq 500 tbath 300.0 rbuff 0.0 -
 IHTFRQ 0 IEQFRQ 0 NTRFRQ 0 ISVFRQ 500 -
 IUNCRD 29 IUNVEL -1 KUNIT -1 IUNWRI -1 IUNREA 28  -
 nsavc 10 nsavv 0 IASORS 0 IASVEL 1 ISCVEL 0 ICHECW 0

close unit 28
close unit 29
close unit 30

tsm clear
ic print
Update inbfrq 1 cutnb 99.0 ctonnb 6.5 ctofnb 7.5

! Get rid of dihedral constraints for energy evaluations
cons cldh

open unit 29 file read name @9temp.dcd

! Calculate dA/dx from trajectory.  cutnb 99.0 -> inbf 0
!======================================================
tsm
! fix dihe ala 1 c ala 2 n ala 2 ca ala 2 c toli 1.0e-5
 fix dihe ala 2 n ala 2 ca ala 2 c ala 3 n toli 1.0e-5
 maxi 10000
! move dihe ala 1 c ala 2 n ala 2 ca ala 2 c by 1.0 -
!   inte sele bynum 6:12 end
 move dihe ala 2 n ala 2 ca ala 2 c ala 3 n by 1.0 -
   inte sele bynum 8:12 end
!cftb first 29 nunit 1 temp 300.0 skip 1 cont 500 -
!   inbf 0 ihbf 0
  cfta first 29 nunit 1 temp 300.0 skip 1 cont 50 -
    inbf 0 ihbf 0 imgf 0
end

close unit 29

open unit 30 form read name @9temp.icp

! Re-calculate dA/dx using the text file
tsm
   cftj uicp 30 temp 300.0 cont 50
end

close unit 30
open unit 30 form read name @9temp.icp

! Use TP to get second opinion: see cftigas for explanantions
!=====================================================================
tsm post ic surf inte maxp 1 maxw 6 maxs 200
 proc firs 30 nuni 1 bins 50 ctem temp 300.0 delt 10.0 -
  begin 1 stop 2000
end

tsm clear

! Expected result: should be similar to cftmtst1.inp and cftmtst3.inp
! I got  dA/dpsi = -6.24
! KK June 2003
stop
