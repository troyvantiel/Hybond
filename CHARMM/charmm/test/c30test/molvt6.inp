* CHARMM 30 test case molvt6.inp
* MOLVIB: Fomamaide vibrations using CHARMM top/par
* tests CHARMM/MOLVIB interface, PED, coordinate transformations, symmetry
* Author: Krzysztof Kuczera, Lawrence, KS 10-June-2003
* Needs files: top_all22_prot.inp and par_all22_prot.inp
*===========================================================================
*


if ?numnode .ne. 1 then
 echo "Test NOT performed in parallel."
 stop
endif

stre datadir.def

Open unit 1 card read name @0top_all22_prot.inp
Read RTF card unit 1
Open unit 3 card read name @0par_all22_prot.inp
Read para card unit 3

! FORMamide residue definition
read RTF card append
* Formamide all-H topology: from top_all22_model.inp
*
   22     1

RESI FORM        0.00 ! formamide, adm jr.
ATOM HA   HA     0.08 ! 	
ATOM C    CC     0.42 !  O     Hc
ATOM N    NH2   -0.69 !  \\   /
ATOM Hc   H      0.35 !   C--N
ATOM Ht   H      0.35 !  /    \
ATOM O    O     -0.51 ! HA     Ht
                      !
BOND  C   HA   C   N   N   Hc   N   Ht
DOUBLE  C   O
IMPR  C  HA N  O   c  n  ha  o
IC  O   C   N   HC     1.2190 124.70    0. 118.50 1.0016 ! variable 1
IC  HC  N   C   HA     1.0016 118.50  180. 112.70 1.0980 ! variable 2
IC  O   C   N   HT     1.2190 124.70  180. 120.00 1.0015 ! variable 3


END

! Generate PSF
read sequ FORM 1
generate TEST setup first none last none

read coor card free
* formamide model geometry
*
    6
    1    1 FORM C      0.03197   0.03567   0.00000 TEST 1      6.00000
    2    1 FORM N      1.33097  -0.00639   0.00000 TEST 1      7.00000
    3    1 FORM HT     1.85779   0.81744   0.00000 TEST 1      1.00000
    4    1 FORM HC     1.76913  -0.88155   0.00000 TEST 1      1.00000
    5    1 FORM HA    -0.47295   1.02533   0.00000 TEST 1      1.00000
    6    1 FORM O     -0.59570  -1.02074   0.00000 TEST 1      8.00000



coor print
ic fill 
ic print


! optimize geometry
mini powell nstep 1000 tolgrad 0.000002 

coor print
ic fill 
ic print

! Do CHARMM normal mode analysis;
! this prints out frequencies and generates second derivatives
vibran nmode 18
diag
end

! Start MOLVIB section
!======================================================================
!* Formamide
!* Molvib input file 
!* CHARMM geometry, masses and force constants are passed to MOLVIB,
!* a normal mode analysis module
!* GFX = start with cartesian force constants
!* DIM 12 12 15 = there will be 15 'primitive' ICs, reduced to 12 independent
!*                linear combinations bu transformation with matrix U
!* IC = definitions of 'primitive' ICs (1=bond str, 2=angle bend, 3=wag, 4=dihe)
!* UMAT = non-zero elements of matrix forming independent linear combinations
!*        of the 'primitive' ICs
!* PED  = potential energy analysis information; for every independent
!*        IC, a symbolic representation is given to enable "nice table" at end
!*

MOLVIB NDI1 12 NDI2 12 NDI3 15 SECO  PRIN
GFX       
DIM     12   12   15
IC
1  2  3  0  0
1  2  4  0  0
1  1  5  0  0
1  1  2  0  0
1  1  6  0  0
2  2  1  6  0
2  2  1  5  0
2  6  1  5  0
2  3  2  4  0
2  1  2  3  0
2  1  2  4  0
4  5  1  2  4
4  5  1  2  3
4  6  1  2  4
4  6  1  2  3
UMAT     0    1    0           ! do row normalization for this matrix
  1  1     1.       1  2      1. 
  2  1     1.       2  2     -1. 
  3  3     1.       4  4      1.
  5  5     1.      
  6  7     1.       6  8     -1.
  7  6     2.       7  7     -1.      7  8    -1.
  8  9     2.       8 10     -1.      8 11    -1.
  9 10     1.       9 11     -1.
 10 12     1.      10 13      1.     10 14    -1.      10 15    -1.
 11 12     1.      11 13      1.     11 14     1.      11 15     1.
 12 12     1.      12 13     -1.     12 14     1.      12 15    -1.
-1
PED      0   20
  1  sNH2sym     2  sNH2as
  3  sC-H        4  sC-N        5  sC=O
  7  dN-C-O      6  dC-H        8  sc-NH2      9  r-NH2
 10  wCH        11  t-NH2      12  w-NH2
 -1
SYMM     2
   9   3
A'  A"  
END

stop
