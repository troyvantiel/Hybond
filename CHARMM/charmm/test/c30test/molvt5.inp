* CHARMM 30 test case molvt5.inp
* MOLVIB: TIP3 water vibrations using CHARMM top/par
* tests CHARMM/MOLVIB interface, PED, symmetry, EXPerimental frequencies
* Author: Krzysztof Kuczera, Lawrence, KS 10-June-2003
* Needs files: top_all22_prot.inp and par_all22_prot.inp
*===========================================================================
* MOLVIB-in-CHARMM test:
* minimize WATer structure, get second derivatives and run MOLVIB
*


if ?numnode .ne. 1 then
 echo "Test NOT performed in parallel."
 stop
endif

stre datadir.def

!===================================================================
! Part A. Initial calculations with CHARMM:                        |
! Genration, geometry optimization, calculation of force constants |
!===================================================================

! Topology and parameters
!====================================
Open unit 1 card read name @0top_all22_prot.inp
Read RTF card unit 1
Open unit 3 card read name @0par_all22_prot.inp
Read para card unit 3

! Generate PSF
!===================================
READ SEQU TIP3 1
GENERATE TEST noangle nodihe

! Create coordinates
read coor card free
* water
*
    1
1 1 TIP3 OH2   0.000000    0.000000    0.000000    8.0

hbuild
coor print

mini powell nstep 1000 tolgrad 0.000001 
coor print

vibran nmode 9
  diag
end

!==================================================================
! Part B. MOLVIB run using CHARMM coordinates and force constants |
!==================================================================


MOLVIB NDI1 3  NDI2 3  NDI3 3  SECO  PRIN
GFX       
PRNT     4
DIM      3    3    3
IC
1 1 2 0 0
1 1 3 0 0
2 2 1 3 0
UMAT     0    1    0           ! do row normalization for this matrix
  1  1     1.       1  2      1.      2  3      1.
  3  1     1.       3  2     -1.
-1
PED      0   10
  1  sO-H/sym    2  dH-O-H      3  sO-H/as     
 -1
EXPF
1595.
3657.
3756.
END

stop
