* REUS no water
*

if ?sccdftb .ne. 1 then stop
STREam datadir.def

ioformat extended

read rtf card name @0/top_all36_cgenff.rtf
read para card flex name @0/par_all36_cgenff.prm
read psf card name @0/malon.psf
read coor card name @0/malon.crd
stream @0/malon.str

coor stat
coor orient

CRYStal DEFIne CUBIc 999.0 999.0 999.0 90.0 90.0 90.0
CRYStal BUILD NOPER 0

! minimization
mini sd nstep 500
mini abnr nstep 5000 nprint 500 tolg 0.01
write coor card name @9/minimi.crd

! Umbrella_Setting
!!SCC-DFTB
define qm sele segid MALON show end

scalar WMAIN set 1.0 sele (qm) .and. type C*  SHOW end
scalar WMAIN set 2.0 sele (qm) .and. type H*  SHOW end
scalar WMAIN set 3.0 sele (qm) .and. type O*  SHOW end


SCCDFTB remove CHRG 0 SELE QM END TEMP 0.0 SCFT 0.00000001 D3RD HBOND

energy !value is -8307.54486 if use hh-mod with the 3OB parameters

write psf card name @9/init.psf
write coor card name @9/init.crd
write coor pdb name @9/init.pdb

!replica code

repdstr nrep 12 exlm freq 20 unit 100
open write unit 100 card name @9/replica.exch

! set up umbrella
rxncor: define o1 point select atom malon 1 O1 end
rxncor: define h1 point select atom malon 1 H1 end
rxncor: define d1 distance o1 h1

rxncor: define o2 point select atom malon 1 O2 end
rxncor: define h1 point select atom malon 1 H1 end
rxncor: define d2 distance o2 h1

rxncor: define lcod SCOMbination d1 1.0 d2 -1.0
rxncor: set nrxn 1 lcod

if ?myrep .eq. 0 set d0 -1.0
if ?myrep .eq. 1 set d0 -0.8
if ?myrep .eq. 2 set d0 -0.6
if ?myrep .eq. 3 set d0 -0.4
if ?myrep .eq. 4 set d0 -0.2
if ?myrep .eq. 5 set d0 0.0
if ?myrep .eq. 6 set d0 0.2
if ?myrep .eq. 7 set d0 0.4
if ?myrep .eq. 8 set d0 0.6
if ?myrep .eq. 9 set d0 0.8
if ?myrep .eq. 10 set d0 1.0
if ?myrep .eq. 11 set d0 1.2

open write unit 101 card name @9/reus.out
outu 101

rxncor: umbrella name lcod kumb 22.0 del0 @d0 form 1
rxncor: statistics name lcod lowdelta -1.2 hidelta 1.0 deldel 0.1 start -1.0 !added Jan. 2018
rxncor: trace lcod unit 102
open write unit 102 form name @9/lcod.dat

!!MD
open unit 103 write unform name @9/malon.dcd
open unit 104 write card name @9/malon.rst
open unit 105 write card name @9/malon.xyz

scalar fbeta set 25.0

dynamics start nstep 100 timestep 0.0005 LEAP -
       	lang tbath 300.0 rbuf 0.0 -
        iuncrd 103 iunwri 104 mxyz 1 iunxyz 105 iunvel -1 iunq -1 -
        nsavc 10 nprint 10 nsavx 10 iprfrq 10 ntrfrq 10 iuno -
        iseed 3939 3939 3939 3939
stop
