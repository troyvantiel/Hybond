* Generate a four-atomic benchmark system
* and test MKDUMMY code (which turns selected atoms into dummy atoms)
* 
 
calc bond1 = 2.00 + 1.0
calc bond2 = 2.00 - 1.0 
calc angle1 =  130   
calc angle2 = 150.   

set massa 9
set massb 9
set massc 9

ioformat extended

read rtf card 
* topology for four-atomic benchmark
*
36  1

MASS   1 a     @massa ! first imaginary atom type
MASS   2 b     @massb ! second "
MASS   3 c     @massc ! second "

DEFA FIRS NONE LAST NONE

resi four 0.0
group
atom a1 c  1.
atom a2 a  0.
atom a3 a  0.
atom a4 b -1.
bond a1 a2  a2 a3  a3 a4
angl a1 a2 a3  a2 a3 a4
dihe a1 a2 a3 a4
ic a1 a2 a3 a4 0. 0. 0. 0. 0.
!ic a4 a3 a2 a1 0. 0. 0. 0. 0.
!ic a4 a1 a2 a3 0. 0. 0. 0. 0.
!ic -x a1 a2 a3 0. 0. 0. 0. 0.
!ic a2 a3 a4 +x 0. 0. 0. 0. 0.
!ic +x +y a3 a4 0 0 0 0 0
ic a4 a3 -x -y 0 0 0 0 0
ic +x a2 a3 a4 0 0 0 0 0
ic +x a1 a2 a3 0 0 0 0 0
ic +x +y a1 a2 0 0 0 0 0

END

read para card flex name
* parameters for toy compounds of Stefan Boresch' imagination
*

ATOMS
MASS   1 a     @massa ! first imaginary atom type
MASS   2 b     @massb ! second "
MASS   3 c     @massc ! second "

BONDS
a a 200. 2.0
a b 200. 2.0
a c 200. 2.0

ANGLES
a a a  50. 110.
a a b  50. 110.
a a c  50. 110.

dihe
a a a a  1. 3 180.00
a a a b  1. 3 180.00
c a a a  1. 3 180.00
c a a b  1. 3 180.00

NONBONDED nbxmod  5 atom cdiel fshift vatom vdistance vfswitch -
cutnb 14.0 ctofnb 12.0 ctonnb 10.0 eps 1.0 e14fac 1.0 wmin 1.5
* 0 0. 0.
b      0.000000  -0.100000     1.000000                      
c      0.000000  -0.200000     2.000000   

END

read sequence four 1
generate four first none last none warn setup

ic para
ic seed 1 a1 1 a2 1 a3
ic build 

energy 

open unit 21 write card name testdummy.top 
open unit 22 write card name testdummy.par 
mkdummy sele all end utop 21 upar 22 rsca 0.0 csca 0.0 redi  
close unit 21

print psf

energy

calc fail = abs ( ?ener )

set status pass
if @fail .gt. 0.00001  set status fail
echo mkdummytest testcase result: @status

stop
