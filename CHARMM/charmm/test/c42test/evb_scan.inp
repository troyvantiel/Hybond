*Malonaldehyde 2 state EVB  
*
!            H                                    H          
!           /                                      \
!          O1      O2                       O1      O2
!          |       ||                       ||      | 
!          |       ||                       ||      | 
!          C1      C2                       C1      C2
!         / \\    /  \                     / \     // \
!       H1   \\  /    H2                 H1   \   //   H2
!              C3                               C3
!              |                                |
!              H3                               H3         
!        state 1 (malonaldehyde_state1.rtf)      state 2 (malonaldehyde_state2.rtf)

if ?ensemble .ne. 1 then
 echo "Test NOT performed."
 stop
endif

if ?numnode .ne. 2 then						   
   echo "This test NOT run"					   
   echo " Need 2 processors for this test"
   stop					
endif					

stream datadir.def 

! nensem : number of EVB states
ensemble nensem 2

bomlev 0
prnlev 3 
wrnlev 2

if ?ensemble .ne. 1 then
 echo "Test NOT performed."
 stop
endif

set node ?whoiam

if @node .EQ. 0 set top state1
if @node .EQ. 1 set top state2

!
! READ IN TOPOLOGIES
!
ensemble open read unit 11 card name @0/malonaldehyde@node.rtf 
read rtf card unit 11
ensemble close unit 11 

!
! READ IN PARAMETERS
!
Faster OFF

use mmff force field 

ensemble open read form unit 1 name @0/mmffsup.par
read parameter card mmff SUPP unit 1
ensemble close unit 1

ensemble open unit 1 read form  name @0/mmffprop.par 
read parameter card mmff PROP unit 1
ensemble close unit 1

ensemble open read form unit 1 name @0/mmffsymb.par
read parameter card mmff SYMB unit 1
ensemble close unit 1

ensemble open read form unit 1 name @0/mmffdef.par 
read parameter card mmff DEFI  unit 1
ensemble close unit 1

ensemble open read form unit 1 name @0/mmffbndk.par 
read parameter card mmff BNDK  unit 1
ensemble close unit 1

ensemble open read form unit 1 name @0/mmffhdef.par 
read parameter card mmff HDEF  unit 1
ensemble close unit 1

ensemble open read form unit 1 name @0/mmffarom.par 
read parameter card mmff AROM  unit 1
ensemble close unit 1

ensemble open read form unit 1 name @0/mmffvdw.par 
read parameter card mmff VDW   unit 1
ensemble close unit 1

ensemble open read form unit 1 name @0/adjusted_mmffbond.par  
read parameter card mmff BOND  unit 1
ensemble close unit 1

ensemble open read form unit 1 name @0/mmffchg.par 
read parameter card mmff CHRG unit 1 
ensemble close unit 1 

ensemble open read form unit 1 name @0/mmffpbci.par 
read parameter card mmff PBCI unit 1 
ensemble close unit 1

ensemble open read form unit 1 name @0/mmffang.par 
read parameter card mmff ANGL unit 1 
ensemble close unit 1

ensemble open read form unit 1 name @0/mmffstbn.par 
read parameter card mmff STBN unit 1
ensemble close unit 1

ensemble open read form unit 1 name @0/mmffdfsb.par 
read parameter card mmff DFSB unit 1
ensemble close unit 1

ensemble open read form unit 1 name @0/mmffoop.par 
read parameter card mmff OOPL unit 1
ensemble close unit 1

ensemble open read form unit 1 name @0/mmfftor.par 
read parameter card mmff TORS unit 1
ensemble close unit 1

!
! GENERATE SEQUENCE
! 
ensemble open read unit 21 card name @0/malonaldehyde.crd
read sequence coor resid unit 21
ensemble close unit 21

generate mal setup

open read unit 19 card name @0/malonaldehyde.crd
read coor unit 19 card
close unit 19 

!
! CALCULATE DIPOLE
!
!  This is needed to allow the calculation of state averaged dipole moments. 
!  Useful in debugging/analysis. As there is only one molecule then we can use
!  'sele all end'
!
coor dipo sele all end

!
! CALCULATE NON-EVB ENERGY
!  
!  This is here for comparison with EVB energy calculation
!
energy

!
! SET UP EVB CALCULATION
!
!  Coupling parameters for 2-D Gaussian + a constant coupling    
!
set CONS 3.0

!
! INITIALISE EVB CALCULATION 
!
!  EVB hamiltonian matrix, H = :
!
!  state 1: |[0,0] [0,1]|
!  state 2: |[1,0] [1,1]|
!
!  Only modify upper triangular part as lower triangular part is filled in 
!  automatically. 
!
!   V0 and V1 and the potential energies of state 1 and 2 respectively.
!
!   EVBS : save unit for dipole output
!   SHFT 0 <VALUE> 			: H[0,0] <- V0 + <VALUE>   
!   COUP 0 1 CONS <VALUE>  		: H[0,1] <- H[0,1] + <VALUE> 
!   COUP 0 1 GAUS TWOD <2D GAUSSIAN> 	: H[0,1] <- H[0,1] + <2D GAUSSIAN> 

ensemble evb
	COUP 0 1 CONS @CONS
envbi

energy

!
! RUN RELAXED SCAN
!
ensemble open write card unit 21 name @9/reaction.energy.@node

set atom1 mal 1 O1
set atom2 mal 1 H
set atom3 mal 1 O2

set inc 0.1
set v -1.0
set n 1

label loop
	skip none

	resd reset kval 2000.0 rval @v - 
	1.0 @atom1 @atom2 -1.0 @atom2 @atom3

	mini abnr nstep 200 nprint 100

	skip resd
	energy
	write title unit 21
	* @v ?ener
	*

incr v by @inc
incr n by 1
if n .le. 21 goto loop

ensemble close unit 21
  
stop

