* A simple example for using mcec to study proton transfer
* Although mcec is most useful for long-range proton transfers
* mediated by many groups, this example studies a direct proton transfer.
* For more examples, see the original paper by Koenig et al., 
* JPCA, 110, 548-563 (2006).
*

if ?sccdftb .ne. 1 then stop
STREam datadir.def

ioformat extended

read rtf card name @0/top_all36_cgenff.rtf
read para card flex name @0/par_all36_cgenff.prm
read psf card name @0/malon.psf
read coor card name @0/malon.crd
stream @0/malon.str

coor stat
coor orient


!SCC-DFTB
define qm sele segid MALON show end

scalar WMAIN set 1.0 sele (qm) .and. type C*  SHOW end
scalar WMAIN set 2.0 sele (qm) .and. type H*  SHOW end
scalar WMAIN set 3.0 sele (qm) .and. type O*  SHOW end

SCCDFTB remove CHRG 0 SELE QM END TEMP 0.0 SCFT 0.00000001 D3RD HBOND 

energy !value is -8300.99418 if use hh with the 3OB parameters
       !will generate nbo.47 file, which can be read by the NBO program

! Now let's set up mCEC
define o_1      sele (resid 1 .and. type O1) show end 
define o_2      sele (resid 1 .and. type O2) show end 
define oxy      sele (o_1 .or. o_2) show end 
define hydro    sele (resid 1 .and. type H1) show end 

scalar wmain set 0.0 
scalar wmain set 0.0 sele o_1 end ! See the original JPC, A paper by Koenig et al. (2006)
                                  ! for a discussion of proper weights for the oxygen atoms

rxncor: define xi       cecm sele oxy end sele hydro end rsw 1.15 dsw 0.045
rxncor: define D        point sele (resid 1 .and. type O1) show end 
rxncor: define A        point sele (resid 1 .and. type O2) show end 
rxncor: define R1       dist D XI
rxncor: define R2       dist A XI
rxncor: define num      scomb R1 1 R2 -1
rxncor: define denom    scomb R1 1 R2 1
rxncor: define zeta     rati num denom
rxncor: set nrxn 1 zeta

set d0 0.0      !--set a value for zeta coordinate-- 

rxncor: umbrella name zeta kumb 500.0 del0 @d0 form 1
rxncor: statistics name zeta lowdelta -1.2 hidelta 1.0 deldel 0.1 start -1.0
rxncor: trace R1   unit 100 
rxncor: trace R2   unit 101 
rxncor: trace zeta unit 102 
open write unit 100 form name @9/malon_r1_@d0.rc
open write unit 101 form name @9/malon_r2_@d0.rc
open write unit 102 form name @9/malon_zeta_@d0.rc

energy

!--minimization--
mini sd nstep 500 
mini abnr nstep 5000 nprint 500 tolg 0.01
write coor card name @9/malon_mini_zeta_@d0.crd

! Do energy
energy
@testcheck ?ener -8307.82602 0.005 malon_mcec

STOP

