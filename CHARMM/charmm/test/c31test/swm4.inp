* SWM4-DP water
* (by Guillaume Lamoureux)
*
stream datadir.def

SET ALPHAO = 1.042520
SET DOM    = 0.238080

OPEN UNIT 33 WRITE FORM NAME @9swm4.dat

OPEN READ CARD UNIT 1 NAME @0top_all22_model_swm4.inp
READ RTF CARD UNIT 1
OPEN READ CARD UNIT 1 NAME @0top_all22_drude.inp
READ RTF CARD APPEND UNIT 1

OPEN CARD READ UNIT 1 NAME @0par_all22_prot_swm4.inp
READ PARAM CARD UNIT 1
OPEN READ CARD UNIT 1 NAME @0par_all22_drude.inp
READ PARAM CARD APPEND UNIT 1


!=======================================================
!  Monomer
!-------------------------------------------------------

READ SEQUENCE SWM4 1
GENERATE WAT SETUP NOANGLE NODIHEDRAL

READ COOR CARD
* SWM4-DP water
*
    4
    1    1 SWM4 OH2    0.00000   0.28197   0.00000
    2    1 SWM4 OM     0.00000   0.04389   0.00000
    3    1 SWM4 H1     0.75695  -0.30391   0.00000
    4    1 SWM4 H2    -0.75695  -0.30391   0.00000

SCALAR WMAIN = ZERO
SCALAR WMAIN SET @ALPHAO SELECT TYPE OH2 END
DRUDE MASS 0.1 KDRUDE 500.0 SELECT TYPE OH2 END
SCALAR MASS SHOW
SCALAR CHARGE SHOW

COOR COPY COMP
SHAKE BONH PARAM TOLERANCE 1.0E-9 -
      NOFAST -
      SELECT ( SEGID WAT .AND. .NOT. TYPE D* ) END -
      SELECT ( SEGID WAT .AND. .NOT. TYPE D* ) END

LONEPAIR BISECTOR DIST @DOM ANGLE 0.0 DIHE 0.0 -
         SELECT ATOM WAT * OM END  SELECT ATOM WAT * OH2 END - 
         SELECT ATOM WAT * H1 END  SELECT ATOM WAT * H2  END

COOR SHAKE

ENERGY ATOM VATOM VSHIFT -
       CTOFNB 97.0 CUTNB 99.0 WMIN 1.0

MINI SD NSTEP 100
COOR ORIENT
PRINT COOR

QUICK 1 2    ! Oxygen-Drude distance
QUICK 1 3    ! OM distance
QUICK 1 4    ! OH bond length
QUICK 1 5    ! OH bond length
QUICK 4 1 5  ! HOH angle
COOR DIPOLE
SET MU1 = ?RDIP

WRITE TITLE UNIT 33
* MONOMER:
*   MU    @MU1
*   ALPHA @ALPHAO
*

DRUDE RESET
DELETE ATOMS SELECT ALL END


!=======================================================
!  Dimer
!-------------------------------------------------------

READ SEQUENCE SWM4 2
GENERATE WAT SETUP NOANGLE NODIHEDRAL

READ COOR CARD
* SWM4-DP water
*
    8
    1    1 SWM4 OH2    3.00000   0.00000   0.28197
    2    1 SWM4 OM     3.00000   0.00000   0.04389
    3    1 SWM4 H1     3.00000   0.75695  -0.30391
    4    1 SWM4 H2     3.00000  -0.75695  -0.30391
    5    2 SWM4 OH2    0.00000   0.28197   0.00000
    6    2 SWM4 OM     0.00000   0.04389   0.00000
    7    2 SWM4 H1     0.75695  -0.30391   0.00000
    8    2 SWM4 H2    -0.75695  -0.30391   0.00000

SCALAR WMAIN = ZERO
SCALAR WMAIN SET @ALPHAO SELECT TYPE OH2 END
DRUDE MASS 0.1 KDRUDE 500.0 SELECT TYPE OH2 END
SCALAR MASS SHOW
SCALAR CHARGE SHOW

COOR COPY COMP
SHAKE BONH PARAM TOLERANCE 1.0E-9 -
      NOFAST -
      SELECT ( SEGID WAT .AND. .NOT. TYPE D* ) END -
      SELECT ( SEGID WAT .AND. .NOT. TYPE D* ) END

LONEPAIR BISECTOR DIST @DOM ANGLE 0.0 DIHE 0.0 -
         SELECT ATOM WAT * OM END  SELECT ATOM WAT * OH2 END - 
         SELECT ATOM WAT * H1 END  SELECT ATOM WAT * H2  END

COOR SHAKE

ENERGY ATOM VATOM VSHIFT -
       CTOFNB 97.0 CUTNB 99.0 WMIN 1.0


MINI SD NSTEP 5000 NPRINT 1000
SET EMIN = ?ENER

COOR ORIENT
PRINT COOR
QUICK 1 6    ! OO distance
SET DOO = ?DIST
QUICK 1 6 9  ! Donor angle
SET THETA = ?THET
QUICK 6 1 3  ! Acceptor angle
CALC PHI = 180 - ?THET

COOR DIPOLE SELECT ALL END
SET MU2 = ?RDIP

WRITE TITLE UNIT 33
* DIMER:
*   EMIN  @EMIN
*   DOO   @DOO
*   THETA @THETA
*   PHI   @PHI
*   MU2   @MU2
*

OPEN WRITE CARD UNIT 10 NAME @9swm4.crd
WRITE COOR CARD UNIT 10


STOP
