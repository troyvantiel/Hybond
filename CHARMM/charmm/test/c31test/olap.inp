* CHARMM 29 testcase (c31a2) overlap.inp
* Test the overlap module with acetate dimer
* Author: Milan Hodoscek
* requires no input files
*

if ?overlap .ne. 1 then
 echo "olap.inp> Test NOT performed."
 echo olap testcase result: skip
 stop
endif

stream datadir.def

read rtf card
* data/top_all22_model.inp is out of date :-(
*
   27     1

MASS     1 H      1.00800 H ! polar H
MASS     3 HA     1.00800 H ! nonpolar H
MASS    24 CT3   12.01100 C ! aliphatic sp3 C for CH3 
MASS    33 CD    12.01100 C ! carbonyl C for none amides, asp,glu,cter
MASS    71 OB    15.99900 O ! carbonyl oxygen in acetic acid
MASS    73 OH1   15.99900 O ! hydroxyl oxygen

DEFA FIRS NONE LAST NONE
AUTO ANGLES DIHE   

RESI ACEH        0.00 ! acetic acid, ADM jr.
GROUP
ATOM C2   CT3   -0.30 !
ATOM C1   CD     0.75 !   H21      O2
ATOM H21  HA     0.09 !      \    //
ATOM H22  HA     0.09 !  H22-C2--C1
ATOM H23  HA     0.09 !      /     \ 
ATOM O2   OB    -0.55 !   H23       O1-HO1
ATOM O1   OH1   -0.61 !
ATOM HO1  H      0.44 !
BOND C1 O1  O1 HO1  C1 C2  C2 H21  C2 H22  C2 H23
DOUBLE C1 O2
IMPR C1 C2  O1 O2
DONO BLNK HO1 ! O1
ACCE O1
ACCE O2
IC O2   C1   C2   H21   0.0000  0.0000   0.0000  0.0000  0.0000
IC HO1  O1   C1   O2    0.0000  0.0000   0.0000  0.0000  0.0000
IC HO1  O1   C1   C2    0.0000  0.0000 180.0000  0.0000  0.0000
IC O1   C1   C2   H21   0.0000  0.0000 180.0000  0.0000  0.0000
IC O1   C1   C2   H22   0.0000  0.0000  60.0000  0.0000  0.0000
IC O1   C1   C2   H23   0.0000  0.0000 -60.0000  0.0000  0.0000

end

read param card
* parameters just for ACEH
*

bonds
CT3  CD    200.000     1.5220 
HA   CT3   322.000     1.1110
OB   CD    750.000     1.2200
OH1  H     545.000     0.9600
OH1  CD    230.000     1.4000

angles
HA   CT3  CD    33.000    109.50   30.00   2.16300
H    OH1  CD     55.000   115.0000
OH1  CD   CT3    55.000   110.5000
OB   CD   CT3   70.000    125.00   20.00   2.44200
HA   CT3  HA    35.500    108.40    5.40   1.80200
OH1  CD   OB    50.000    123.00  210.00   2.26200

dihe
X    CD   OH1  X        2.0500  2   180.00 
X    CT3  CD   X        0.0000  6   180.00

impr
OB   X    X    CD    100.0000         0      0.0000

NONBONDED nbxmod  5 atom cdiel shift vatom vdistance vswitch -
cutnb 14.0 ctofnb 12.0 ctonnb 10.0 eps 1.0 e14fac 1.0 wmin 1.5 
CD     0.000000  -0.070000     2.000000
CT3    0.000000  -0.080000     2.060000   0.000000  -0.010000  1.900000
H      0.000000  -0.046000     0.224500
HA     0.000000  -0.022000     1.320000
OB     0.000000  -0.120000     1.700000   0.000000  -0.120000     1.400000
OH1    0.000000  -0.152100     1.770000

end

read sequ aceh 1

generate ac setup

ic param
ic seed ac 1 o2 ac 1 c1 ac 1 c2
ic build

read sequ aceh 1
generate ma setup

ic param
ic seed ma 1 o2 ma 1 c1 ma 1 c2
ic build

coor trans zdir 1 dist 10 sele segi ma end

!open write unit 1 card name "aceh.psf"
!write psf card unit 1
!open write unit 1 card name "aceh.crd"
!write coor card unit 1

! fill up the wmain with the charges array
! They should be from ESP, but this is just a test...
scalar wmain = charge
! charges are very small compared to volumes (for formula 5)
!scalar wmain mult 0

print coor

define fix sele type ho1 .or. type c1  .or. type h21 end

set w 1

olap numb 2 volw @w
olap syst 1 weight   1 sele segi ma end
olap syst 2 weight   1 sele segi ac end

! ++++++++++++++++++++++++++++++++++++++++++++++++++++
! NOTE: This stuff must come after main olap command!!
!       wmain has the ESP charges at the beginning
! ++++++++++++++++++++++++++++++++++++++++++++++++++++
! this can be also something more elaborated
! but for now we have something similar to
! Schaefer stuff
set r 1.0
scalar wmain = radii
scalar wmain mult @r  ! this is alpha
!scalar wmain mult 100.0 sele .not. fix  end

print coor

block 2
call 2  sele segi ma end
coef 1 2 0.0
end

olap debug

energy

olap nodebug

energy

olap print

energy

@testcheck ?ener -126.70939 -0.0001 olap


stop
! this does nothing because radii are too small,
! or the systems are too far.
open write unit 1 file name "aceh0.dcd"
mini abnr nstep 1000 nprint 100 nsavc 1 iuncrd 1

! See this file with: vmd -psf aceh.psf -dcd aceh.dcd
open write unit 1 file name "aceh.dcd"
dyna verlet strt nstep 10000 timestep 0.001  -
    iprfrq 10000 ihtfrq 0 ieqfrq 1000 ntrfrq 10000  -
    iuncrd 1 -
    nprint 10000 nsavc 10 nsavv 0 ihbfrq 0 nbscale 1.0 -
    firstt 500.0 finalt 500.0 teminc 0.0  tstruc 507.0 -
    iasors 0 iasvel 1 iscvel 0 ichecw 0 twindh 10.0 twindl -10.0

test first tol 0.0 step 0.0001

coor copy comp

open read unit 1 file name "aceh.dcd"
coor dyna first 1 nunit 1
open write unit 1 file name "acehx.dcd"
traj iwrite 1 nwrite 1 nfile 1
traj write

coor swap

! coor dyna command overwrites WMAIN array, so here we put it back:
scalar wmain = radii
scalar wmain mult @r
scalar wmain mult 100.0 sele .not. fix end

open write unit 1 file name "aceh1.dcd"
mini abnr nstep 1000 nprint 100 nsavc 1 iuncrd 1

stop
