*  TNPACKTEST by Philippe Derreumaux, 28 January 1994.
*  Test case for TNPACK-method minimizer.
*  Alanine Dipeptide with explicit hydrogens used.
* 

if ?tnpack .ne. 1 then
 echo "Test NOT performed."
 stop
endif

set pnode
if ?NUMNODE .gt. 1 set pnode node 0

stream datadir.def

read rtf card
* Alanine Topology File. (TOPH19.RTF)
*
    19    1
  MASS     1 H      1.00800 ! hydrogen which can h-bond to neutral atom
  MASS     4 HT     1.00800 ! TIPS3P WATER HYDROGEN
  MASS    11 C     12.01100 ! carbonyl carbon
  MASS    12 CH1E  13.01900 ! extended atom carbon w/ one hydrogen
  MASS    14 CH3E  15.03500 !   -    "    -           three
  MASS    38 NH1   14.00670 ! peptide nitrogen bound to one hydrogen
  MASS    51 O     15.99940 ! carbonyl oxygen
  MASS    58 OT    15.99940 ! TIPS3P WATER OXYGEN

  DECL -C
  DECL -O
  DECL +N
  DECL +H
  DECL +CA

  AUTOGENERATE ANGLES
  DEFA FIRS NTER LAST CTER

  RESI ALA     0.00000
  GROU
  ATOM N    NH1    -0.35
  ATOM H    H       0.25
  ATOM CA   CH1E    0.10
  GROU
  ATOM CB   CH3E    0.00
  GROU
  ATOM C    C       0.55
  ATOM O    O      -0.55
  BOND N    CA        CA   C         C    +N        C    O         N    H
  BOND CA   CB
  DIHE -C   N    CA   C         N    CA   C    +N        CA   C    +N   +CA
  IMPH N    -C   CA   H         C    CA   +N   O         CA   N    C    CB
  DONO  H  N
  ACCE O  C
  IC   -C   CA   *N   H      0.0000    0.00  180.00    0.00   0.0000
  IC   -C   N    CA   C      0.0000    0.00  180.00    0.00   0.0000
  IC   N    CA   C    +N     0.0000    0.00  180.00    0.00   0.0000
  IC   +N   CA   *C   O      0.0000    0.00  180.00    0.00   0.0000
  IC   CA   C    +N   +CA    0.0000    0.00  180.00    0.00   0.0000
  IC   N    C    *CA  CB     0.0000    0.00  120.00    0.00   0.0000

  RESI ACE     0.00000 ! Acetyl blocking group with standard naming.
  GROU
  ATOM CH3  CH3E    0.00
  GROU
  ATOM C    C       0.55
  ATOM O    O      -0.55
  BOND CH3  C         C    +N        C    O
  DIHE CH3  C    +N   +CA
  IMPH C    CH3  +N   O
  ACCE O C
  IC   +N   CH3  *C   O      0.0000    0.00  180.00    0.00   0.0000
  IC   CH3  C    +N   +CA    0.0000    0.00  180.00    0.00   0.0000
  PATC  FIRS NONE LAST NONE

  RESI CBX     0.00000
  GROU
  ATOM N    NH1    -0.35
  ATOM H    H       0.25
  ATOM CA   CH3E    0.10
  BOND N    CA        N    H
  IMPH N    -C   CA   H
  DONO  H    N
  IC   -C   CA   *N   H      0.0000    0.00  180.00    0.00   0.0000
  PATC  FIRS NONE LAST NONE

  RESI TIP3    .000   ! TIPS3P WATER MODEL
  GROUP
  ATOM OH2  OT     -0.834
  ATOM H1   HT      0.417
  ATOM H2   HT      0.417
  BOND OH2  H1  OH2  H2   H1  H2  ! THE LAST BOND IS NEEDED FOR SHAKE
  ANGLE  H1 OH2 H2
  ACCE OH2
  PATC  FIRS NONE LAST NONE

END

Read param card
* Parameter file for alanine. (PARAM19.PRM)
*

 BONDS
  C    CH1E   405.0       1.52
  C    CH3E   405.0       1.52
  C    NH1    471.0       1.33
  C    O      580.0       1.23
  CH1E CH3E   225.0       1.52
  CH1E NH1    422.0       1.45
  CH3E NH1    422.0       1.49
  H    NH1    405.0       0.98!  GELIN AND IR STRETCH 3200 CM 1
  HT   OT     450.0       0.9572 ! from TIPS3P geometry
  HT   HT       0.0       1.5139 ! from TIPS3P geometry (for SHAKE w/PARAM)
 THETAS
  CH1E C    NH1     20.0     117.5
  CH1E C    O       85.0     121.5
  CH3E C    NH1     20.0     117.5
  CH3E C    O       85.0     121.5
  NH1  C    O       65.0     121.0
  C    CH1E CH3E    70.0     106.5
  C    CH1E NH1     45.0     111.6
  CH3E CH1E NH1     65.0     108.5
  C    NH1  CH1E    77.5     120.0
  C    NH1  CH3E    77.5     120.0
  C    NH1  H       30.0     120.0
  CH1E NH1  H       35.0     120.0
  CH3E NH1  H       35.0     120.0
  HT   OT   HT      55.0     104.52    ! FROM TIPS3P GEOMETRY
 PHI
  X    C    CH1E X        0.0       3       0.0! FROM GELIN THESIS AMIDES
  X    C    NH1  X        8.2       2     180.0! THE EXPERIMENTAL BARRIER.
  X    CH1E NH1  X        0.3       3       0.0
 IMPHI
  C    X    X    O      100.0    0     0.0
  CH1E X    X    CH3E    55.0    0   35.26439
  NH1  X    X    H       45.0    0     0.0! AMIDES PROTON OOP
 NONBONDED  NBXMOD 5  ATOM CDIEL SHIFT VATOM VDISTANCE VSHIFT -
     CUTNB 8.0  CTOFNB 7.5  CTONNB 6.5  EPS 1.0  E14FAC 0.4  WMIN 1.5
!
!                    Emin      Rmin
!                 (kcal/mol)    (A)
  H        0.0440  -0.0498    0.8000 
  HT       0.0440    -0.0498    0.8000 !TIP3P water hydrogen, see NBFIX below
  C        1.65    -0.1200    2.100   1.65 -0.1 1.9
  CH1E     1.35    -0.0486    2.365   1.35 -0.1 1.9
  CH3E     2.17    -0.1811    2.165   1.77 -0.1 1.9
  N*       1.1000  -0.2384    1.6000   ! includes N,NC2,NH1,NH2,NH3,NP,and NR
  O*       0.8400  -0.1591    1.6000   ! includes O, OH1, OM, and OS
  OT       0.8400    -0.1591    1.6000 !TIP3P water oxygen,   see NBFIX below
 NBFIX
!
!	We're gonna NBFIX the TIP3P water-water interactions
!	here to make them more like Jorgensen's.  The vdW parameters
!	specified above will be in effect, therefore, for ONLY
!	protein (read, protein OR nucleic acid)-water interactions.
!	OT-OT is exactly Jorgensen's; HT interactions are added
!	here.
!                Emin      Rmin
!              (kcal/mol)   (A)
  OT   OT      -0.152073  3.5365  ! TIPS3P VDW INTERACTION
  HT   HT      -0.04598   0.4490
  HT   OT      -0.08363   1.9927
 HBOND AEXP 4 REXP 6 HAEX 0 AAEX 0   NOACCEPTORS  HBNOEXCLUSIONS  ALL  -
     CUTHB 0.5 CTOFHB 5.0 CTONHB 4.0  CUTHA 90.0  CTOFHA 90.0  CTONHA 90.0
  N*+* O*          -0.00      2.9

END

! Generate the dipeptide seqment.
read sequ card
* N-methylalanylacetamide (the alanine dipeptide)
* constructed as a blocked alanine residue
*
3
ACE ALA CBX

GENERATE MAIN SETUP
IC PARAM 
IC SEED 2 N 1 C 1 CH3
IC BUILD
IC FILL
PRINT IC

prnlev 3 @pnode
wrnlev 1 
NOBOmb

time now

set 1 -60.0
set 2  60.0
cons dihe 1 c 2 n 2 ca 2 c force 200. min @1
cons dihe 2 n 2 ca 2 c 3 n force 200. min @2


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!MINIMIZATION with TNPACK, with the finite-difference formula for Hd option
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
faster -1
mini tn nstep 600 inbfrq -1 ihbfrq 1 ourhd preco sched rest -
search ncgcyc 20 tolgrd 0.01 noor nopm -
!cutnb 80.0 ctofnb 79.0 fshift -
cutnb 13.0 ctofnb 12.0 fshift -

coor copy comp
ic fill
coor stat sele segid main end
time now

@testcheck ?ener -21.4617564 0.0001 tntest1_chk1

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!MINIMIZATION with TNPACK, with the explicit Hd option
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
cons cldh
faster -1
mini tn nstep 60 inbfrq -1 ihbfrq 1 userhd preco sched rest -
search ncgcyc 20 tolgrd 0.000001 noor nopm higp
ic fill
print ic


 vibran
 diag
 end

@testcheck ?ener -23.857772 0.0001 tntest1_chk2
stop
