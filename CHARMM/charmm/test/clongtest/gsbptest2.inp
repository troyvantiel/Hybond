* CHARMM 28 testcase gsbptest2.inp
* .Test the Generalized Solvent Boundary Potential for Aspartyl-tRNAse
* .do some dynamics with MIJ matrix and PHIX (external static field)
*  from gsbptest1.inp: gsbptest1 must have been run previously
* .Author: Benoit Roux
*

if ?pbeq .ne. 1 then stop

stream datadir.def

open read card unit 1 name @0top_all22_prot.inp
read rtf card unit 1
close unit 1

open read card unit 1 name @0par_all22_prot.inp
read param card unit 1
close unit 1

set rsphere 17.0              ! radius of the spherical region of interest
set rexcl    2.0              ! water exclusion radius 
set rincl    1.0              ! outer region protein inclusion radius
set rgeo     4.5              ! to setup the MMFP potential

calc rwater   = @rsphere - @rexcl
calc rprotein = @rsphere + @rincl 
calc rdroff   = @rsphere - @rgeo

! RS1 domain
read sequence card
* RNase
*
590 
MET ARG THR GLU TYR CYS GLY GLN LEU ARG LEU SER HSP VAL GLY GLN GLN VAL THR LEU
CYS GLY TRP VAL ASN ARG ARG ARG ASP LEU GLY SER LEU ILE PHE ILE ASP MET ARG ASP
ARG GLU GLY ILE VAL GLN VAL PHE PHE ASP PRO ASP ARG ALA ASP ALA LEU LYS LEU ALA
SER GLU LEU ARG ASN GLU PHE CYS ILE GLN VAL THR GLY THR VAL ARG ALA ARG ASP GLU
LYS ASN ILE ASN ARG ASP MET ALA THR GLY GLU ILE GLU VAL LEU ALA SER SER LEU THR
ILE ILE ASN ARG ALA ASP VAL LEU PRO LEU ASP SER ASN HSD VAL ASN THR GLU GLU ALA
ARG LEU LYS TYR ARG TYR LEU ASP LEU ARG ARG PRO GLU MET ALA GLN ARG LEU LYS THR
ARG ALA LYS ILE THR SER LEU VAL ARG ARG PHE MET ASP ASP HSD GLY PHE LEU ASP ILE
GLU THR PRO MET LEU THR LYS ALA THR PRO GLU GLY ALA ARG ASP TYR LEU VAL PRO SER
ARG VAL HSE LYS GLY LYS PHE TYR ALA LEU PRO GLN SER PRO GLN LEU PHE LYS GLN LEU
LEU MET MET SER GLY PHE ASP ARG TYR TYR GLN ILE VAL LYS CYS PHE ARG ASP GLU ASP
LEU ARG ALA ASP ARG GLN PRO GLU PHE THR GLN ILE ASP VAL GLU THR SER PHE MET THR
ALA PRO GLN VAL ARG GLU VAL MET GLU ALA LEU VAL ARG HSD LEU TRP LEU GLU VAL LYS
GLY VAL ASP LEU GLY ASP PHE PRO VAL MET THR PHE ALA GLU ALA GLU ARG ARG TYR GLY
SER ASP LYS PRO ASP LEU ARG ASN PRO MET GLU LEU THR ASP VAL ALA ASP LEU LEU LYS
SER VAL GLU PHE ALA VAL PHE ALA GLY PRO ALA ASN ASP PRO LYS GLY ARG VAL ALA ALA
LEU ARG VAL PRO GLY GLY ALA SER LEU THR ARG LYS GLN ILE ASP GLU TYR GLY ASN PHE
VAL LYS ILE TYR GLY ALA LYS GLY LEU ALA TYR ILE LYS VAL ASN GLU ARG ALA LYS GLY
LEU GLU GLY ILE ASN SER PRO VAL ALA LYS PHE LEU ASN ALA GLU ILE ILE GLU ASP ILE
LEU ASP ARG THR ALA ALA GLN ASP GLY ASP MET ILE PHE PHE GLY ALA ASP ASN LYS LYS
ILE VAL ALA ASP ALA MET GLY ALA LEU ARG LEU LYS VAL GLY LYS ASP LEU GLY LEU THR
ASP GLU SER LYS TRP ALA PRO LEU TRP VAL ILE ASP PHE PRO MET PHE GLU ASP ASP GLY
GLU GLY GLY LEU THR ALA MET HSE HSE PRO PHE THR SER PRO LYS ASP MET THR ALA ALA
GLU LEU LYS ALA ALA PRO GLU ASN ALA VAL ALA ASN ALA TYR ASP MET VAL ILE ASN GLY
TYR GLU VAL GLY GLY GLY SER VAL ARG ILE HSE ASN GLY ASP MET GLN GLN THR VAL PHE
GLY ILE LEU GLY ILE ASN GLU GLU GLU GLN ARG GLU LYS PHE GLY PHE LEU LEU ASP ALA
LEU LYS TYR GLY THR PRO PRO HSE ALA GLY LEU ALA PHE GLY LEU ASP ARG LEU THR MET
LEU LEU THR GLY THR ASP ASN ILE ARG ASP VAL ILE ALA PHE PRO LYS THR THR ALA ALA
ALA CYS LEU MET THR GLU ALA PRO SER PHE ALA ASN PRO THR ALA LEU ALA GLU LEU SER
ILE GLN VAL VAL LYS LYS ALA GLU ASN ASN

generate RS1 setup warning

! RS2 domain
gene RS2 setup duplicate RS1

! RS1 ligand : ASP1 (OT2 from OH in ADE)
read sequence ASP 1
generate ASP1 setup warning

! Water sphere inside a sphere of 20 A centered around (24.800 -18.289 4.071) 
read sequence TIP3 384
generate SOLV noangle nodihedral setup warning

! read coordinates (from bigmodel.pdb)
open read card unit 1 name @0rs12_asp1.crd
read coor card unit 1 
close unit 1

! center of active site (from read_pdb.out)
set xcen   1.0305  
set ycen -18.289
set zcen   4.071
coor stat

BOMLEV -1

! DELETE all water molecules located in
! the outside of a 15-A sphere from the active center
! A smaller sphere than 17-A one is used to consider the water exclusion radius.
delete atom sele .byres. ( type OH2 .and. .not. ( POINT @xcen @ycen @zcen CUT @rwater )) end

! DELETE all residues whose any atoms are not located inside a 18-A sphere so
! by considering only the VDW interactions with the remaining atoms and
! we can reduce the storage requirement and nonbonding update expense.
delete atom sele .not. ( .byres. ( POINT @xcen @ycen @zcen CUT @rprotein )) end

! FIND protein atoms which have 1-3 connections from atoms outside 
! the intended simulation sphere
define INNER   sele POINT @xcen @ycen @zcen CUT @rsphere end
define OUTER   sele .not. INNER end
define BONDOR  sele OUTER .and. .bonded. INNER end
define BONDIR1 sele INNER .and. .bonded. BONDOR end
define BONDIR2 sele INNER .and. .bonded. BONDIR1 end
define FIX13   sele .bygroup. ( BONDIR1 .or. BONDIR2 ) end

! FIND protein atoms near the boundary between r=15 and r=17.
define EXCLAT  sele ( INNER .and. .not. ( POINT @xcen @ycen @zcen CUT @rwater ) .and. -
                    .not. segid SOLV ) end
define EXCLAT  sele .bygroup. EXCLAT end

! FIX protein atom defined above and in outer region
CONS FIX  sele OUTER .or. ( INNER .and. ( EXCLAT .or. FIX13 )) end

! We don't need to consider the bare Coulombic interactions between IR and OR 
! because it is already incorporated into PHIX.
SCALAR CHARGE SET 0.0 sele .not. INNER end ! (note that the explict Coulombic interactions 
                                           !  with the inner region must be kept)

! Constrain the water molecules so that they cannot go out the simulation region 
MMFP
GEO SPHERE -
    Xref @xcen Yref @ycen Zref @zcen XDIR 1.0 YDIR 1.0 ZDIR 1.0 -
    QUARtic FORCE 0.5 P1 2.25 DROFF @rdroff select type oh2 end
END

!Now setup the Generalized Solvent Boundary Potential (GSBP)
PBEQ

OPEN READ FILE UNIT 50 NAME @9gsbptest_MIJ2.dat
READ MIJ UNIT 50
CLOSE UNIT 50

OPEN READ FILE UNIT 50 NAME @9gsbptest_PHIX.dat
READ PHIX UNIT 50
CLOSE UNIT 50

! One can define the maximum number of basis functions (MAXNPOL) 
!         which can be used for reaction field and
!         the step frequency (NLIST) for updating basis functions 
!         according to their contributions.

PRNLEV 8
GSBP SPHERE -
     NMPOL     2 MAXNPOL    4 NOSORT -
     SRDIST @rsphere RRXCEN @xcen RRYCEN @ycen RRZCEN @zcen
PRNLEV 5
END

!=====================================================================================
SKIP All Excl GSBP
ENER CUTNB 100.0
TEST FIRST STEP 0.001 TOL 0.0 SELE segid ASP1 .and. .not. hydrogen END
SKIP NONE

!From here, just run normal energy minimization or simulation

UPDA CTONnb 100.0 CTOFnb 101.0 CUTNb 102.0      ! no-cutoff for Coulombic interactions
ENER INBFrq 0                                   ! no nonbonding list update

MINI ABNR NSTEP 100

shake bonh

! Equilibration
DYNA STRT VERL NSTEP  100 TIME 0.001 CDIE -
     IPRFRQ 100 IHTFRQ 0 IEQFRQ 25 INBFRQ 0 -
     IUNREA -1 IUNWRI -1 IUNCRD -1 IUNVEL -1 -
     NPRINT 10 NSAVC 0 NSAVV 0 -
     FIRSTT 300.0 FINALT 300.0 TEMINC 1.0 -
     TWINDH 10.0 TWINDL -10.0 -
     IASORS 1 IASVEL 1 ICHECW 1

STOP
