* VIBRATIONAL ANALYSYS TEST CASE
*

! tries to constrain bonds across parallel partition boundaries
! VIBRAN and quasi are not parallel
if ?NUMNODE .gt. 1 then
 echo "Test NOT performed."
 stop
endif

if ?novibran .eq. 1 then
 echo "Test NOT performed."
 stop
endif

stream datadir.def

OPEN READ FORM UNIT 1 NAME  @0toph8.rtf
READ RTF  CARD UNIT 1
CLOSE UNIT 1
BOMLEV -2
OPEN READ FORM UNIT 2 NAME  @0param3.prm
READ PARAMETER CARD UNIT 2
CLOSE UNIT 2
BOMLEV -1

READ SEQUENCE CARD
*  FIRST SEQUENCE FOR SECOND DERIVATIVE TEST
*
    2
AMN CBX
GENERATE A   SETU

READ SEQUENCE CARD
*  SECOND SEQUENCE FOR SECOND DERIVATIVE TEST
*
    2
AMN CBX
GENERATE B  SETU

READ COOR CARD
*  SECOND DERIVATIVE TEST COORDINATES
*  DATE:    10/26/83      5: 1:25      CREATED BY USER: BROOKSG
*
   12
    1    1 AMN  CL    -3.13965  -1.54481   0.19285 A    1      0.00000
    2    1 AMN  C     -1.85653  -0.82937  -0.22784 A    1      0.00000
    3    1 AMN  O     -1.03318  -1.44920  -0.89737 A    1      0.00000
    4    2 CBX  N     -1.53757   0.43573   0.07578 A    2      0.00000
    5    2 CBX  H     -0.68605   0.77269  -0.26811 A    2      0.00000
    6    2 CBX  CA    -2.38883   1.35790   0.89148 A    2      0.00000
    7    3 AMN  CL     3.13965   1.54481   0.19285 A    3      0.00000
    8    3 AMN  C      1.85653   0.82937  -0.22784 A    3      0.00000
    9    3 AMN  O      1.03318   1.44920  -0.89737 A    3      0.00000
   10    4 CBX  N      1.53757  -0.43573   0.07578 A    4      0.00000
   11    4 CBX  H      0.68605  -0.77269  -0.26811 A    4      0.00000
   12    4 CBX  CA     2.38883  -1.35790   0.89148 A    4      0.00000

!!!!! 1 ! section one
! set up structures and compute energies
HBONDS IHBFRQ 50 ! CUTHA 80.0 CUTHB 20.0
PRINT     HBONDS ANAL
IC PARA
COOR ORIENT MASS
COOR COPY COMP
COOR ROTA COMP XDIR 1.0 PHI 5.0 SELE ATOM A * * END
PRINT COOR COMP
PRINT COOR
COOR ORIE RMS COMP MASS
PRINT COOR COMP
ENERGY
ENERGY COMP

!!!!! 2 ! section two
! test the second derivative potential
VIBRAN     NMOD     50
!GOTO DOIT
WRITE SECO CARD FINIT UNIT 6 STEP 0.0005
* list of second derivative elements that differ by more than TOL
* from that determined by finite differences.
*
END

!!!!! 3 ! section three
! compute and print some normal mode data
SHAKE BONDS TOL 1.0E-12
VIBRAN     NMOD     50
DIAG
PRINT NORM MODE 1 THRU 16 DIPO
PRINT NORM MODE 7 THRU 10 FACT 1.0 INTD  VECT
PRINT NORM MODE 7 THRU 10  INTD FINIT KCAL 2.0
OPEN WRITE FILE UNIT 38 NAME @9FOR038.DAT
WRITE NORM UNIT 38 FILE
* NORMAL MODES FOR VIBRTST TEST CASE
*
OPEN WRITE FILE UNIT 39 NAME @9FOR039.DAT
WRITE NORM MODE 7 THRU 16 UNIT 39 FILE
* NORMAL MODES FOR VIBRTST TEST CASE
*   MODES 7 THRU 16
*
OPEN WRITE FILE UNIT 40 NAME @9FOR040.DAT
WRITE SECO UNIT 40
* SECOND DERIVATIVES FOR VIBRTST TEST CASE
*

!!!!! 4 ! section four
! do some nomal mode projections
PROJ DIFF MODE 1 THRU 36 KCAL 1.0
PROJ BOND 1 CL 1 C
PROJ ANGL 1 CL 1 C 2 N
PROJ DIHE 1 CL 1 C 2 N 2 CA

!!!!! 5 ! section five
! test the edit include command
EDIT DELE  MODE 13 THRU 100
EDIT INCL DIFF
EDIT INCL ROTA  X
EDIT INCL ROTA  Y
EDIT INCL ROTA  Z
PRINT NORM DOTP

!!!!! 6 ! section six
! test the explore command
OPEN WRITE CARD UNIT 89 NAME @9FOR089.DAT
EXPLORE MODE 12 KCAL 2.0 GRID 7 UNIT 89 ADJU

!!!!! 7 ! section seven
! test sperical harmonic section
EDIT DELE MODE 1 THRU 40
LABEL DOIT
BASIS TR
print norm dotp
EDIT INCL SPHE  X  IX 0  IY 0  IZ 0  orth
EDIT INCL SPHE  Y  IX 0  IY 0  IZ 0  orth
EDIT INCL SPHE  Z  IX 0  IY 0  IZ 0  orth
EDIT INCL SPHE  X  IX 1  IY 0  IZ 0  orth
EDIT INCL SPHE  Y  IX 1  IY 0  IZ 0  orth
EDIT INCL SPHE  Z  IX 1  IY 0  IZ 0  orth
EDIT INCL SPHE  X  IX 0  IY 1  IZ 0  orth
EDIT INCL SPHE  Y  IX 0  IY 1  IZ 0  orth
EDIT INCL SPHE  Z  IX 0  IY 1  IZ 0  orth
EDIT INCL SPHE  X  IX 0  IY 0  IZ 1  orth
EDIT INCL SPHE  Y  IX 0  IY 0  IZ 1  orth
EDIT INCL SPHE  Z  IX 0  IY 0  IZ 1  orth
EDIT INCL SPHE  X  IX 2  IY 0  IZ 0  orth
EDIT INCL SPHE  Y  IX 2  IY 0  IZ 0  orth
EDIT INCL SPHE  Z  IX 2  IY 0  IZ 0  orth
EDIT INCL SPHE  X  IX 0  IY 2  IZ 0  orth
EDIT INCL SPHE  Y  IX 0  IY 2  IZ 0  orth
EDIT INCL SPHE  Z  IX 0  IY 2  IZ 0  orth
EDIT INCL SPHE  X  IX 0  IY 0  IZ 2  orth
EDIT INCL SPHE  Y  IX 0  IY 0  IZ 2  orth
EDIT INCL SPHE  Z  IX 0  IY 0  IZ 2  orth
EDIT INCL SPHE  X  IX 1  IY 1  IZ 0  orth
EDIT INCL SPHE  Y  IX 1  IY 1  IZ 0  orth
EDIT INCL SPHE  Z  IX 1  IY 1  IZ 0  orth
EDIT INCL SPHE  X  IX 0  IY 1  IZ 1  orth
EDIT INCL SPHE  Y  IX 0  IY 1  IZ 1  orth
EDIT INCL SPHE  Z  IX 0  IY 1  IZ 1  orth
EDIT INCL SPHE  X  IX 1  IY 0  IZ 1  orth
EDIT INCL SPHE  Y  IX 1  IY 0  IZ 1  orth
EDIT INCL SPHE  Z  IX 1  IY 0  IZ 1  orth
RAYLEIG SAVE
edit delete mode 1 thru 6
EDIT SHAKE
EDIT ORTH purge
RAYLEIG SAVE
!PRINT NORM  INTD
!WRITE NORM UNIT 60 FILE
!GOTO REDU
OPEN READ  FILE UNIT 39 NAME @9FOR039.DAT
READ NORM UNIT 39 APPE
PRINT NORM MODE 1 THRU 30 DOTP

!!!!! 8 ! section eight
! test setting up various basis and the RAYLeigh command
EDIT DELE MODE 1 THRU 100
BASIS TR SELE SEGID A END BALANCE NOORTH
PRINT NORM VECT
RAYLEIG
BASIS IC DIHE  NOORTH
PRINT NORM DOTPR
PRINT NORM INTD MODE 7 THRU 12

!!!!! 9 ! section nine
! set up that basis for the reduced calculation
EDIT DELE MODE 1 THRU 20
BASIS TR SELE SEGID A END BALANCE
BASIS IC DIHE
!BASIS IC ANGLE FIRST
!BASIS IC ANGLE SECOND
PRINT NORM
RAYLEIG SAVE
PRINT NORM INTD
OPEN WRITE FILE UNIT 60 NAME @9FOR060.DAT
WRITE NORM UNIT 60 FILE
* TRIAL BASIS FOR VIBRTST TEST CASE
* ALL DEGREES OF FREEDOM INCLUDED EXCEPT THE 6 T/R AND BOND STRETCH
*

!!!!! 10 ! section ten
! do the reduced basis calculation
LABEL REDU
TIMER 1
OPEN READ  FILE UNIT 60 NAME @9FOR060.DAT
REDUCE IUNBAS 60 ! BIG ! NFREQ 10
PRINT NORM INTD

! compare the reduced basis calculation with the full basis calc.
OPEN READ  FILE UNIT 38 NAME @9FOR038.DAT
READ NORM APPE UNIT 38 MODE 7 THRU 36 FILE
PRINT NORM DOTP
END
STOP
