* VIBPAFL.INP
*

! VIBRAN and quasi are not parallel
if ?NUMNODE .gt. 1 then
 echo "Test NOT performed."
 stop
endif

if ?novibran .eq. 1 then
 echo "Test NOT performed."
 stop
endif

stream datadir.def

READ RTF CARD
* Explicit methane topology file with bonds only
*
   20    1
MASS       1     H     1.00800
MASS      11     C    12.01100

RESIDUE METH 0.0000
ATOM       H1    H     0.000
ATOM       H2    H     0.000
ATOM       H3    H     0.000
ATOM       H4    H     0.000
ATOM       C     C     0.000
BOND      C H1     C H2     C H3     C H4
ANGLE   H1 C H2   H1 C H3  H1 C H4  H2 C H3  H3 C H4  H4 C H2
IC H1 C H2
IC H1 C H3
IC H1 C H4
IC H2 C H3
IC H3 C H4
IC H4 C H1
END
PRINT RTF

READ PARAM CARD
* Explicit bonding only methane parameter file
*
BOND
C  H   2.000   1.000
ANGLE
H C H 50.000   109.5
END
PRINT PARAM

READ SEQUENCE CARD
* A Molecule of Methane
*
1 METH
GENERATE A SETUP

READ COORD CARD
* Methane, at the origin, one H along z axis, one in  xz-plane, bonds=1
*
    5
    1    1 METH H1     0.00000   0.00000   1.00000
    2    1 METH H2     0.94260   0.00000  -0.33380
    3    1 METH H3    -0.47130   0.81630  -0.33380
    4    1 METH H4    -0.47130  -0.81630  -0.33380
    5    1 METH C      0.00000   0.00000   0.00000
PRINT COOR

IC FILL
PRINT IC

!MINI ABNR nSTEP 1000
!open unit 1 write form name methane.crd
!write coor card unit 1
!* Coordinates of methane after 1000 steps of abnr minimization
!*

!stop
read coor card
* COORDINATES OF METHANE AFTER 1000 STEPS OF ABNR MINIMIZATION
*  DATE:     8/14/ 5     13:45: 0      CREATED BY USER: brooks
*
    5
    1    1 METH H1      .00063    .00000    .99972 A    1       .00000
    2    1 METH H2      .94260   -.00001   -.33421 A    1       .00000
    3    1 METH H3     -.47161    .81650   -.33332 A    1       .00000
    4    1 METH H4     -.47162   -.81649   -.33331 A    1       .00000
    5    1 METH C       .00000    .00000   -.00028 A    1       .00000

PRINT COOR

IC FILL
PRINT IC

OPEN WRITE UNIT 19 FILE NAME @9METH.TRJ
OPEN WRITE UNIT 20 FILE NAME @9METH7_15.TRJ
OPEN WRITE UNIT 21 FILE NAME @9METH1.NMD
OPEN WRITE UNIT 22 FILE NAME @9METH2.NMD
OPEN WRITE UNIT 23 FILE NAME @9METH3.NMD

VIBRAN NMOD 15
DIAG NFREQ 15
WRITE TRAJ MODE 7 THRU 15 STEP 0.002 PHASE 3.6 TEMP 300.0 UNIT 19
* file
*
WRITE TRAJ MODE 7 THRU 15 SUPE STEP 0.01 PHASE 3.6 TEMP 300.0 UNIT 20
* Methane modes 7 through 15 superimposed, 1 ps 
*
WRITE NORM MODE 1 THRU 7 UNIT 21
* MINIMIZED METHANE, MODES 1 THRU 7
*
WRITE NORM MODE 5 THRU 12 UNIT 22
* MINIMIZED METHANE, MODES 5 THRU 12
*
WRITE NORM MODE 10 THRU 15 UNIT 23
* MINIMIZED METHANE, MODES 10 THRU 15
*
END

OPEN READ UNIT 19 FILE NAME @9METH.TRJ
!ANAL
!ACCUM COORD FIRSTU 19 STOP 900
!BUILD ATOM DYN R AVE $ DYN X AVE $ DYN Y AVE $ DYN Z AVE $
!PRINT TABLE COLUMN
!BUILD ATOM DYN R M2 $ DYN X M2 $ DYN Y M2 $ DYN Z M2 $
!PRINT TABLE COLUMN
!BUILD ATOM DYN R M2 PAX $ DYN X M2 PAX $ DYN Y M2 PAX $ DYN Z M2 PAX $
!PRINT TABLE COLUMN
!END
OPEN READ UNIT 21 FILE NAME @9METH1.NMD
OPEN READ UNIT 22 FILE NAME @9METH2.NMD
OPEN READ UNIT 23 FILE NAME @9METH3.NMD
VIBRAN NMOD 15
READ NORM UNIT 21
EDIT DELETE MODES 5 THRU 7
PAFL ATOM COOR SAVE TEMP 300.0 TFREQ 5.0
READ NORM UNIT 22
EDIT DELETE MODES 6 THRU 8
PAFL ATOM COOR SAVE CONT TEMP 300.0 TFREQ 5.0
READ NORM UNIT 23
PAFL ATOM COOR CONT TEMP 300.0 TFREQ 5.0
END

OPEN READ UNIT 21 FILE NAME @9METH1.NMD
OPEN READ UNIT 22 FILE NAME @9METH2.NMD
OPEN READ UNIT 23 FILE NAME @9METH3.NMD
VIBRAN NMOD 15
READ NORM UNIT 21
EDIT DELETE MODES 5 THRU 7
PAFL ATOM SAVE TEMP 300.0 TFREQ 5.0
READ NORM UNIT 22
EDIT DELETE MODES 6 THRU 8
PAFL ATOM SAVE CONT TEMP 300.0 TFREQ 5.0
READ NORM UNIT 23
PAFL ATOM CONT TEMP 300.0 TFREQ 5.0
END

!NOTE THAT THE FLUCTUATIONS CALCULATED FROM THE TRAJECTORY WITH ANAL SHOULD
!BE ABOUT ONE-THIRD AS GREAT AS THOSE FROM VIBRAN.  THE FACTOR OF ONE-THIRD
!COMES IN BECAUSE THE TRAJECTORY IS NINE INDIVIDUAL MODES WHICH ARE
!TAKEN CONSECUTIVELY, RATHER THAN SIMULTANEOUSLY.  THE RMS DISPLACEMENTS
!OF THE ATOMS ARE THUS SQRT(1/9)=1/3 WHAT THEY SHOULD BE.  THE "ABOUT"
!COMES IN BECAUSE VIBRAN DOES AN ANALYTIC CALCULATION, WHEREAS ANAL DOES
!A DISCRETE STEP EVALUATION.
STOP
