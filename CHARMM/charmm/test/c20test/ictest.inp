* INTERNAL COORDINATE AND COORDINATE MANIPULATION TEST CASE
*

stream datadir.def

OPEN READ FORM UNIT 1 NAME  @0toph8.rtf
READ RTF  CARD UNIT  1
BOMLEV -2
OPEN READ FORM UNIT 2 NAME  @0param4.prm
READ PARAMETER CARD UNIT 2
BOMLEV -1

READ SEQUENCE  CARD
*  FIRST SEQUENCE FOR SECOND IC TEST
*
    2
AMN CBX
GENERATE A SETU
GENERATE B SETU DUPLICATE A

IC    PARA  ! fill IC angles and bond values from parameters
PRINT IC
!
! introduce an IC cross link between segments
!
IC  EDIT
DIHE   1  CL  1  C    2  N    2  CA   0.0
DIHE   3  CL  3  C    4  N    4  CA   0.0
DIHE   2  CA  2  N    2  H    4  H  180.0
DIHE   2  N   2  H    4  H    4  N  180.0
DIHE   2  H   4  H    4  N    4  CA 180.0
ANGL   2  N   2  H    4  H          100.0
ANGL   2  H   4  H    4  N          100.0
DIST   2  H   4  H                  4.5
END
PRINT IC
!
! start off and build this structure
!
IC   SEED  2 CA 2 N 2 H
PRINT COOR
IC  BILD
PRINT     COORD
COOR COPY COMP
COOR ORIENT COMP ! translate and rotate to align with origin
PRINT COOR COMP
COOR STATS COMP SELE ATOM A * * END

OPEN WRITE CARD UNIT 44 NAME @9FOR044.DAT
WRITE COOR COMP CARD UNIT 44
* INITIAL COORDINATES FOR AMN-CBX *2
*
OPEN WRITE CARD UNIT 51 NAME @9FOR051.DAT
WRITE IC CARD UNIT 51
OPEN WRITE CARD UNIT 52 NAME @9FOR052.DAT
WRITE IC CARD RESI UNIT 52
OPEN READ CARD UNIT 51 NAME @9FOR051.DAT
READ IC CARD UNIT 51
OPEN READ CARD UNIT 52 NAME @9FOR052.DAT
READ IC CARD UNIT 52
print ic

!!!!! 1 first marker for ICTEST

! generate segment B by a z-axis rotation. coordinates should match
COOR COPY SELE SEGID A END
COOR DUPLICATE SELE ATOM A * * END SELE ATOM B * * END
PRINT COOR
COOR ROTATE PHI 180.0 ZDIR 1.0 SELE SEGID B END
COOR RMS

HBONDS IHBFRQ 50
PRINT     HBONDS ANAL
IC FILL
ENER SHIFT
! print out forces
COOR FORCE COMP
coor print comp
! print out acceleration (force/mass)
COOR FORCE COMP MASSW
coor print comp
COOR SCALE COMP FACT 0.1
PRINT COOR COMP
! print IC analytic derivatives for this vector
IC DERI COMP
PRINT IC

!!!!! 2 second marker for ICTEST

! move one of the atoms (A 1 CL) by rebuilding it
COOR COPY COMP
IC FILL
IC KEEP SELE ATOM * * CL SHOW END
PRINT IC
IC EDIT
DIST 1 CL 1 C  3.0
DIHE 1 CL 1 C 2 N 2 CA 10.0
END
COOR INIT SELE ATOM * * CL SHOW END
PRINT IC
PRINT COOR
IC BILD
IC FILL
PRINT IC
IC DIFF
PRINT IC


!!!!! 3 third marker for ICTEST

! test some simple manipulation commands
COOR ORIENT
COOR ORIENT RMS
COOR AVER FACT 0.8
COOR ORIENT
COOR ROTATE XDIR 2.0 YDIR 0.5 ZDIR 1.0 PHI 20.0
COOR TRAN XDIR 0.5 YDIR 1.0 ZDIR 2.0
COOR ORIENT
COOR ORIE RMS
PRINT     COORD
IC  FILL
PRINT IC
COOR SWAP
IC DIFF
PRINT IC
PRINT COOR
!
!!!!! 4 fourth marker for ICTEST
! TEST SOME ADVANCED CORMAN COMMANDS
!
COOR MIND SELE SEGID A END SELE ATOM B 1 CL END
COOR MIND SELE ATOM A 1 CL END EXCLUSIONS

COOR DIST CUT 4.0 ENERGY SELE SEGID A END SELE SEGID B END
COOR DIST CUT 10.0 ENERGY CLOSE SELE TYPE CL END SELE TYPE CA END 14EXCL
COOR DIST ENERGY NONONB 14EXCL EXCL SELE SEGID A END SELE SEGID A END

COOR DIST ENERGY SELE TYPE CL END SELE TYPE CA END
COOR DIST SELE TYPE CL END
COOR DIST WEIGH XDIR 0.0 YDIR 0.0 ZDIR 0.0
SCALAR WMAIN STAT
PRINT COOR

COOR RGYR
COOR RGYR MASS
SCALAR WMAIN SET 1.0
COOR RGYR WEIGH
COOR RGYR WEIGH SELE SEGID A END

!!!!! 5 fifth marker for ICTEST
! compute volume
SCALAR WMAIN = RADI
COOR SEARCH -
      XMIN -4.0 XMAX 4.0 XGRID 20 -
      YMIN -4.0 YMAX 4.0 YGRID 20 -
      ZMIN -4.0 ZMAX 4.0 ZGRID 20
SCALAR WMAIN SHOW
SCALAR WMAIN = RADI
COOR SEARCH -
      XMIN -8.0 XMAX 8.0 XGRID 40 -
      YMIN -8.0 YMAX 8.0 YGRID 40 -
      ZMIN -8.0 ZMAX 8.0 ZGRID 40
SCALAR WMAIN SHOW
SCALAR WMAIN SET 2.5
COOR SEARCH RCUT  3.5 -
      XMIN -8.0 XMAX 8.0 XGRID 80 -
      YMIN -8.0 YMAX 8.0 YGRID 80 -
      ZMIN -8.0 ZMAX 8.0 ZGRID 80
SCALAR WMAIN SHOW
SCALAR WMAIN SET 2.5
SCALAR WMAIN STORE 1
SCALAR WMAIN SET 3.5
SCALAR WMAIN STORE 2
COOR VOLUME SPACE 64000
SCALAR WMAIN SHOW
COOR VOLUME SPACE 256000
SCALAR WMAIN SHOW

SCALAR WMAIN SET 2.5
COOR SEARCH RCUT 3.5 -
   XMIN -6.4633 XMAX  6.4633 XGRID  63 -
   YMIN -4.1744 YMAX  4.1744 YGRID  41 -
   ZMIN -2.5000 ZMAX  2.5000 ZGRID  24
SCALAR WMAIN SHOW

!!!!! 6 sixth marker for ICTEST
! test angle and bond edit features 9/84

ic edit
dist 1 c 3 c 0.0
angl 1 c 4 n 3 c 0.0
angl 4 n 3 c 2 n 0.0
end
ic fill
print ic


!!!!! 7 seventh marker for ICTEST
! test surface command
COOR SURFACE ACCESS
PRINT COOR
COOR SURFAC CONTA
PRINT COOR
COOR SURFAC ACCESS ACCUR 0.02
PRINT COOR

if ?NUMNODE .gt. 1 then 
    echo "Test section not parallel - SKIPPED"
    goto skipsec
endif

!!!!! 8 eigth marker for ICTEST
! process coordinates with shake
coor copy ! bring up perturbed coordinates
shake  bond para
coor shake
print coor
ic fill
print ic
coor orie rms mass
ic diff
print ic
!!!!! 9 ninth marker for ICTEST
IC KEEP THIRD SELE TYPE N END
PRINT IC

label skipsec
STOP
