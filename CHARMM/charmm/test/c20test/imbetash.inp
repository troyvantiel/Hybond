*  INPUT FOR IMAGE BETA SHEET TEST
*

stream datadir.def

OPEN READ FORM UNIT 1 NAME  @0toph9.rtf
READ RTF  CARD UNIT 1
BOMLEV -2
OPEN READ FORM UNIT 2 NAME  @0param6.prm
READ PARAMETER CARD UNIT  2
BOMLEV  0

READ SEQUENCE CARD
*  BETA SHEET SEQUENCE FOR IMAGE TEST RUNS
*
    2
ALA ALA
GENERATE  MAIN   SETU first none last none

print psf

SET 1 -9.0
SET 2 6.78
READ IMAG CARD PRINT
*  IMAGE FILE FOR BETA SHEET TEST CASE
*  ONLY ONE TRANSFORMATION (Z AXIS ROTATION)
*
IMAGE Z
ROTATE 0.0 0.0 1.0 180.0
IMAGE UP
ROTATE 1.0 0.0 0.0 @1
TRANSLATE @2 0.0 0.0
IMAGE DN
DEFINE INVE UP
IMAGE UP2
DEFINE UP UP
IMAGE DN2
DEFINE DN DN
IMAGE ZUP
DEFINE Z UP
IMAGE ZDN
DEFINE Z DN
IMAGE ZUP2
DEFINE Z UP UP
IMAGE ZDN2
DEFINE Z DN DN
END

READ COORD CARD
*  IMAGE ALA BETASHEET COORDINATES
*  DATE:     9/ 4/83      3:37:17      CREATED BY USER: BROOKSG
*
   12
   27    1 ALA  N     -1.26753  -1.97054  -0.41601 MAIN 1      0.00000
   28    1 ALA  H     -1.35589  -0.99819  -0.59555 MAIN 1      0.00000
   29    1 ALA  CA    -0.05921  -2.63838  -0.88260 MAIN 1      0.00000
   30    1 ALA  CB    -0.11507  -2.77810  -2.39679 MAIN 1      0.00000
   31    1 ALA  C      1.20813  -1.87303  -0.50379 MAIN 1      0.00000
   32    1 ALA  O      1.36305  -0.68373  -0.78711 MAIN 1      0.00000
   33    2 ALA  N      2.08539  -2.54548   0.24095 MAIN 2      0.00000
   34    2 ALA  H      1.83824  -3.42994   0.57046 MAIN 2      0.00000
   35    2 ALA  CA     3.37509  -1.98729   0.62249 MAIN 2      0.00000
   36    2 ALA  CB     3.49281  -2.01941   2.13952 MAIN 2      0.00000
   37    2 ALA  C      4.58369  -2.73251   0.06111 MAIN 2      0.00000
   38    2 ALA  O      4.68358  -3.95131   0.19305 MAIN 2      0.00000

IC PURGE

UPDATE INBF 0 IHBF 0  IMGFRQ 100
IMPATCH LINK PRIM MAIN 2  UP MAIN 1 SETU

IC FILL
PRINT IC
OPEN WRITE CARD UNIT 12 NAME @9FOR012.DAT
WRITE     COORDS    CARD   UNIT  12
*  IMAGE ALA BETASHEET COORDINATES
*
MINI   -
    ABNR  NSTEP 25 INBFRQ 50 STEP 0.02  -
       IHBFRQ 50 CUTHB 4.5 CTOFHB 4.0 CTONHB 3.5 ALL -
       CUTHA 65.0 CTOFHA 60.0 CTONHA 55.0 -
    ATOM CUTNB 8.0 CTOFNB 7.5 CTONNB 7.0 WMIN 1.5 STRICT 0.0
PRINT IMAGE FORCE
PRINT IMAG PSF

COOR PRINT    imag
COOR FORCE COMP
PRINT COOR COMP
IC FILL
PRINT IC
HBONDS
PRINT HBONDS

PRINT ENERGY
OPEN WRITE CARD UNIT 14 NAME @9FOR014.DAT
WRITE     COORDS    CARD   UNIT  14
*  IMAGE ALA BETASHEET COORDINATES
* ANGLE @1 DISTANCE @2 ENERGY ?ENER
*
stop
