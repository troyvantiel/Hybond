* QUASIHARMONIC DYNAMICS TEST CASE
*

! VIBRAN and quasi are not parallel
if ?NUMNODE .gt. 1 then
 echo "Test NOT performed."
 stop
endif

stream datadir.def
BOMLEV -2
OPEN READ FORM UNIT 1 NAME  @0toph9.rtf
READ RTF  CARD UNIT 1
CLOSE UNIT 1
OPEN READ FORM UNIT 2 NAME  @0param5.prm
READ PARAMETER CARD UNIT 2
CLOSE UNIT 2

READ SEQUENCE CARD
*  ETHANOL FOR THE TEST OF QUASIHARMONICS
*
    1
ETH
GENERATE ETH   SETU

!GOTO QUASI

!!!!! 1 marker for the first section of quasiharmonics testing
IC PARAM
IC SEED 1 C2 1 C1 1 O
IC BUILD

COOR ORIENT MASS

MINI NRAP NSTEP 5  NBXMOD 5

IC FILL
PRINT IC
PRINT COORD
OPEN WRITE FILE UNIT 25 NAME @9FOR025.DAT
WRITE COOR UNIT 25 FILE
* QUASI HARMONIC TEST CASE MINIMUM ENERGY COORDINATES
*

!!!!! 2 marker for the first section of quasiharmonics testing
SHAKE BONH PURGE
OPEN WRITE CARD UNIT 89 NAME @9FOR089.DAT
DYNA VERL NSTEP 500  timestp .001 INBFRQ 0 IHBFRQ 0 -
      FIRSTT 50. FINALT 50. NPRINT 50 -
     ISEED 311 IPRFRQ 500 IHTFRQ 0 IEQFRQ 100 NTRFRQ 100   IUNWRI 89
OPEN READ  CARD UNIT 89 NAME @9FOR089.DAT
OPEN WRITE FILE UNIT 40 NAME @9FOR040.DAT
DYNA REST IUNREA 89  NSTEP 1000 IEQFRQ 0  -
      NSAVC 1 IUNCRD 40  IUNWRI -1  IPRFRQ 1000
PRINT COOR
!!!!! 3 third section
OPEN READ  FILE UNIT 40 NAME @9FOR040.DAT
OPEN WRITE FILE UNIT 60 NAME @9FOR060.DAT
OPEN READ  FILE UNIT 25 NAME @9FOR025.DAT
READ COOR COMP UNIT 25 FILE RESTART
MERGE FIRSTU 40 NUNIT 1 OUTPUTU 60 -
      ORIENT MASS SELE ALL END ! PRINT
COOR ORIE RMS MASS
!!!!! 4 fourth section
LABEL QUASI
OPEN READ  FILE UNIT 60 NAME @9FOR060.DAT
COOR DYNA FIRSTU 60 NUNIT 1
OPEN WRITE CARD UNIT 38 NAME @9QUASI.AVE
WRITE COOR CARD UNIT 38
* AVERAGE COORDINATES FOR ETH DYNAMICS
*
PRINT COOR
SCALAR WMAIN STAT
IC FILL
PRINT IC
OPEN READ  FILE UNIT 25 NAME @9FOR025.DAT
READ COOR COMP UNIT 25 FILE RESTART
IC DIFF
PRINT IC
!!!!! 5 fifth section
VIBRAN
OPEN READ  FILE UNIT 60 NAME @9FOR060.DAT
QUASI FIRSTU 60 NUNIT 1 TEMP 56.7
PRINT NORM INTD 
END
!!!!! 6 ! section six
! test setting up various basis and the RAYLeigh command
OPEN READ  FILE UNIT 25 NAME @9FOR025.DAT
READ COOR COMP UNIT 25 FILE RESTART
COOR COPY
VIBRAN
DIAG
PRINT NORM INTD
EDIT DELETE MODE 1 THRU 20
!!!!! 7 ! section seven
! set up the basis for the reduced calculation
BASIS IC DIHE
BASIS IC BOND FIRST
BASIS IC BOND SECOND
BASIS IC ANGLE FIRST
BASIS IC ANGLE SECOND
RAYLEIG SAVE
!PRINT NORM INTD dotpr
OPEN WRITE FILE UNIT 32 NAME @9FOR032.DAT
WRITE NORM UNIT 32
* TRIAL BASIS FOR QUASI TEST CASE
*
!!!!! 8 ! section eight
! do the reduced basis calculation
TIMER 1
OPEN READ  FILE UNIT 32 NAME @9FOR032.DAT
REDUCE IUNBAS 32
PRINT NORM INTD
end
!!!!! 9 ! section nine
! do the reduced basis quasiharmonic calculation
OPEN READ  CARD UNIT 25 NAME @9QUASI.AVE
READ COOR UNIT 25 CARD RESTART
vibran
OPEN READ  FILE UNIT 60 NAME @9FOR060.DAT
OPEN READ  FILE UNIT 32 NAME @9FOR032.DAT
RBQUASI IUNBAS 32 FIRSTU 60 NUNIT 1 TEMP 56.7
COOR COPY
PRINT NORM INTD
END
STOP
