* TESTS A NUMBER OF CONSTRAINTS CALCULATIONS
*

!  tries to use shake on bonds across parallel partition boundaries
if ?NUMNODE .gt. 1 then
 echo "Test NOT performed."
 stop
endif

stream datadir.def
BOMLEV -2

OPEN READ  FORM UNIT 1 NAME @0toph9.rtf
READ RTF   CARD UNIT 1
OPEN READ  FORM UNIT 2 NAME @0param6.prm
READ PARAM CARD UNIT 2
READ SEQUENCE CARDS
* TRYPTOPHAN
*
    1
TRP
GENERATE TRYP setup

open write unit 8 file name @9constest.psf
WRITE PSF FILE UNIT 8
* TRYPTOPHAN USING EXPLICIT HYDROGEN
*

ic purge

READ COORD CARDS
* TRYPTOPHAN BUILT AND MINIMIZED WITH SD 50 STEPS, ABNR 500 STEP,
* AND CG 50 STEPS
* EPS=R POTENTIAL
*
   19
    1    1 TRP  HT1    0.10124   0.57409  -1.38529
    2    1 TRP  HT2    1.71114   0.43637  -1.90440
    3    1 TRP  N      1.06717   0.30225  -1.10075
    4    1 TRP  HT3    1.07999  -0.68894  -0.79051
    5    1 TRP  CA     1.47212   1.19190   0.02727
    6    1 TRP  CB     2.69565   2.02615  -0.34753
    7    1 TRP  CG     3.92166   1.16873  -0.67893
    8    1 TRP  CD1    4.81252   0.77506   0.29018
    9    1 TRP  CD2    4.29511   0.67909  -1.89046
   10    1 TRP  NE1    5.72693   0.04608  -0.32026
   11    1 TRP  HE1    6.09389  -0.74349   0.12669
   12    1 TRP  CE2    5.42381  -0.01032  -1.60094
   13    1 TRP  CE3    3.76875   0.77367  -3.15481
   14    1 TRP  CZ2    6.13756  -0.67683  -2.56270
   15    1 TRP  CZ3    4.47167   0.10800  -4.14811
   16    1 TRP  CH2    5.63634  -0.60503  -3.85644
   17    1 TRP  C      0.27986   2.11295   0.30889
   18    1 TRP  OT1   -0.40216   2.48057  -0.63623
   19    1 TRP  OT2   -0.10570   2.19226   1.45887

open write unit 8 file name @9constest.crd
WRITE COOR FILE UNIT 8
* TRYPTOPHAN BUILT AND MINIMIZED WITH SD 50 STEPS, ABNR 500 STEP,
* AND CG 50 STEPS
* EPS=R POTENTIAL
*
coor copy comp
update CUTNB 100.0 CTONNB 99.0 CTOFNB 99.5
mini abnr nstep 50 nprint 10
coor orie rms mass
energy
open write unit 17 file name @9constest.dat
WRITE COOR FILE UNIT 17
* TRYPTOPHAN MINIMIZED FOR CONSTEST
*
!!!!! 1 marker for the first section of constraint testing
! test harmonic constraints
cons harm force 1.0 sele atom * * c* end mass
print cons
coor copy comp
coor rotate zdir 1.0 phi 5.0
gete print
mini abnr nstep 50 nprint 10
print energy
coor orie rms mass
gete print
cons harm force 0.0 sele all end
print cons
!!!!! 2 second section of constraint
! test dihedral constraints
open read unit 17 file name @9constest.dat
READ COOR FILE UNIT 17 RESTART
COOR COPY COMP
ic fill
print ic
cons dihe bynum 3 5 6 7 force 50.0 min -68.37
cons dihe tryp 1 ca  tryp 1 cb  tryp 1 cg  tryp 1 cd2 force 50.0 min 99.85
print cons
gete print
cons cldh
cons dihe bynum 3 5 6 7 force 50.0 min -58.37
cons dihe tryp 1 ca  tryp 1 cb  tryp 1 cg  tryp 1 cd2 force 50.0 min 89.85
gete print
mini abnr nstep 50 nprint 10
print energy
ic diff
print ic
cons cldh
print cons
Label ici
!!!!! 2bis second section bis of constraint
! test dihedral constraints with cos form.
open read unit 17 file name @9constest.dat
READ COOR FILE UNIT 17 RESTART
COOR COPY COMP
ic fill
print ic
cons dihe bynum 3 5 6 7 force 50.0 min -68.37 period -10
cons dihe tryp 1 ca  tryp 1 cb  tryp 1 cg  tryp 1 cd2 -
          force 50.0 min 99.85 period 10
print cons
gete print
cons cldh
cons dihe bynum 3 5 6 7 force 100.0 min -58.37 period 1
cons dihe tryp 1 ca  tryp 1 cb  tryp 1 cg  tryp 1 cd2 -
          force 100.0 min 89.85 period 1
gete print
mini abnr nstep 50 nprint 10
print energy
ic diff
print ic
cons cldh
print cons
!!!!! 3 third section
!  test internal coordinate constraints
open read unit 17 file name @9constest.dat
READ COOR FILE UNIT 17 RESTART
open read unit 8 file name @9constest.crd
READ COOR FILE UNIT 8 RESTART COMP
ic fill comp
cons ic bond 200.0 angle 100.0
print cons
gete print
mini abnr nstep 50 nprint 10
print energy
ic diff
print ic
ic fill comp
cons ic bond 200.0 dihe 50.0
print cons
mini abnr nstep 50 nprint 10
print energy
ic diff
print ic
cons ic bond 0.0 angle 0.0 dihe 0.0
print cons
!!!!! 4 fourth section
!  test droplet potential
open read unit 17 file name @9constest.dat
READ COOR FILE UNIT 17 RESTART
coor copy comp
cons drop force 0.003 expo 4
print cons
faster off
skip all excl cdro
gete print
test first step 0.001
SKIP NONE
faster default
mini abnr nstep 50 nprint 10
print energy
coor orie rms mass
coor rgyr mass  comp
coor rgyr mass
coor orie mass
coor tran xdir 1.0 ydir 1.0 zdir 1.0
coor rotate xdir 1.2 ydir -0.5 zdir 0.7 phi 62.45
gete print
cons drop force 0.0
print cons
!!!!! 5 fifth section
!  test shake constraints
open read unit 8 file name @9constest.crd
READ COOR FILE UNIT 8 RESTART
coor copy comp
shake nofast bonh bond
mini conj nstep 25
print energy
ic diff
print ic
shake nofast
!!!!! 6 sixth section
!   test rigid constraints
open read unit 8 file name @9constest.crd
READ COOR FILE UNIT 8 RESTART
coor copy comp
cons fix sele atom * * n* .or. atom * * o* end
mini abnr nstep 50 nprint 10
print energy
coor diff
print coor
cons fix sele none end
!!!!! 7 seventh section
!   test rigid constraints with purging
open read unit 8 file name @9constest.crd
READ COOR FILE UNIT 8 RESTART
coor copy comp
cons fix sele atom * * c* end purge
mini abnr nstep 50 nprint 10
print energy
coor diff
print coor
