* CHARMM 25 Testcase ewald_atom.inp
* Bernard R. Brooks, 15-JUL-97, c25b1
* water-vacum system
*

if ?lrvdw .ne. 1 then
 echo "Test NOT performed."
 stop
endif

bomlev -3
stream datadir.def
set 6 @9                  ! 9 is used for cell dimension

read rtf card              
* TIPS3P WATER TOPOLOGY FILE
* TIPS3P WATER TOPOLOGY FILE
*
   18    1
MASS     4 HT     1.00800 ! TIPS3P WATER HYDROGEN
MASS    64 OT    15.99940 ! TIPS3P WATER OXYGEN

RESI TIP3    .000   ! TIPS3P WATER MODEL
GROUP
ATOM OH2  OT     -0.834
ATOM H1   HT      0.417
ATOM H2   HT      0.417
BOND OH2 H1  OH2 H2  H1 H2  ! THE LAST BOND IS NEEDED FOR SHAKE
THET H1  OH2  H2
!                          NOTE: NO DONORS OR ACCEPTORS FOR TIPS3P
PATC  FIRS NONE LAST NONE

END

read param card
* TIPS3P WATER PARAMETER FILE
*
BOND
HT   OT     450.0       0.9572 ! from TIPS3P geometry
HT   HT       0.0       1.5139 ! from TIPS3P geometry (for SHAKE w/PARAM)
THETAS
HT   OT   HT      55.0     104.52    ! FROM TIPS3P GEOMETRY
!
NONBONDED   GROUP CDIEL SWITCH VGROUP VDISTANCE VSWITCH -
     CUTNB 9.0  CTOFNB 8.0 CTONNB 8.0 EPS 1.0  E14FAC 1.0  WMIN 1.5
!
HT       0.0440    -0.0498    0.8000 !TIP3P water hydrogen, see NBFIX below
OT       0.8400    -0.1591    1.6000 !TIP3P water oxygen,   see NBFIX below
!!HT       0.0440    1.0       0.8000
!!OT       0.8400    6.0       1.6000

NBFIX
!OT   OT      -0.152073   3.5365  ! TIPS3P VDW INTERACTION
!HT   OT      -0.05       1.0  ! MODIFIED TIPS3P WITH CATASTROPHY ZONE REMOVED
OT   OT      -0.152073  3.5365  ! TIPS3P VDW INTERACTION
HT   HT      -0.04598   0.4490
HT   OT      -0.08363   1.9927
! FOR NOW, USE CHARLIE'S PARAMETERS
!OT OT -0.1521 3.5364
!HT HT -0.04598 0.4490
!OT HT -0.08363 1.9927

END

read sequence TIP3 560
gener WAT noangle nodihedral

open unit 14 read card name @0wat560.crd 
read coor card unit 14             
close unit 14

set cutnb  21
set cutim  21
set 7 25.40608
set 8 25.40608
set 9 25.40608 
set fft 48
crystal define cubic @7 @8 @9 90.0 90.0 90.0
CRYStal BUILd  CUTOff @cutim
image byres xcen 0.0 ycen 0.0 zcen 0.0 sele all end

skip bond angl 
calc ctofnb = @cutnb
calc ctonnb = @cutnb

energy -
    inbfrq 1 atom vatom cutnb @cutnb ctofnb @ctofnb ctonnb @ctonnb -
    cdie eps 1. -
    shift vswitch cutim @cutim imgfrq 1 wmin 0.5 -
    Ewald kappa 0.420 pmEwald order 4 fftx @fft ffty @fft fftz @fft -
    ntrfq 200 - !PME

set lrc lrc
update @lrc -
    cutnb @cutnb ctofnb @ctofnb ctonnb @ctonnb cutim @ctofnb 
energy

calc vdw = ?vdw + ?lrv  + ?imnb  
calc diff = ?ener + 5538
calc diff = @diff * @diff 
!if @diff .gt. .01 goto deadly

!------------- LOOP START -----------------------------
label nextcut 
   decr cutnb by 3.
   calc ctofnb = @cutnb
   calc ctonnb = @cutnb

   update @lrc -
     cutnb @cutnb ctofnb @ctofnb ctonnb @ctonnb cutim @ctofnb 
    
   energy ! lrc cutnb @cutnb ctofnb @ctofnb ctonnb @ctonnb cutim @ctofnb 
   calc vdw = ?vdw + ?elrc + ?imnb  
   echo Result @vdw ?vdw ?elrc  ?imnb  
if @cutnb .gt. 9 goto nextcut
!------------ LOOP END ----------------------------------

! Energies should go from -5538 to -5537 
calc diff = ?ener + 5537.0
calc diff = @diff * @diff 
if @diff .gt. .01 goto deadly


stop

!---------------------- DEADLY ----------------------------------------
! Force charmm to die via a wrndie using a command that does not exist
!
label deadly
   bomlev 1
   BAD ANSWERS
