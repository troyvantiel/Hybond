* Met-enkephalin mode
* Multiple time scaled method
*

if ?mts .ne. 1 then
 echo "MTSM1 TESTCASE RESULT: SKIP"
 stop
endif

stream datadir.def

! rtf and parameter
OPEN UNIT 1 READ FORM NAME @0toph19.rtf
READ RTF CARD UNIT 1
CLOSE UNIT 1

OPEN UNIT 1 READ FORM NAME @0param19.prm
READ PARAM CARD UNIT 1
CLOSE UNIT 1

READ SEQU CARD
* Met-enkephalin
*
5 
tyr gly gly phe met

GENERATE ENKP SETUP

IC PARAM
IC SEED 2 C 3 N 3 CA
IC BUILD

MINI SD NSTEP 100 INBFRQ 10
MINI ABNR NSTEP 300 NPRINT 100 INBFRQ 10 IHBFRQ 10

! Molecular Dynamics begin
!

! Multiple Time Scaled Method Start
! Part I - Single Reversible RESPA method

UPDATE  RDIE  VATOM  SWITCH  VSWITCH  EPS 1.0 -
      CUTNB 110.0 CTOFNB 108.5  CTONNB 107.0 

MTS 6 
BOND
ANGL
END

OPEN WRITE UNIT 31 CARD NAME @9FOR032.RST

DYNA VVER START NSTEP 1000 TIME 0.0005 -
     NPRINT 100   IPRFRQ  1000   -
     INBFRQ 20   IHBFRQ 0 FIRSTT 200.0 -
     IUNREA -1  IUNWRI 31  IUNCRD -1  IUNVEL -1 -
     KUNIT -1  IUNO -1 NSAVC 5  NSAVV 5  NSNOS 10 ISVFRQ 1000 

! Part II
! Double Reversible RESPA method

MTS 5 2
MASS 2  3.0
BOND 1
ANGL 1
DIHE 2
END

OPEN READ UNIT 30 CARD NAME @9FOR032.RST
OPEN WRIT UNIT 31 CARD NAME @9FOR031.RST

DYNA VVER REST NSTEP 1000 TIME 0.0005 - 
     NPRINT 100   IPRFRQ  1000   - 
     INBFRQ 20   IHBFRQ 0 -   
     IUNREA 30  IUNWRI 31  IUNCRD -32  IUNVEL -33 - 
     KUNIT -34  IUNO -41 NSAVC 5  NSAVV 5  NSNOS 10 ISVFRQ 1000 

STOP
