* H2O+ETHANE RUN FOR NOSE-HOOVER DYNAMICS
* PART II (MULTI-THERMOBATH)
*

if ?NUMNODE .gt. 1 then
 echo "Test NOT performed."
 stop
endif
if ?olddyn .ne. 1 then
 echo "Test NOT performed."
 stop
endif

stream datadir.def

! Read topology file
READ RTF CARD
* TOPOLOGY FILE for water and ethane
*
   22    1
MASS  4 HT      1.0080 ! TIPS3P WATER HYDROGEN
MASS 14 ch3e   15.0350
MASS 58 OT     15.9994 ! TIPS3P WATER OXYGEN

RESI ETHA 0.0
GROUP
ATOM C1   CH3E  0.0
ATOM C2   CH3E  0.0
BOND C1 C2
PATCH FIRST NONE LAST NONE

RESI TIP3  0.000   ! TIPS3P WATER MODEL
GROUP
ATOM OH2  OT     -0.834
ATOM H1   HT      0.417
ATOM H2   HT      0.417
BOND OH2 H1  OH2 H2  H1 H2  ! THE LAST BOND IS NEEDED FOR SHAKE
THET H1 OH2 H2
PATC FIRS NONE LAST NONE

END

! Read parameter file
READ PARAM CARD
* parameters for water and ethane
*

BOND
CH3E CH3E 225. 1.54
HT   OT   450. 0.9572 ! from TIPS3P geometry
HT   HT     0. 1.5139 ! from TIPS3P geometry (for SHAKE w/PARAM)

THETA
HT   OT   HT    55.0    104.52    ! FROM TIPS3P GEOMETRY

NONB !Brooks Data
! according to a hint that charmm handles LJ-parameters slightly differently
CH3E 0. -0.207    2.12
HT   0.  0.0      0.0000
OT   0. -0.152073 1.77

END

!Generate solue 
READ SEQU CARD
* ethane  
*
1 
ETHA

GENERATE ETHA FIRST NONE LAST NONE SETUP WARN

!Solute coordinates
READ COOR CARD 
* coordinates for ethane
*
    2
    1    1 ETHA C1     0.00000   0.00000   0.00000
    2    1 ETHA C2     1.54000   0.00000   0.00000


! Set up water box
READ SEQUENCE TIP3 125
GENERATE WAT 

OPEN UNIT 3 READ CARD NAME @0tip125.crd
READ COORD CARD UNIT 3 APPEND
CLOSE UNIT 3

! Delete overlapping waters
DELETE ATOM SELECTION .BYRES. (SEGID WAT .AND. TYPE OH2 .AND. -
	( ( .NOT. SEGID WAT .AND. .NOT. HYDROGEN ) .AROUND. 2.8 ) ) -
        END

SET 6 15.517182
SET 7 15.517182
SET 8 15.517182
OPEN UNIT 8 READ CARD NAME @0cubic.img
READ IMAG UNIT 8
PRINT IMAGES TRAN

IMAGE BYRES XCEN 0.0 YCEN 0.0 ZCEN 0.0 SELE SEGID ETHA END
IMAGE BYRES XCEN 0.0 YCEN 0.0 ZCEN 0.0 SELE SEGID WAT  END

SHAKE BOND TOL 1.0D-9

UPDATE INBFRQ 5 IHBFRQ 0  -
       CUTIM 8.0 IMGFRQ 5 -
       ATOM SHIFT VATOM VSWITCH -
       CUTNB 8.0  CTOFNB 7.5   CTONNB 6.5  EPS 1.0 CDIE
       
NOSE 2
CALL 2 SELE RESNAME ETHA END
COEF 1 QREF 50.0 TREF 300.0
COEF 2 QREF 50.0 TREF 300.0
NCYC 5
END

OPEN WRITE UNIT 31 CARD NAME @9FOR015.RST

DYNA STRT NSTEP 100  TIME 0.001 NPRINT 10 IPRFRQ 100 -
     IUNREA -30 IUNWRI 31  IUNCRD -1  IUNVEL -1 FIRSTT 100.0 -
     KUNIT -41  NSAVC 50  NSAVV 50 ISVFRQ 1000

@testcheck ?tote -1130.97513 0.00001 nose2_1_tote
@testcheck ?ener -1348.72415 0.00001 nose2_1_ener
@testcheck ?elec -1021.09550 0.00001 nose2_1_elec
@testcheck ?vdw    144.31811 0.00001 nose2_1_vdw

NOSE 2
CALL 2 SELE RESNAME ETHA END
COEF 1 QREF 50.0 TREF 300.0
COEF 2 QREF 50.0 TREF 300.0
NCYC 5
END

OPEN WRITE UNIT 31 CARD NAME @9FOR016.RST
OPEN READ  UNIT 30 CARD NAME @9FOR015.RST

DYNA REST RSTN NSTEP 100  TIME 0.001 NPRINT 10 IPRFRQ 100 -
     IUNREA 30 IUNWRI 31  IUNCRD -1  IUNVEL  -1 -
     KUNIT -41  NSAVC 50  NSAVV 50 ISVFRQ 100 

@testcheck ?tote -1149.90155 0.00001 nose2_2_tote
@testcheck ?ener -1349.68463 0.00001 nose2_2_ener
@testcheck ?elec  -996.87688 0.00001 nose2_2_elec
@testcheck ?vdw    138.57191 0.00001 nose2_2_vdw

stop
