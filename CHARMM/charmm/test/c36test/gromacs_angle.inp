* CHARMM c36a5 test case gromacs_angle.inp
* Test gromacs angle potential and mixing CHARMM/GROMACS angle potentials
* Authors: Benjamin T. Miller, Michael G. Lerner
* January, 2011
*

read rtf card
* RTF for dummy system
*
   27 1

MASS    1 D      1.00000 D ! Dummy atom (CHARMM angles)
MASS    2 E      1.00000 E ! Dummy atom (GROMACS angles)

RESI DUC         0.00 ! dummy residue, CHARMM potential, min at 90
GROUP
ATOM D1   D      0.00
ATOM D2   D      0.00
ATOM D3   D      0.00
BOND D1 D2 D2 D3
ANGL D1 D2 D3

RESI DUG         0.00 ! dummy residue, GROMACS potential
GROUP
ATOM E1   E      0.00
ATOM E2   E      0.00
ATOM E3   E      0.00
BOND E1 E2 E2 E3
ANGL E1 E2 E3

END

read param card
* Parameters for dummy atoms
*

BOND
D       D             5.000000    2.000000
E       E             5.000000    2.000000

ANGLE
D       D       D             6.000000   90.000000
E       E       E             6.000000  -90.000000

DIHEDRAL
! we don't need no stinkin' dihedrals

NONBONDED NBXMOD 4 ATOM CDIEL SHIFT VATOM VDISTANCE VSHIFT -
CUTNB 16 CTOFNB 12 EPS 1.0 WMIN 1.5 E14FAC 0.7
!atom
D          0.0       0.8    4.700000
E          0.0       0.8    4.700000

END

read sequ card
* one CHARMM style angle, one Gromacs style
*
2
DUC DUG

generate tst setup first none last none

! this script varies the angle by 5 degrees between 0 and 180, checking
! the angle energies against what they are supposed to be ... also test
! first is run at each step to ensure correct forces.

! skip all energies except for the angles 
skip all excl angle

! set cosine of the minimum and set initial coordinates
calc thetamin = 3.1415962 / 2.0
calc cosmin   = cos( @thetamin )
scalar x set  0.0 select resname duc end
scalar y set  0.0 select resname duc end
scalar z set -5.0 select resname duc end
scalar x set  0.0 select resname dug end
scalar y set  0.0 select resname dug end
scalar z set  5.0 select resname dug end
scalar x set  2.0 select type d3 .or. type e3 end
scalar x set  2.0 select type d1 .or. type e1 end

! initial coordinates are 0.0 degrees (unphysical normally due to VDW
! replusion, but this just a test).
set angle = 0.0

! --- begin main loop
label mainloop

calc angr = ( @angle / 180.0 ) * ?pi
calc chme = 6.0 * abs( @angr - @thetamin )**2
calc grme = 3.0 * abs( cos( @angr ) - @cosmin )**2
calc tote = @chme + @grme

! rotate d1 and e1
calc newx = 2.0 * ( cos( @angr ) )
calc newy = 2.0 * ( sin( @angr ) )
scalar x set @newx select type d1 .or. type e1 end
scalar y set @newy select type d1 .or. type e1 end

update

! CHARMM energy from DUC
interaction select resname duc end
if ?angl .ae. @chme then echo TEST PASSED

! GROMACS energy from DUG
interaction select resname dug end
if ?angl .ae. @grme then echo TEST PASSED

! Put them both together and make sure that worked...
energy
if ?angl .ae. @tote then echo TEST PASSED

! now run test first to make sure that the forces are OK
! (second derivatives do not run with Gromacs-style angles)
test first step 0.0001

! --- check for exit condition and iterate
calc angle = @angle + 2.0
if @angle .le. 180.0 then goto mainloop

stop
