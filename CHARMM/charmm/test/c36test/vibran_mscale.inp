* CHARMM c36a1 Testcase vibran_mscale.inp
* Testing the 2nd derivatives for MSCALE 
* Requires vsys1.inp and vsys2.inp for each subsystem 
* Note: the subsystems can be run with either analytic or finite 
* difference 2nd derivatives (see mscale.doc and vsys1.inp) 
* Authors: Lee Woodcock, Milan Hodoscek
* July 2008
*

if ?mscale .ne. 1 then
 echo "mscale.inp> Test NOT performed."
 stop
endif

! Read in the standard test definitions
stream datadir.def

open read unit 1 card name @0top_all22_prot.inp
read rtf unit 1 card

open read unit 1 card name @0par_all22_prot.inp
read param unit 1 card

read sequ tip3 3

generate w setup noang nodihe

read  coor card
* 3 WATERs
*  DATE:     5/22/ 7     13:14: 7      CREATED BY USER: milan
*
    9
    1    1 TIP3 OH2    0.03112  -0.05455   0.00000 W    1      0.00000
    2    1 TIP3 H1     0.98695  -0.00338   0.00000 W    1      0.00000
    3    1 TIP3 H2    -0.25806   0.85792   0.00000 W    1      0.00000
    4    2 TIP3 OH2    0.03112  -0.05455   5.00000 W    2      0.00000
    5    2 TIP3 H1     0.98695  -0.00338   5.00000 W    2      0.00000
    6    2 TIP3 H2    -0.25806   0.85792   5.00000 W    2      0.00000
    7    3 TIP3 OH2    0.03112  -0.05455  10.00000 W    3      0.00000
    8    3 TIP3 H1     0.98695  -0.00338  10.00000 W    3      0.00000
    9    3 TIP3 H2    -0.25806   0.85792  10.00000 W    3      0.00000

fast off 

energy cdie cutnb 14.0 ctofnb 12.0 fshift vshift atom

vibran 
diag 
end 

! All that is happening here is that I am adding and subtracting the same piece 
! to make sure that all the machinery is working... if everything is correct 
! the final normal modes should match the original ones (from above) 
mscale nsubs 2

! for cmake builds use
! subs first coef 1.00 prog "../bin/charmm" -
subs first coef 1.00 prog "../exec/gnu_M/charmm" -
                   outp "@9/vsys1.out" inpu "@0/vsys1.str" -
                   sele resid 1 .or. resid 2 end

! for cmake builds use
! subs second coef -1.00 prog "../bin/charmm" -
subs second coef -1.00 prog "../exec/gnu_M/charmm" -
                   outp "@9/vsys2.out" inpu "@0/vsys2.str" -
                   sele resid 1 .or. resid 2 end
sysd

end

energy cdie cutnb 14.0 ctofnb 12.0 fshift vshift atom

vibran
diag 
end 

stop

