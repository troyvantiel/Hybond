* Generate a four-atomic benchmark system 
* and test OPTI command in VIBRAN 
* Find minimum energy   GK 2014
* 

! Set masses 
set massa 1
set massb 2
set massc 3

ioformat extended

set status pass 

read rtf card 
* topology for four-atomic benchmark
*
36  1

MASS   1 a     @massa ! first imaginary atom type
MASS   2 b     @massb ! second "
MASS   3 c     @massc ! second "

DEFA FIRS NONE LAST NONE

resi four 0.0
group
atom a1 c 0.
atom a2 a 0.
atom a3 a 0.
atom a4 b 0.
bond a1 a2  a2 a3  a3 a4
angl a1 a2 a3  a2 a3 a4
dihe a1 a2 a3 a4
ic a1 a2 a3 a4 0. 0. 0. 0. 0.
ic a4 a3 -x -y 0 0 0 0 0
ic +x a2 a3 a4 0 0 0 0 0
ic +x a1 a2 a3 0 0 0 0 0
ic +x +y a1 a2 0 0 0 0 0

END

read para card flex name
* parameters for toy compounds of Stefan Boresch' imagination
*

ATOMS
MASS   1 a     @massa ! first imaginary atom type
MASS   2 b     @massb ! second "
MASS   3 c     @massc ! second "

BONDS
a a 200. 2.0
a b 200. 2.0
a c 200. 2.0

ANGLES
a a a  50. 110.
a a b  50. 110.
a a c  50. 110.

dihe
a a a a  1. 3 180.00
a a a b  1. 3 180.00
c a a a  1. 3 180.00
c a a b  1. 3 180.00

NONBONDED nbxmod  5 atom cdiel fshift vatom vdistance vfswitch -
cutnb 14.0 ctofnb 12.0 ctonnb 10.0 eps 1.0 e14fac 1.0 wmin 1.5
* 0 0. 0.

END

read sequence four 1
generate four first none last none warn setup

ic para
ic seed 1 a1 1 a2 1 a3
ic build 

mini nrap nstep 10

energy 

set goodenergy = ?ener


coor init sele all end 

! Change bond lengths a little bit away from minimum
ic edit 
dist 1 a1 1 a2 2.10
dist 1 a3 1 a4 2.10 
angle 1 a1 1 a2 1 a3 111.0
angle 1 a2 1 a3 1 a4 111.0
end

ic seed 1 a1 1 a2 1 a3
ic build

energy

VIBRAN
 edit incl cbnd 1 A2 1 A1 orth ! fix first bond 
 edit incl cbnd 1 A3 1 A4 orth ! fix second bond 
 edit incl cang 1 a2 1 a3 1 a4 orth  ! fix first angle
 edit incl cang 1 a3 1 a2 1 a1 orth  ! fix secon angle
 RAYLEIGH SAVE
 OPTImize FORCE FACT 1.0 ENERGY 
END

energy

set newenergy = ?ener

calc error = @newenergy - @goodenergy


! If there is a residual error of more than 0.01 kcal/mol for this test case, 
! there is something wrong
if @error .gt. 0.01 set status fail 



coor init sele all end 

! Change bond lengths far away from minimum
ic edit 
dist 1 a1 1 a2 3.0
dist 1 a3 1 a4 1.0
angle 1 a1 1 a2 1 a3 160.0
angle 1 a2 1 a3 1 a4 60.0
end 

ic seed 1 a1 1 a2 1 a3
ic build

energy 

set badenergy = ?ener

VIBRAN
 edit incl cbnd 1 A2 1 A1 orth ! fix first bond 
 edit incl cbnd 1 A3 1 A4 orth ! fix second bond 
 edit incl cang 1 a2 1 a3 1 a4 orth  ! fix first angle
 edit incl cang 1 a3 1 a2 1 a1 orth  ! fix secon angle
 OPTImize FORCE FACT 1.0 ENERGY PARA
END

energy

set newenergy = ?ener

calc error = @newenergy - @goodenergy

! If there is a residual error of more than 0.01 kcal/mol for this test case, 
! there is something really wrong
if @error .gt. 0.01 set status fail 

echo vibran opti testcase result: @status

stop 
