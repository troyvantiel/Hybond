* initialize.str, last modified Oct/17/2001
* By Sonja M. Schwarzl & Stefan Fischer
* setup for calculation of scaling factors for charge scaling
* 

!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

BOMLev -1
! WRNLev -1
! PRNLev -1
FAST ON

CALC UCUTNB =  @UCTOFNB + 4.0 

!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
! g:  counter for group interactions
SET g  0 
     ! WMAIN has be set to the atomic radii for the calculation with PBEQ.
SCAL WMAIN = RADIUS
     ! After a call to SOLVE or ENPB the atomic radii are saved in a special 
     ! array.
     ! The atomic contribution to the electrostatic free energy are returned
     ! in WMAIN (destroying the radii).
     ! In every loop the radii will be recalled from SCA1 to WMAIN.
     ! For storing the dummy array SCA1 is used.
     ! SCAL WMAIN SHOW
SCAL SCA1 = WMAIN

! read in coordinates with global scaling factors for atoms in the target region
! in 4th column 
OPEN UNIT 3 READ CARD NAME @INPUTFAC
READ COOR CARD UNIT 3
CLOSE UNIT 3
 
     ! scale charges and store pre-scaled charges in array SCA7.
SCAL SCA7 = WMAIN  
SCAL SCA7 RECI
SCAL SCA7 PROD CHARGE

! store unscaled charges in SCA2
SCAL SCA2 = CHARGE

     ! All operations with charges will be performed with array CHARGE.

SCAL SCA3 SET 0.0 SELE ALL END ! used for intermediate storage of 
                               ! interaction energies in vacuum
SCAL SCA4 SET 1.0 SELE ALL END ! used for intermediate storage of 
                               ! interaction energies in solution
SCAL SCA6 SET 1.0 SELE ALL END ! storage vector for scaling factors
                               ! only entries for atoms outside the target
                               ! region are modified      
SCAL SCA9 SET 1.0 SELE ALL END ! intermediate storage of electrostatic 
                               ! potential

IF @PARAM EQ 22 STREam "defi22.str"
IF @PARAM EQ 19 STREam "defi19.str"


! calculate targeted scaling factors only for non-target region
! take scaled charges for target region using global scaling factors
STREam target.str


RETURN










