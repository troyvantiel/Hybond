* initialize.str, last modified May/12/2004
* By Sonja M. Schwarzl & Stefan Fischer
* setup for calculation of scaling factors for charge scaling
* 

!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx

BOMLev -1
WRNLev -1
PRNLev -1
FAST ON

CALC UCUTNB =  @UCTOFNB + 4.0 

!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
! g used for count group interaction 
SET g = 0 
     ! WMAIN has be set to the atomic radii for the calculation with PBEQ.
SCAL WMAIN = RADIUS
     ! After a call to SOLVE or ENPB the atomic radii are saved in a special 
     ! array.
     ! The atomic contribution to the electrostatic free energy are returned
     ! in WMAIN (destroying the radii).
     ! In every loop the radii will be recalled from SCA1 to WMAIN.
     ! For storing the dummy array SCA1 is used.
     ! SCAL WMAIN SHOW
SCAL SCA1 = WMAIN
     
     ! The array CHARGE is stored in dummy array SCA2.
SCAL SCA2 = CHARGE
     ! All operations with charges will be performed with array CHARGE.
     ! SCAL SCA2 SHOW

SCAL SCA3 SET 0.0 SELE ALL END ! used for intermediate storage of pairwise
                               ! interaction energies in vacuum
SCAL SCA4 SET 0.0 SELE ALL END ! used for intermediate storage of pairwise
                               ! interaction energies in solution
SCAL SCA5 SET 0.0 SELE ALL END ! used for intermediate storage of pairwise
                               ! scaling factors
SCAL SCA6 SET 1.0 SELE ALL END ! storage vector for scaling factors
SCAL SCA7 SET 0.0 SELE ALL END ! used for intermediate storage of pairwise
                               ! interaction energies in solution within
                               ! cutoff distance
SCAL SCA8 SET 0.0 SELE ALL END ! storage vector for E^solv,cut_I
SCAL SCA9 SET 1.0 SELE ALL END ! intermediate storage of electrostatic 
                               ! potential

IF @PARAM EQ 22 STREam "defi22.str"
IF @PARAM EQ 19 STREam "defi19.str"

     ! define selection that contains exactly one atom per target group I
DEFI groupatoms SELEct (.not. RESName PRO   .and. (TYPE CA .or. TYPE CB .or. TYPE OT2)) -
                         .or. (RESName PRO .and. (TYPE CA .or. TYPE OT2)) .or. -
                         (RESName TIP3 .and. TYPE OH2) .or. -
                         (RESName DIP3 .and. TYPE OH2) .or. uatoms end

! take care that the target region boundary is along group definitions.
STREam target.str

! Set all charges outside the targetregion to zero. Thereby,
! these atoms are not included in the calculation of scaling factors:
SCAL SCA2 SET 0.0 SELEct .not. targetregion END

RETURN
